      SUBROUTINE M01ZCF(IPERM,M1,M2,ICYCL,IFAIL)
C     MARK 12 RELEASE. NAG COPYRIGHT 1986.
C
C     M01ZCF DECOMPOSES A PERMUTATION INTO CYCLES, AS AN AID TO
C     RE-ORDERING RANKED DATA.
C
C     GIVEN A VECTOR IRANK WHICH SPECIFIES THE RANKS OF THE DATA
C     (AS GENERATED BY THE M01D- ROUTINES), M01ZCF GENERATES A NEW
C     VECTOR ICYCL, IN WHICH THE PERMUTATION IS REPRESENTED IN ITS
C     COMPONENT CYCLES, WITH THE FIRST ELEMENT IN EACH CYCLE
C     NEGATED. FOR EXAMPLE, THE PERMUTATION
C
C           5   7   4   2   1   6   3
C
C     IS COMPOSED OF THE CYCLES
C
C          (1   5) (2   7   3   4) (6)
C
C     AND THE VECTOR ICYCL GENERATED BY M01ZCF CONTAINS
C
C          -1   5  -2   7   3   4  -6
C
C     WRITTEN BY NAG CENTRAL OFFICE
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='M01ZCF')
C     .. Scalar Arguments ..
      INTEGER           IFAIL, M1, M2
C     .. Array Arguments ..
      INTEGER           ICYCL(M2), IPERM(M2)
C     .. Local Scalars ..
      INTEGER           I, IERR, J, K, L
C     .. Local Arrays ..
      CHARACTER*80      P01REC(2)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS
C     .. Executable Statements ..
C
C       CHECK THE PARAMETERS AND MODIFY IPERM
C
      IF (M2.LT.1 .OR. M1.LT.1 .OR. M1.GT.M2) THEN
         IERR = 1
         WRITE (P01REC,FMT=99999) M1, M2
      ELSE
         IERR = 0
         DO 20 I = M1, M2
            J = IPERM(I)
            IF ((J.LT.M1) .OR. (J.GT.M2)) GO TO 100
            IPERM(I) = -J
   20    CONTINUE
C
C        RECORD EACH NON-TRIVIAL CYCLE.
C
         L = M1
         DO 60 I = M1, M2
            K = -IPERM(I)
            IF (K.GE.0) THEN
               ICYCL(L) = -I
               J = I
   40          IPERM(J) = K
               L = L + 1
               J = K
               K = -IPERM(J)
               IF (K.GT.0) THEN
                  ICYCL(L) = J
                  GO TO 40
               END IF
               IF (J.NE.I) GO TO 120
            END IF
   60    CONTINUE
      END IF
C
C       RETURN
C
   80 IF (IERR.NE.0) THEN
         IFAIL = P01ABF(IFAIL,IERR,SRNAME,2,P01REC)
      ELSE
         IFAIL = 0
      END IF
      RETURN
  100 IERR = 2
      WRITE (P01REC(2),FMT=99997) I, J
      GO TO 140
  120 IERR = 3
      WRITE (P01REC(2),FMT=99996) J
  140 WRITE (P01REC(1),FMT=99998)
C
C     RESTORE IPERM
C
      DO 160 I = M1, M2
         IPERM(I) = ABS(IPERM(I))
  160 CONTINUE
      GO TO 80
C
99999 FORMAT (' ** On entry one or more of the following parameter val',
     *  'ues is illegal',/'    M1 =',I16,'  M2 =',I16)
99998 FORMAT (' ** IPERM(M1:M2) does not contain a permutation of the ',
     *  'integers M1 to M2')
99997 FORMAT ('    IPERM(',I6,') contains an out-of-range value',I16)
99996 FORMAT ('    IPERM contains a repeated value',I16)
      END
