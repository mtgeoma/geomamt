      SUBROUTINE X03ABF(A,ISIZEA,B,ISIZEB,N,ISTEPA,ISTEPB,CX,DX,SW,
     *                  IFAIL)
C     MARK 10 RE-ISSUE. NAG COPYRIGHT 1982
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     DOUBLE PRECISION COMPLEX BASE VERSION
C
C     CALCULATES THE VALUE OF A COMPLEX SCALAR PRODUCT USING BASIC
C     OR ADDITIONAL PRECISION AND ADDS IT TO A BASIC PRECISION
C     INITIAL VALUE.
C
C     FOR THIS DOUBLE PRECISION COMPLEX VERSION, ALL ADDITIONAL
C     (I.E. QUADRUPLE) PRECISION COMPUTATION IS PERFORMED BY THE
C     AUXILIARY ROUTINE X03ABY.  SEE THE COMMENTS AT THE HEAD OF
C     THAT ROUTINE CONCERNING IMPLEMENTATION.
C
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='X03ABF')
C     .. Scalar Arguments ..
      COMPLEX*16        CX, DX
      INTEGER           IFAIL, ISIZEA, ISIZEB, ISTEPA, ISTEPB, N
      LOGICAL           SW
C     .. Array Arguments ..
      COMPLEX*16        A(ISIZEA), B(ISIZEB)
C     .. Local Scalars ..
      COMPLEX*16        SUM
      INTEGER           I, IERR, IS, IT
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          X03ABY
C     .. Executable Statements ..
      IERR = 0
      IF (ISTEPA.LE.0 .OR. ISTEPB.LE.0) IERR = 1
      IF (ISIZEA.LT.(N-1)*ISTEPA+1 .OR. ISIZEB.LT.(N-1)*ISTEPB+1)
     *    IERR = 2
      IF (IERR.EQ.0) GO TO 20
      IFAIL = P01ABF(IFAIL,IERR,SRNAME,0,P01REC)
      RETURN
   20 IFAIL = 0
      IF (SW) GO TO 80
C
C     BASIC PRECISION CALCULATION
C
      SUM = CX
      IF (N.LT.1) GO TO 60
      IS = 1
      IT = 1
      DO 40 I = 1, N
         SUM = SUM + A(IS)*B(IT)
         IS = IS + ISTEPA
         IT = IT + ISTEPB
   40 CONTINUE
   60 DX = SUM
      RETURN
C
C     ADDITIONAL PRECISION COMPUTATION
C
   80 CALL X03ABY(A,ISIZEA,B,ISIZEB,N,ISTEPA,ISTEPB,CX,DX)
      RETURN
      END
