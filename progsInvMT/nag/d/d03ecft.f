      SUBROUTINE D03ECF(N1,N2,N3,N1M,N2M,A,B,C,D,E,F,G,Q,T,APARAM,ITMAX,
     *                  ITCOUN,ITUSED,NDIR,IXN,IYN,IZN,CONRES,CONCHN,
     *                  RESIDS,CHNGS,WRKSP1,WRKSP2,WRKSP3,WRKSP4,IFAIL)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     **************************************************************
C
C             DAVID A. H.  JACOBS              22/5/79
C     D03ECF OBTAINS THE SOLUTION OF A SYSTEM OF SIMULTANEOUS
C     ALGEBRAIC EQUATIONS OF SEVEN POINT MOLECULE FORM ON A
C     THREE-DIMENSIONAL TOPOLOGICALLY RECTANGULAR MESH.
C
C     IT IS THE DRIVING ROUTINE FOR ROUTINE D03UBF FOR
C     STANDARD APPLICATIONS
C
C     INPUTS
C     ------
C
C     N1       NUMBER OF NODES IN THE FIRST COORDINATE DIRECTION.
C
C     N2       NUMBER OF NODES IN THE SECOND COORDINATE DIRECTION.
C
C     N3       NUMBER OF NODES IN THE THIRD COORDINATE DIRECTION.
C
C     N1M      FIRST DIMENSION OF ALL THREE DIMENSIONAL ARRAYS.
C
C     N2M      SECOND DIMENSION OF ALL THREE DIMENSIONAL ARRAYS.
C
C     A,B,C,   ARRAYS ALL OF DIMENSION (N1M,N2M,N3) STORING
C     D,E,F,G  THE COEFFICIENTS OF THE ITERATIVE UPDATE
C              EQUATIONS -
C
C       A(I,J,K)*T(I,J,K-1)+B(I,J,K)*T(I,J-1,K)+C(I,J,K)*T(I-1,J,K)+
C       D(I,J,K)*T(I,J,K)+E(I,J,K)*T(I+1,J,K)+F(I,J,K)*T(I,J+1,K)+
C       G(I,J,K)*T(I,J,K+1)=Q(I,J,K)
C
C              WITH I=1,2,...,N1 , J=1,2,...,N2 AND K=1,2,...,N3
C              AND WHERE T(I,J,K) IS THE ARRAY WHOSE VALUES
C              ARE SOUGHT (AND WHICH OVERWRITES THE INITIAL
C              APPROXIMATION STORED ON INPUT IN THE ARRAY
C              T). ANY VALUES OF T OUTSIDE THE
C              (1-N1,1-N2,1-N3) ARRAY ARE TAKEN AS ZERO.
C     Q        IS THE ARRAY OF SOURCE TERMS DIMENSIONED
C              Q(N1M,N2M,N3).
C
C     T        ARRAY STORING THE APPROXIMATE SOLUTION OF T
C              IN THE ABOVE EQUATION WHICH IS PASSED TO THE
C              ROUTINE. IF NO BETTER APPROXIMATION IS
C              KNOWN, AN ARRAY OF ZEROS CAN BE USED.
C              DIMENSIONED T(N1M,N2M,N3) CHANGED ON RETURN
C              TO THE SOLUTION OBTAINED.
C
C     APARAM   IS AN ITERATION ACCELERATION PARAMETER FACTOR
C              TYPICALLY SET TO 1.0. IF CONVERGENCE IS
C              SLOW, IT CAN BE DECREASED. IF DIVERGENCE IS
C              OBTAINED, IT SHOULD BE INCREASED. IN EITHER
C              CASE IT MUST NOT GO OUTSIDE THE BOUNDS
C              PRESCRIBED.
C
C     ITMAX    IS THE MAXIMUM NUMBER OF ITERATIONS TO BE USED
C
C     ITCOUN   IS AN INDICATOR SET EQUAL TO 1 ON THE FIRST
C              CALL TO D03ECF FOR SUBSEQUENT CALLS FOR THE
C              SAME PROBLEM, I.E. SAME N1 N2 N3, BUT
C              POSSIBLY DIFFERENT COEFFICIENTS AND/OR
C              SOURCE TERMS,  AS OCCUR WITH NON-LINEAR
C              SYSTEMS OR TIME-DEPENDENT SYSTEMS, ITCOUN
C              SHOULD BE SET TO THE NUMBER OF ITERATIONS
C              USED SO FAR.  ON SUBSEQUENT CALLS TO ENSURE
C              SUITABLE CYCLING OF THE ITERATION
C              ACCELERATION PARAMETERS IN D03UBF. CHANGED ON
C              EXIT TO THE NUMBER OF ITERATIONS USED SO FAR
C              I.E. ITCOUN  ON RETURN  =  ITCOUN  ON INPUT
C              +  ITUSED  ON RETURN
C     NDIR     IS AN INTEGER SET TO A NON-ZERO VALUE FOR
C              SYSTEMS OF EQUATIONS WHICH HAVE A UNIQUE
C              SOLUTION. FOR SYSTEMS DERIVED FROM, FOR
C              EXAMPLE LAPACES EQUATION WITH ALL NEUMANN
C              BOUNDARY CONDITIONS,  PROBLEMS TO WHICH AN
C              ARBITRARY CONSTANT CAN BE ADDED TO THE
C              SOLUTION, NDIR SHOULD BE SET TO 0 (ZERO) AND
C              THE VALUES OF THE  NEXT THREE PARAMETERS
C              MUST BE SPECIFIED. FOR SUCH PROBLEMS THE
C              ROUTINE SUBTRACTS THE VALUE OF THE FUNCTION
C              DERIVED AT THE  NODE (IXN,IYN,IZN) FROM THE
C              WHOLE SOLUTION AFTER EACH ITERATION TO
C              REDUCE THE POSSIBILITY OF LARGE ROUNDING
C              ERRORS. THE  USER MUST ALSO ENSURE THAT FOR
C              SUCH PROBLEMS THE APPROPRIATE CONSISTENCY
C              CONDITION ON THE SOURCE TERMS IS SATISFIED.
C
C     IXN,IYN,IZN  NODAL INDECIES OF THE NODE AT WHICH THE
C              SOLUTION IS TO BE REDUCED TO ZERO FOR
C              PROBLEMS FOR WHICH NDIR IS SET TO 0 THE NODE
C              SHOULD NOT CORRESPOND TO A CORNER NODE OF
C              THE  REGION OF INTEREST.
C
C     CONVERGENCE CRITERIA
C
C     CONRES   CONVERGENCE CRITERION ON THE MAXIMUM ABSOLUTE
C              VALUE OF THE NORMALIZED RESIDUAL, THE LATTER
C              BEING DEFINED AS THE RESIDUAL OF THE
C              EQUATION DIVIDED BY THE CENTRAL COEFFICIENT
C              WHEN THE LATER IS NOT EQUAL TO 0.0, AND
C              DEFINED AS THE  RESIDUAL WHEN THE CENTRAL
C              COEFFICIENT IS 0.0.
C
C     CONCHN   CONVERGENCE CRITERION ON THE MAXIMUM ABSOLUTE
C              VALUE OF THE CHANGE MADE AT EACH ITERATION
C              TO THE ARRAY T.
C
C       CONVERGENCE IS ACHIEVED WHEN BOTH THE CRITERIA ARE
C       SATISFIED.
C
C     RESIDS   ARRAY OF DIMENSION  ITMAX ... SEE OUTPUT
C
C     CHNGS    ARRAY OF DIMENSION  ITMAX ... SEE OUTPUT
C
C     WRKSP1   IS A WORKSPACE ARRAY OF DIMENSION
C              (N1M,N2M,N3) USED BY D03UBF
C
C     WRKSP2    ... DITTO ...
C
C     WRKSP3    ... DITTO ...
C
C     WRKSP4   IS A WORKSPACE ARRAY OF DIMENSION
C              (N1M,N2M,N3) USED TO PASS THE RESIDUALS TO
C              D03UBF, AND IN WHICH THE UPDATE VALUES ARE
C              RETURNED FROM D03UBF TO THIS ROUTINE.
C     IFAIL    IS AN ERROR PARAMETER INDICATOR SET BY THE USER TO
C              INDICATE THE TYPE OF FAILURE IF AN ERROR IS
C              ENCOUNTERED.
C
C     ALL ABOVE PASSED AS ARGUMENTS
C
C     PROCESS
C     -------
C
C     SET ERROR PARAMETER
C     CHECK INPUT INTEGERS
C     COMMENCEMENT OF CALCULATIONAL PROCEDURE
C     START OF ITERATIVE SOLUTION PROCEDURE
C        SET CONVERGENCE PARAMETER ACCUMULATORS TO ZERO
C        CALCULATE THE RESIDUALS AND STORE IN ARRAY WRKSP4
C        CALL SUBROUTINE  D03UBF TO PERFORM ONE ITERATION OF S.I.P.
C         AND WHICH RETURNS THE ARRAY OF UPDATES IN THE ARRAY WRKSP4
C        COMBINE THE UPDATES WITH THE OLD SOLUTION
C        CHECK FOR CONVERGENCE
C     REPEAT TO CONVERGENCE OR UNTIL MAXIMUM NUMBER OF
C      ITERATIONS HAVE BEEN USED.
C     SET ERROR INDICATOR ACCORDINGLY.
C     RETURN
C
C     OUTPUTS
C     -------
C
C     T          ARRAY STORING THE SOLUTION TO THE SYSTEM OF
C                EQUATIONS PROVIDED.
C
C     ITUSED     NUMBER OF ITERATIONS ACTUALLY USED THIS CALL.
C
C     ITCOUN     ACCUMULATED NUMBER OF ITERATIONS USED
C                ITCOUN  ON OUTPUT  =  ITCOUN  ON INPUT  +
C                 ITUSED  ON OUTPUT
C
C     RESIDS     VECTOR STORING THE MAXIMUM ABSOLUTE RESIDUALS
C                OF EACH ITERATION,  DIMENSION  ITMAX.  ONLY
C                THE FIRST  ITUSED VALUES ARE SET ON RETURN.
C
C     CHNGS      ARRAY STORING THE MAXIMUM ABSOLUTE CHANGES OF
C                EACH ITERATION, DIMENSION   ITMAX.  ONLY THE
C                FIRST  ITUSED VALUES ARE SET ON RETURN.
C
C     IFAIL      ERROR INDICATOR
C          = 0  CORRECT RETURN
C          = 1  EITHER N1.LE.1 OR N2.LE.1 OR N3.LE.1
C          = 2  N1M IS LESS THAN N1 OR N2M IS LESS THAN N2
C          = 3  APARAM.LE.0.0
C          = 4  APARAM.GT.((N1-1)**2+(N2-1)**2+(N3-1)**2)/3.
C          = 5  CONVERGENCE NOT ACHIEVED. REFER TO VALUES
C               IN RESIDS AND CHNGS TO DETERMINE IF THE
C               ITERATION WAS CONVERGING. REFER TO GUIDE
C               ON SETTING APARAM IF CONVERGENCE IS VERY
C               SLOW.
C
C     ALL PASSED AS ARGUMENTS
C
C     ROUTINES USED
C     -------------
C
C     D03UBF    SINGLE ITERATION OF S.I.P. FOR A SEVEN POINT
C               MOLECULE SYSTEM.
C     P01AAF     ERROR HANDLING ROUTINE
C
C     **************************************************************
C
C
C     SET ERROR PARAMETER AND ITERATION COUNTER
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='D03ECF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  APARAM, CONCHN, CONRES
      INTEGER           IFAIL, ITCOUN, ITMAX, ITUSED, IXN, IYN, IZN, N1,
     *                  N1M, N2, N2M, N3, NDIR
C     .. Array Arguments ..
      DOUBLE PRECISION  A(N1M,N2M,N3), B(N1M,N2M,N3), C(N1M,N2M,N3),
     *                  CHNGS(ITMAX), D(N1M,N2M,N3), E(N1M,N2M,N3),
     *                  F(N1M,N2M,N3), G(N1M,N2M,N3), Q(N1M,N2M,N3),
     *                  RESIDS(ITMAX), T(N1M,N2M,N3),
     *                  WRKSP1(N1M,N2M,N3), WRKSP2(N1M,N2M,N3),
     *                  WRKSP3(N1M,N2M,N3), WRKSP4(N1M,N2M,N3)
C     .. Local Scalars ..
      DOUBLE PRECISION  DELMAX, RES, RESMAX, TNEUM
      INTEGER           I, IERROR, IFAIL1, ITNUM, J, K
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          D03UBF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, MAX
C     .. Executable Statements ..
      IERROR = 0
      ITUSED = 0
C
C     CHECK INPUT INTEGERS
C
      IF (N1.LE.1) GO TO 220
      IF (N2.LE.1) GO TO 220
      IF (N3.LE.1) GO TO 220
C
      IF (N1M.LT.N1) GO TO 240
      IF (N2M.LT.N2) GO TO 240
C
C     SET VALUE OF TNEUM FOR CASE WHEN NDIR.NE.0
C
      TNEUM = 0.0D0
C
C     START ITERATION LOOP
C
      DO 180 ITNUM = 1, ITMAX
C
C        CALCULATE THE RESIDUALS
C
         RESMAX = 0.0D0
         DO 100 K = 1, N3
            DO 80 J = 1, N2
               DO 60 I = 1, N1
                  IF (D(I,J,K).EQ.0.0D0) GO TO 20
C
C                   SEVEN POINT MOLECULE FORMULA
C
                  RES = Q(I,J,K) - D(I,J,K)*T(I,J,K)
                  IF (K-1.GE.1) RES = RES - A(I,J,K)*T(I,J,K-1)
                  IF (J-1.GE.1) RES = RES - B(I,J,K)*T(I,J-1,K)
                  IF (I-1.GE.1) RES = RES - C(I,J,K)*T(I-1,J,K)
                  IF (I+1.LE.N1) RES = RES - E(I,J,K)*T(I+1,J,K)
                  IF (J+1.LE.N2) RES = RES - F(I,J,K)*T(I,J+1,K)
                  IF (K+1.LE.N3) RES = RES - G(I,J,K)*T(I,J,K+1)
C
                  RESMAX = MAX(RESMAX,ABS(RES/D(I,J,K)))
                  WRKSP4(I,J,K) = RES
C
                  GO TO 40
   20             CONTINUE
C
C                   EXPLICIT EQUATION
C
                  WRKSP4(I,J,K) = Q(I,J,K) - T(I,J,K)
                  RESMAX = MAX(RESMAX,ABS(WRKSP4(I,J,K)))
C
   40             CONTINUE
   60          CONTINUE
   80       CONTINUE
  100    CONTINUE
C
C        INCREMENT THE ACCUMULATED ITERATION COUNTER
C
         ITCOUN = ITCOUN + 1
C
C
C        SET ERROR PARAMETER
C
         IFAIL1 = 1
C
         CALL D03UBF(N1,N2,N3,N1M,N2M,A,B,C,D,E,F,G,APARAM,ITCOUN,
     *               WRKSP4,WRKSP1,WRKSP2,WRKSP3,IFAIL1)
C
C        CHECK ERROR FLAG RETURN FROM D03UB
C
         IF (IFAIL1.NE.0) GO TO 280
C
C        SET ITUSED
C
         ITUSED = ITNUM
C
C        UPDATE THE DEPENDENT VARIABLE
C
C        SET TNEUM FOR CASE  NDIR=0
C
         IF (NDIR.EQ.0) TNEUM = T(IXN,IYN,IZN)
C
         DELMAX = 0.0D0
C
         DO 160 K = 1, N3
            DO 140 J = 1, N2
               DO 120 I = 1, N1
                  T(I,J,K) = T(I,J,K) + WRKSP4(I,J,K) - TNEUM
                  DELMAX = MAX(DELMAX,ABS(WRKSP4(I,J,K)))
  120          CONTINUE
  140       CONTINUE
  160    CONTINUE
C
         RESIDS(ITNUM) = RESMAX
         CHNGS(ITNUM) = DELMAX
C
         IF ((RESMAX.LT.CONRES) .AND. (DELMAX.LT.CONCHN)) GO TO 200
C
  180 CONTINUE
C
C     NO CONVERGENCE
C
      GO TO 260
C
C       CORRECT RETURN
C
  200 CONTINUE
      IFAIL = 0
      RETURN
C
C     ERROR RETURNS
C
  220 CONTINUE
C     N1 LE 1  OR  N12 LE 1  OR  N3 LE 1
      IERROR = 1
      GO TO 300
C
  240 CONTINUE
C     N1M LT N1  OR  N2M LT N2
      IERROR = 2
      GO TO 300
C
  260 CONTINUE
C     CONVERGENCE NOT ACHIEVED
      IERROR = 5
      GO TO 300
C
  280 CONTINUE
C     ERROR RETURN FROM D03UB
      IERROR = IFAIL1
C
  300 CONTINUE
C
C     ERROR CONDITION
C
      IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
C
      RETURN
C
      END
