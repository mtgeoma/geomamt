      SUBROUTINE D03EBF(N1,N2,N1M,A,B,C,D,E,Q,T,APARAM,ITMAX,ITCOUN,
     *                  ITUSED,NDIR,IXN,IYN,CONRES,CONCHN,RESIDS,CHNGS,
     *                  WRKSP1,WRKSP2,WRKSP3,IFAIL)
C     MARK 7 RELEASE. NAG COPYRIGHT 1978.
C     MARK 8C REVISED. IER-267 (OCT. 1980).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     *************************************************************
C     D03EBF OBTAINS THE SOLUTION TO A SYSTEM OF SIMULTANEOUS
C     ALGEBRAIC EQUATIONS OF FIVE POINT MOLECULE FORM ON A
C     TOPOLOGICALLY RECTANGULAR MESH.
C     IT IS THE DRIVING ROUTINE FOR D03UAF FOR STANDARD
C     APPLICATIONS.
C
C
C     INPUTS
C
C     N1      NUMBER OF NODES IN THE FIRST COORDINATE DIRECTION.
C     N2      NUMBER OF NODES IN THE SECOND COORDINATE DIRECTION.
C     N1M     FIRST DIMENSION OF ALL THE TWO-DIMENSIONAL ARRAYS.
C     A       ARRAY OF DIMENSION (N1M,N2) STORES THE COEFFICIENT
C             OF THE ITERATIVE UPDATE EQUATIONS AS SHOWN BELOW -
C     B       DITTO ... DIMENSION (N1M,N2) ...
C     C       DITTO ... DIMENSION (N1M,N2) ...
C     D       DITTO ... DIMENSION (N1M,N2) ...
C     E       DITTO ... DIMENSION (N1M,N2) ...
C
C        A(I,J)*T(I,J-1)+B(I,J)*T(I-1,J)+C(I,J)*T(I,J)+D(I,J)*
C        T(I+1,J)+E(I,J)*T(I,J+1) = Q(I,J)
C
C             WITH I=1,2,...,N1 AND J=1,2,...,N2 AND WHERE T(I,J)
C             IS THE ARRAY WHOSE VALUES ARE SOUGHT AND WHERE
C             Q(I,J) IS THE ARRAY OF SOURCE TERMS. ANY VALUES OF
C             T OUTSIDE THE PROBLEM AREA (1-N1,1-N2) ARE TAKEN
C             AS ZERO.
C     Q       IS THE ARRAY OF SOURCE TERMS DIMENSION (N1M,N2)
C     T       ARRAY STORING THE APPROXIMATE SOLUTION OF T IN THE
C             ABOVE EQUATION WHICH IS PASSED TO THE ROUTINE.
C             IF NO BETTER APPROXIMATION IS KNOWN, AN ARRAY OF
C             ZEROS CAN BE USED DIMENSIONED (N1M,N2).
C     APARAM  IS AN ITERATION ACCELERATION PARAMETER FACTOR
C             TYPICALLY SET TO 1.0. IF CONVERGENCE IS SLOW, IT
C             CAN BE DECREASED. IF DIVERGENCE IS OBTAINED, IT
C             SHOULD BE INCREASED. IN EITHER CASE IT MUST NOT
C             GO OUTSIDE THE BOUNDS PRESCRIBED (SEE IFAIL
C             PARAMETER FOR D03UAF).
C     ITMAX   IS THE MAXIMUM NUMBER OF ITERATIONS TO BE USED.
C     ITCOUN  IS AN INDICATOR SET EQUAL TO 0 ON THE FIRST CALL
C             TO D03EBF. FOR SUBSEQUENT CALLS FOR THE SAME
C             PROBLEM, I.E. SAME N1 N2,BUT POSSIBLY DIFFERENT
C             COEFFICIENTS AND/OR SOURCE TERMS, AS OCCUR WITH NON
C             LINEAR SYSTEMS OR TIME DEPENDENT SYSTEMS, ITCOUN
C             SHOULD BE SET TO THE NUMBER OF ITERATIONS USED SO
C             FAR ON SUBSEQUENT CALLS TO ENSURE SUITABLE CYCLING
C             OF THE ITERATION ACCELERATION PARAMETERS IN D03UAF.
C             CHANGED ON OUTPUT TO THE NUMBER OF ITERATIONS USED
C             SO FAR I.E. ITCOUN ON RETURN = ITCOUN ON INPUT +
C             ITUSED ON RETURN
C     NDIR    IS AN INTEGER SET TO A NON-ZERO VALUE FOR SYSTEMS
C             OF EQUATIONS WHICH HAVE A UNIQUE SOLUTION. FOR
C             SYSTEMS DERIVED FROM, E.G. LAPLACES EQUATION WITH
C             ALL NEUMANN BOUNDARY CONDITIONS, PROBLEMS WITH AN
C             ARBITRARY CONSTANT CAN BE ADDED TO THE SOLUTION,
C             NDIR SHOULD BE SET TO ZERO AND THE VALUES OF THE
C             NEXT 2 PARAMETERS MUST BE SPECIFIED. FOR SUCH
C             PROBLEMS THE ROUTINE SUBTRACTS THE VALUE OF THE
C             FUNCTION DERIVED AT THE NODE (IXN,IYN) FROM THE
C             WHOLE SOLUTION AFTER EACH ITERATION TO REDUCE THE
C             POSSIBLITY OF LARGE ROUNDING ERRORS. THE USER MU
C             ALSO ENSURE THAT FOR SUCH PROBLEMS THE APPROPRIATE
C             CONSISTENCY CONDITION ON THE SOURCE TERMS IS
C             SATISFIED.
C     IXN,IYN NODAL INDECIES OF THE NODE AT WHICH THE SOLUTION IS
C             TO BE REDUCED TO ZERO FOR PROBLEMS FOR WHICH NDIR IS
C             SET TO ZERO THE NODE SHOULD NOT CORRESPOND TO A
C             CORNER NODE OF THE REGION OF INTEREST.
C
C     CONVERGENCE CRITERIA
C
C     CONRES  CONVERGENCE CRITERION ON THE MAXIMUM ABSOLUTE VALUE
C             OF THE NORMALIZED RESIDUAL, THE LATTER BEING
C             DEFINED AS THE RESIDUAL OF THE EQUATION DIVIDED BY
C             THE CENTRAL COEFFICIENT WHEN THE LATTER IS NOT
C             EQUAL TO O.O, AND DEFINED AS THE RESIDUAL WHEN THE
C             CENTRAL COEFFICIENT IS 0.0.
C     CONCHN  CONVERGENCE CRITERION ON THE MAXIMUM ABSOLUTE VALUE
C             OF THE CHANGE MADE AT EACH ITERATION OF THE ARRAY T
C
C     CONVERGENCE IS ACHIEVED WHEN BOTH THE CRITERIA ARE SATISFIED
C
C     RESIDS  ARRAY OF DIMENSION ITMAX ... SEE OUTPUT
C     CHNGS   ARRAY OF DIMENSION ITMAX ... SEE OUTPUT
C
C     WRKSP1  WORKSPACE ARRAY OF DIMENSION (N1M,N2) USED BY D03UAF
C     WRKSP2   ... DITTO ...
C     WRKSP3  WORKSPACE ARRAY OF DIMENSION (N1M,N2) USED TO PASS
C             THE RESIDUALS TO D03UAF, AND IN WHICH THE UPDATE
C             VALUES ARE RETURNED FROM D03UAF TO THIS ROUTINE.
C
C     IFAIL   IS AN ERROR PARAMETER INDICATOR SET BY THE USER TO
C             INDICATE THE TYPE OF FAILURE IF AN ERROR IS
C             ENCOUNTERED.
C
C     PROCESS
C
C     SET ERROR PARAMETER
C     CHECK INPUT INTEGERS
C     COMMENCEMENT OF CALCULATIONAL PROCEDURE
C     START OF ITERATIVE SOLUTION PROCEDURE
C     SET CONVERGENCE PARAMETER ACCUMULATORS TO ZERO
C     CALCULATE THE RESIDUALS AND STORE IN ARRAY WRKSP3
C     CALL SUBROUTINE D03UAF TO PERFORM 1 ITERATION OF S.I.P
C     AND WHICH RETURNS THE ARRAY OF UPDATES IN THE ARRAY WRKSP3
C     COMBINE THE UPDATES WITH THE OLD SOLUTION
C     CHECK FOR CONVERGENCE
C     REPEAT TO CONVERGENCE OR UNTIL MAXIMUM NUMBER OF ITERATIONS
C     HAVE BEEN USED
C     SET ERROR INDICATOR ACCORDINGLY
C     RETURN
C
C     OUTPUTS
C
C     T       ARRAY STORING THE SOLUTION OF THE SYSTEM OF EQUATIONS
C             PROVIDED.
C     ITUSED  NUMBER OF ITERATIONS ACTUALLY USED THIS CALL.
C     ITCOUN  ACCUMULATED NUMBER OF ITERATIONS USED
C             ITCOUN ON OUTPUT = ITCOUN ON INPUT + ITUSED ON OUTPUT
C     RESIDS  VECTOR STORING THE MAXIMUM ABSOLUTE RESIDUALS OF EACH
C             ITERATION, DIMENSION ITMAX. ONLY THE FIRST ITUSED
C             VALUES ARE SET ON RETURN.
C     CHNGS   ARRAY STORING THE MAXIMUM ABSOLUTE CHANGES OF EACH
C             ITERATION, DIMENSION ITMAX. ONLY THE FIRST ITUSED
C             VALUES ARE SET ON RETURN.
C     IFAIL   ERROR INDICATOR
C             =0 CORRECT RETURN
C             =1 EITHER N1.LE.1 OR N2.LE.1
C             =2 CONVERGENCE NOT ACHIEVED. REFER TO VALUES IN
C                RESIDS AND CHNGS TO DETERMINE IF THE ITERATION
C                WAS CONVERGING. REFER TO GUIDE ON SETTING APARAM
C                IF CONVERGENCE IS VERY SLOW.
C             =3 APARAM.LE.0.0
C             =4 APARAM.GE.((N1-1)**2+(N2-1)**2)/2.0
C
C     ROUTINES USED
C
C     D03UAF  SINGLE ITERATION OF S.I.P. FOR A FIVE POINT MOLECULE
C             SYSTEM.
C     P01AAF  ERROR HANDLING ROUTINE.
C
C     **************************************************************
C
C
C     SET ERROR PARAMETER
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='D03EBF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  APARAM, CONCHN, CONRES
      INTEGER           IFAIL, ITCOUN, ITMAX, ITUSED, IXN, IYN, N1, N1M,
     *                  N2, NDIR
C     .. Array Arguments ..
      DOUBLE PRECISION  A(N1M,N2), B(N1M,N2), C(N1M,N2), CHNGS(ITMAX),
     *                  D(N1M,N2), E(N1M,N2), Q(N1M,N2), RESIDS(ITMAX),
     *                  T(N1M,N2), WRKSP1(N1M,N2), WRKSP2(N1M,N2),
     *                  WRKSP3(N1M,N2)
C     .. Local Scalars ..
      DOUBLE PRECISION  DELMAX, RES, RESMAX, TNEUM
      INTEGER           I, IERROR, IFAIL1, ITNUM, J
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          D03UAF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, MAX
C     .. Executable Statements ..
      IERROR = 0
      ITUSED = 0
C
C     CHECK INPUT INTEGERS
C
      IF (N1.LE.1) GO TO 180
      IF (N2.LE.1) GO TO 180
C
      IF (N1M.LT.N1) GO TO 200
C
C     SET VALUE OF TNEUM FOR CASE WHEN NDIR.NE.0
C
      TNEUM = 0.0D0
C
C     START ITERATION LOOP
C
      DO 140 ITNUM = 1, ITMAX
C
C        CALCULATE THE RESIDUALS
C
         RESMAX = 0.0D0
         DO 80 J = 1, N2
            DO 60 I = 1, N1
               IF (C(I,J).EQ.0.0D0) GO TO 20
C
C              FIVE POINT MOLECULE FORMULA
C
               RES = Q(I,J) - C(I,J)*T(I,J)
               IF (J-1.GE.1) RES = RES - A(I,J)*T(I,J-1)
               IF (I-1.GE.1) RES = RES - B(I,J)*T(I-1,J)
               IF (I+1.LE.N1) RES = RES - D(I,J)*T(I+1,J)
               IF (J+1.LE.N2) RES = RES - E(I,J)*T(I,J+1)
C
               RESMAX = MAX(RESMAX,ABS(RES/C(I,J)))
               WRKSP3(I,J) = RES
C
               GO TO 40
   20          CONTINUE
C
C              EXPLICIT EQUATION
C
               WRKSP3(I,J) = Q(I,J) - T(I,J)
               RESMAX = MAX(RESMAX,ABS(WRKSP3(I,J)))
C
   40          CONTINUE
   60       CONTINUE
   80    CONTINUE
C
C        INCREMENT ACCUMULATED ITERATION COUNTER
C
         ITCOUN = ITCOUN + 1
C
C        SET ERROR PARAMETER
C
C
         IFAIL1 = 1
C
         CALL D03UAF(N1,N2,N1M,A,B,C,D,E,APARAM,ITCOUN,WRKSP3,WRKSP1,
     *               WRKSP2,IFAIL1)
C
C        CHECK ERROR FLAG RETURN FROM D03UAF
C
         IF (IFAIL1.NE.0) GO TO 220
C
C        SET ITUSED
C
         ITUSED = ITNUM
C
C        UPDATE THE DEPENDENT VARIABLE
C
C        SET TNEUM FOR CASE  NDIR=0
C
         IF (NDIR.EQ.0) TNEUM = T(IXN,IYN)
C
         DELMAX = 0.0D0
C
         DO 120 J = 1, N2
            DO 100 I = 1, N1
               T(I,J) = T(I,J) + WRKSP3(I,J) - TNEUM
               DELMAX = MAX(DELMAX,ABS(WRKSP3(I,J)))
  100       CONTINUE
  120    CONTINUE
C
         RESIDS(ITNUM) = RESMAX
         CHNGS(ITNUM) = DELMAX
C
         IF ((RESMAX.LT.CONRES) .AND. (DELMAX.LT.CONCHN)) GO TO 160
C
  140 CONTINUE
C
C     NO CONVERGENCE
C
      GO TO 240
C
C     CORRECT RETURN
C
  160 CONTINUE
      IFAIL = 0
      RETURN
C
C     ERROR RETURNS
C
  180 CONTINUE
      IERROR = 1
      GO TO 260
C
  200 CONTINUE
      IERROR = 2
      GO TO 260
C
  220 CONTINUE
      IERROR = IFAIL1
      GO TO 260
C
  240 CONTINUE
      IERROR = 5
C
  260 CONTINUE
C
C     ERROR CONDITION
C
      IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
C
      RETURN
C
      END
