      SUBROUTINE D04AAF(XVAL,NDER,HBASE,DER,EREST,FUN,IFAIL)
C
C     ***   PURPOSE   ***
C
C     A SUBROUTINE FOR NUMERICAL DIFFERENTIATION AT A POINT.  IT
C     RETURNS A SET OF APPROXIMATIONS TO THE J-TH ORDER DERIVATIVE
C     (J=1,2,...14) OF FUN(X) EVALUATED AT X = XVAL  AND, FOR EACH
C     DERIVATIVE, AN ERROR ESTIMATE ( WHICH INCLUDES THE EFFECT OF
C     AMPLIFICATION OF ROUND- OFF ERRORS).
C
C
C     ***   INPUT  PARAMETERS   ***
C
C     (1) XVAL   REAL.  THE ABSCISSA AT WHICH THE SET OF
C     DERIVATIVES IS REQUIRED.
C     (2) NDER   INTEGER. THE HIGHEST ORDER DERIVATIVE REQUIRED.
C     IF(NDER.GT.0)  ALL DERIVATIVES UP TO MIN(NDER,14) ARE
C     CALCULATED.
C     IF(NDER.LT.0 AND NDER EVEN)  EVEN ORDER DERIVATIVES
C     UP TO MIN(-NDER,14) ARE CALCULATED.
C     IF(NDER.LT.0 AND NDER ODD )  ODD  ORDER DERIVATIVES
C     UP TO MIN(-NDER,13) ARE CALCULATED.
C     (3) HBASE  REAL.  A STEP LENGTH.
C     (6) FUN    THE NAME OF A REAL FUNCTION SUBPROGRAMME,
C     WHICH IS REQUIRED BY THE ROUTINE AS A SUBPROGRAMME
C     AND WHICH REPRESENTS THE FUNCTION BEING DIFFERENTIATED
C     THE ROUTINE REQUIRES 21 FUNCTION EVALUATIONS FUN(X)
C     LOCATED AT    X = XVAL    AND AT
C     X = XVAL + (2*J-1)*HBASE,  J=-9,-8, .... +9,+10.
C     THE FUNCTION VALUE AT  X = XVAL IS DISREGARDED WHEN
C     ONLY ODD ORDER DERIVATIVES ARE REQUIRED.
C
C     ***   OUTPUT PARAMETERS   ***
C
C     (4) DER(J)   J=1,2,...14.  REAL. A VECTOR OF
C     APPROXIMATIONS TO THE J-TH DERIVATIVE OF FUN(X)
C     EVALUATED AT X = XVAL.
C     (5) EREST(J) J=1,2,...14.  REAL. A VECTOR OF
C     ESTIMATES OF THE DABSOLUTE ACCURACY OF DER(J).  THESE
C     ARE NEGATIVE WHEN EREST(J).GT.ABS(DER(J)), OR WHEN,
C     FOR SOME OTHER REASON THE ROUTINE IS DOUBTFUL ABOUT
C     THE VALIDITY OF THE RESULT.
C
C     ***   IMPORTANT WARNING   ***
C
C     EREST IS AN ESTIMATE OF THE OVERALL ERROR.  IT IS ESSENTIAL
C     FOR
C     PROPER USE THAT THE USER CHECKS EACH VALUE OF DER
C     SUBSEQUENTLY
C     USED TO SEE THAT IT IS ACCURATE ENOUGH FOR HIS PURPOSES.
C     FAILURE TO DO THIS MAY RESULT IN THE CONTAMINATION OF ALL
C     SUBSEQUENT RESULTS.
C     IT IS TO BE EXPECTED THAT IN NEARLY ALL CASES  DER(14) WILL
C     BE
C     UNUSABLE.( 14 REPRESENTS A LIMIT IN WHICH FOR THE EASIEST
C     FUNCTION LIKELY TO BE ENCOUNTERED THIS ROUTINE MIGHT JUST
C     OBTAIN
C     AN APPROXIMATION OF THE CORRECT SIGN.)
C
C     ***   NOTE ON CALCULATION   ***
C
C     THE CALCULATION IS BASED ON THE EXTENDED T-TABLE (T SUB
C     K,P,S)
C     DESCRIBED IN LYNESS AND MOLER, NUM. MATH., 8, 458-464,(1966).
C     REFERENCES IN COMMENT CARDS TO TTAB(NK,NP,NS) REFER TO
C     T-TABLE
C     WITH NK=K+1,  NP=P+1,  NS=S+1.   SINCE ONLY PART OF THE
C     EXTENDED
C     T-TABLE IS NEEDED AT ONE TIME, THAT PART IS STORED IN
C     RTAB(10,7)
C     AND IS SUBSEQUENTLY OVERWRITTEN.  HERE
C     RTAB(NK,NP-NS+1)  =  TTAB(NK,NP,NS).
C
C     NAG COPYRIGHT 1976
C     MARK 5 RELEASE
C     MARK 6B REVISED  IER-116 (MAR 1978)
C     MARK 7 REVISED IER-139 (DEC 1978)
C     MARK 8 REVISED. IER-219 (MAR 1980)
C     MARK 8D REVISED. IER-271 (DEC 1980).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='D04AAF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  HBASE, XVAL
      INTEGER           IFAIL, NDER
C     .. Array Arguments ..
      DOUBLE PRECISION  DER(14), EREST(14)
C     .. Function Arguments ..
      DOUBLE PRECISION  FUN
      EXTERNAL          FUN
C     .. Local Scalars ..
      DOUBLE PRECISION  A, B, BIG, ERNOW, ERPREV, FACT, FTEST, FZ, H,
     *                  HFACT, HSQ, HTEST, ONE, RANMIN, SMALL, TEMP,
     *                  TERM, THRON2, TWO, XJNS, XMAX, XMIN, XNUM, ZERO
      INTEGER           J, JNS, N, NDERP, NDPAR, NEGER, NK, NKTOP, NLO,
     *                  NMAX, NMIN, NN1, NP, NPAR, NPMIN, NPR, NS,
     *                  NSMAX, NSQ, NTOP, NTOPM2, NUP
C     .. Local Arrays ..
      DOUBLE PRECISION  ACOF(10), RANGE(7), RTAB(10,7), TZEROM(10)
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  X02AKF, X02ALF
      INTEGER           P01ABF
      EXTERNAL          X02AKF, X02ALF, P01ABF
C     .. Data statements ..
      DATA              ZERO, ONE, TWO, THRON2/0.0D0, 1.0D0, 2.0D0,
     *                  1.5D0/
C     .. Executable Statements ..
C
C     TO ALTER THE PRECISION OF THE WHOLE ROUTINE,ALTER THE
C     PRECISION
C     IN THE ABOVE DECLARATION AND DATA STATEMENTS
C
C     QUIT ON ZERO HBASE AND ZERO NDER.  SET CONTROL PARAMETER
C     NDPAR.
C     NDPAR = +1  ODD-ORDER DERIVATIVES ONLY.
C     NDPAR =  0  ALL DERIVATIVES
C     NDPAR = -1  EVEN-ORDER DERIVATIVES ONLY.
      BIG = X02ALF()
      SMALL = 1000.0D0*X02AKF()
      IF (HBASE.EQ.ZERO) GO TO 20
      NDERP = NDER
      NDPAR = 0
      IF (NDER) 40, 20, 60
   20 IFAIL = P01ABF(IFAIL,1,SRNAME,0,P01REC)
      RETURN
   40 NDPAR = 4*(NDER/2) - 2*NDER - 1
      NDERP = -NDER
   60 CONTINUE
      IF (NDERP.GT.14) NDERP = 14
C
C     NEXT, EVALUATE 21 FUNCTION VALUES , SET ACOF(N), AND SET
C     FIRST
C     COLUMN OF RTAB FOR ODD ORDER DERIVATIVES AND STORE IN TZEROM
C     THE
C     FIRST COLUMN OF RTAB FOR EVEN ORDER DERIVATIVES.
      FZ = FUN(XVAL)
      DO 80 N = 1, 10
         NN1 = 2*N - 1
         NSQ = NN1*NN1
         ACOF(N) = NSQ
         XNUM = NN1
         H = HBASE*XNUM
         TEMP = FUN(XVAL+H)
         TERM = FUN(XVAL-H)
         RTAB(N,1) = (TEMP-TERM)/(TWO*H)
         TZEROM(N) = (TEMP-FZ-FZ+TERM)/(TWO*H**2)
   80 CONTINUE
C
C     SET UP FOR ODD-ORDER DERIVATIVES.
      IF (NDPAR.EQ.-1) GO TO 100
      NPAR = 1
      NSMAX = (NDERP+1)/2
      IF (NSMAX.LE.0) GO TO 440
      GO TO 140
C
C     SET UP FOR EVEN-ORDER DERIVATIVES.
  100 CONTINUE
      NPAR = -1
      IF (NDPAR.EQ.+1) GO TO 520
      NSMAX = NDERP/2
      IF (NSMAX.LE.0) GO TO 520
C
C     SET THE FIRST COLUMN OF THE T-TABLE FOR EVEN ORDER
C     DERIVATIVES.
      DO 120 J = 1, 10
         RTAB(J,1) = TZEROM(J)
  120 CONTINUE
  140 CONTINUE
C
C     ODD-ORDER AND EVEN ORDER PATHS JOIN UP HERE.
C
      NEGER = 0
      HSQ = HBASE*HBASE
      FACT = ONE
      HFACT = ONE
      DO 420 NS = 1, NSMAX
C
C        FROM HERE ON TO   STATEMENT 360 (NEAR END)  WE ARE DEALING
C        WITH A
C        SPECIFIED VALUE OF NS.
C
C        FOR EACH VALUE OF NP WE CALCULATE RANGE(NP)  WHICH IS THE
C        DIFFERE
C        BETWEEN THE GREATEST AND THE LEAST OF THE ELEMENTS
C        TTAB(NK,NP,NS)
C        AS WE GO ALONG WE DETERMINE ALSO THE MINIMUM OF RANGE(NP)
C        WHICH I
C        RANMIN = RANGE(NPMIN).
C        WE RETAIN NUP AND NLO WHICH ARE THE VALUES  OF NK GIVING THE
C        EXTREME VALUES OF TTAB(NK,NPMIN,NS).
C        THIS PART OF NS LOOP CONCLUDES AT STATEMENT NUMBER 280.
C
C
C        FIRST CALCULATE ELEMENTS OF T-TABLE  FOR CURRENT NS VALUE.
         NPMIN = NS
         IF (NS.EQ.1) NPMIN = 2
         DO 180 NP = NPMIN, 7
            NKTOP = 10 - NP + 1
            NPR = NP - NS + 1
            DO 160 NK = 1, NKTOP
               J = NP + NK - 1
               A = ACOF(J)
               B = ACOF(NK)
               TERM = ZERO
               TEMP = ZERO
               IF (NP.NE.NS) TEMP = A*RTAB(NK,NPR-1) -
     *                              B*RTAB(NK+1,NPR-1)
               IF (NS.NE.1) TERM = -RTAB(NK,NPR) + RTAB(NK+1,NPR)
               RTAB(NK,NPR) = (TERM+TEMP)/(A-B)
  160       CONTINUE
  180    CONTINUE
C
C        NOW CALCULATE NUP,NLO AND RANMIN.
         DO 280 NP = NS, 7
            NTOP = 11 - NP
            NPR = NP - NS + 1
            XMAX = RTAB(1,NPR)
            XMIN = RTAB(1,NPR)
            NMAX = 1
            NMIN = 1
C
            DO 200 NK = 2, NTOP
               TEMP = RTAB(NK,NPR)
               IF (TEMP.GT.XMAX) NMAX = NK
               IF (TEMP.GT.XMAX) XMAX = TEMP
               IF (TEMP.LT.XMIN) NMIN = NK
               IF (TEMP.LT.XMIN) XMIN = TEMP
  200       CONTINUE
C
            RANGE(NP) = XMAX - XMIN
            IF (NP.NE.NS) GO TO 220
            RANMIN = RANGE(NP)
            GO TO 240
  220       CONTINUE
            IF (RANGE(NP).GE.RANMIN) GO TO 260
            RANMIN = RANGE(NP)
  240       CONTINUE
            NPMIN = NP
            NUP = NMAX
            NLO = NMIN
            IF (NLO.EQ.NUP) NLO = NLO + 1
  260       CONTINUE
  280    CONTINUE
C
C        NEXT WE TAKE THE AVERAGE OF ALL EXCEPT THE EXTREME VALUES OF
C        TTAB(NK,NPMIN,NS) AS THE RESULT AND RANMIN AS THE ERROR
C        ESTIMATE.
         TERM = ZERO
         NTOP = 11 - NPMIN
         NTOPM2 = NTOP - 2
         XNUM = NTOPM2
         J = NPMIN - NS + 1
         DO 320 NK = 1, NTOP
            IF (NK.EQ.NUP) GO TO 300
            IF (NK.EQ.NLO) GO TO 300
            TERM = TERM + RTAB(NK,J)
  300       CONTINUE
  320    CONTINUE
         TERM = TERM/XNUM
C
C        THE ABOVE RESULT AND ERROR ESTIMATE REFER TO TAYLOR
C        COEFFICIENT.
C        NEXT WE DO SCALING TO OBTAIN DERIVATIVE RESULTS INSTEAD.
C        JNS AND XJNS ARE ACTUAL ORDER OF DERIVATIVE BEING TREATED.
C        SAFETY FACTORS 1.5,1.5,2.0,2.0 AND 2.0 ARE MULTIPLIED INTO
C        THE
C        ERROR ESTIMATES FOR J = 10,11,12,13 AND 14 RESPECTIVELY.
C        THESE
C        HAVE BEEN CHOSEN BY INSPECTION OF PERFORMANCE STATISTICS.
C        THERE
C        IS NO ANALYTIC JUSTIFICATION FOR THESE PARTICULAR FACTORS.
         JNS = 2*NS - 1
         IF (NPAR.LT.0) JNS = 2*NS
         XJNS = JNS
         FACT = FACT*XJNS
C        TEST FOR UNDERFLOW OF HFACT
         IF (HFACT.GE.1.0D0) GO TO 340
         HTEST = HFACT*BIG
         FTEST = TERM*FACT
         IF ((TWO*RANMIN*FACT).GT.FTEST) FTEST = TWO*RANMIN*FACT
         IF (HTEST.GT.FTEST) GO TO 340
         DER(JNS) = BIG
         EREST(JNS) = -DER(JNS)
         NEGER = NEGER + 1
         GO TO 360
C        END OF CODE TO HANDLE ZERO HFACT
  340    CONTINUE
         DER(JNS) = TERM*FACT/HFACT
         IF (JNS.EQ.10) RANMIN = THRON2*RANMIN
         IF (JNS.EQ.11) RANMIN = THRON2*RANMIN
         IF (JNS.GE.12) RANMIN = TWO*RANMIN
         EREST(JNS) = RANMIN*FACT/(HFACT)
C
C        SET SIGN OF EREST.  EREST NEGATIVE EITHER IF IT SWAMPS DER,
C        OR IF
C        TWO PREVIOUS CONSECUTIVE ERESTS OF THIS PARITY ARE NEGATIVE.
C        IT MAY ALSO BE SET NEGATIVE AT END (750).
         IF (NEGER.GE.2) EREST(JNS) = -EREST(JNS)
         IF (NEGER.GE.2) GO TO 360
         IF (TERM.LT.RANMIN) EREST(JNS) = -EREST(JNS)
         IF (-TERM.GE.RANMIN) EREST(JNS) = -EREST(JNS)
         IF (EREST(JNS).LT.ZERO) NEGER = NEGER + 1
         IF (EREST(JNS).GE.ZERO) NEGER = 0
  360    CONTINUE
C
C        SECOND TEST FOR UNDERFLOW OF HFACT
         IF (HFACT.GE.1.0D0) GO TO 380
         IF (HFACT.EQ.0.0D0) GO TO 400
         IF (HSQ.GE.SMALL/HFACT) GO TO 380
         HFACT = 0.0D0
         GO TO 400
  380    HFACT = HSQ*HFACT
  400    FACT = FACT*(XJNS+ONE)
  420 CONTINUE
C
  440 CONTINUE
      IF (NPAR.GT.0) GO TO 100
C
C     SET SIGN OF EREST NEGATIVE IF TWO PREVIOUS CONSECUTIVE ERESTS
C     ARE NEGATIVE.
      IF (NDPAR.NE.0) GO TO 520
      IF (NDERP.LE.2) GO TO 520
      NEGER = 0
      ERPREV = EREST(1)
      DO 500 J = 2, NDERP
         ERNOW = EREST(J)
         IF (NEGER.EQ.2) GO TO 460
         IF (ERPREV.GE.ZERO) GO TO 480
         IF (ERNOW.GE.ZERO) GO TO 480
  460    NEGER = 2
         IF (ERNOW.GT.ZERO) EREST(J) = -ERNOW
  480    ERPREV = ERNOW
  500 CONTINUE
C
  520 CONTINUE
      IFAIL = 0
      RETURN
C     END OF D04AAF   ***   ***   ***   ***   ***
      END
