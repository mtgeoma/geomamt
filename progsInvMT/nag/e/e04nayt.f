      SUBROUTINE E04NAY(NOCURV,POSDEF,RENEWR,UNITQ,QPHESS,N,NCOLR,
     *                  NCTOTL,NFREE,NQ,NROWH,NCOLH,NROWRT,NCOLRT,NHESS,
     *                  KFREE,CSLAST,SNLAST,DRMAX,EMAX,HSIZE,RDLAST,
     *                  HESS,RT,SCALE,ZY,HZ,WRK)
C     MARK 12 RE-ISSUE. NAG COPYRIGHT 1986.
C
C *********************************************************************
C     E04NAY  IS USED TO COMPUTE ELEMENTS OF THE  (NCOLR)-TH  COLUMN OF
C     R,  THE CHOLESKY FACTOR OF THE PROJECTED HESSIAN.  IF  RENEWR  IS
C     TRUE  ON ENTRY,  THE COMPLETE COLUMN IS TO BE COMPUTED.
C     OTHERWISE, ONLY THE LAST DIAGONAL ELEMENT IS REQUIRED.
C     IF THE RESULTING PROJECTED HESSIAN IS SINGULAR OR INDEFINITE, ITS
C     LAST DIAGONAL ELEMENT IS INCREASED BY AN AMOUNT  EMAX  THAT
C     ENSURES POSITIVE DEFINITENESS.  THIS DIAGONAL MODIFICATION WILL
C     ALTER THE SCALE OF THE QP SEARCH VECTOR  P, BUT NOT ITS
C     DIRECTION.
C
C     ON EXIT,  E04NAY WILL HAVE STORED THE  NCOLR  ELEMENTS OF THE NEW
C     COLUMN OF  R  IN THE ARRAY  RT,  AND SET THE VARIABLES  NOCURV,
C     POSDEF,  RENEWR,  DRMAX,  EMAX  AND  HSIZE.
C
C     SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C     ORIGINAL VERSION MARCH 1982.  REV. APRIL 1982.
C *********************************************************************
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  CSLAST, DRMAX, EMAX, HSIZE, RDLAST, SNLAST
      INTEGER           N, NCOLH, NCOLR, NCOLRT, NCTOTL, NFREE, NHESS,
     *                  NQ, NROWH, NROWRT
      LOGICAL           NOCURV, POSDEF, RENEWR, UNITQ
C     .. Array Arguments ..
      DOUBLE PRECISION  HESS(NROWH,NCOLH), HZ(N), RT(NROWRT,NCOLRT),
     *                  SCALE(NCTOTL), WRK(N), ZY(NQ,NQ)
      INTEGER           KFREE(N)
C     .. Subroutine Arguments ..
      EXTERNAL          QPHESS
C     .. Scalars in Common ..
      INTEGER           ISTART, MSG, NOUT
      LOGICAL           SCLDQP
C     .. Arrays in Common ..
      DOUBLE PRECISION  WMACH(15)
C     .. Local Scalars ..
      DOUBLE PRECISION  EPSMCH, ONE, RDSMAX, RDSMIN, RDSQ, RNORM, S,
     *                  TEN, TWO, ZERO, ZTHZ
      INTEGER           IDIAG, J, JTHCOL, K, NCOLR1
C     .. Local Arrays ..
      CHARACTER*60      REC(3)
C     .. External Functions ..
      DOUBLE PRECISION  DNRM2
      EXTERNAL          DNRM2
C     .. External Subroutines ..
      EXTERNAL          E04VDN, F04YAY, F06FBF, F06FCF, DCOPY, DSCAL,
     *                  X04BAF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, MAX, SQRT
C     .. Common blocks ..
      COMMON            /AE04VC/NOUT, MSG, ISTART
      COMMON            /AX02ZA/WMACH
      COMMON            /JE04VC/SCLDQP
C     .. Save statement ..
      SAVE              /AX02ZA/
C     .. Data statements ..
      DATA              ZERO, ONE, TWO, TEN/0.0D+0, 1.0D+0, 2.0D+0,
     *                  10.0D+0/
C     .. Executable Statements ..
      EPSMCH = WMACH(3)
C
      IF (RENEWR) GO TO 20
C
C ---------------------------------------------------------------------
C     ONLY THE LAST ELEMENT OF THE NEW COLUMN OF  R  NEED BE COMPUTED.
C     THIS SITUATION CAN ONLY OCCUR WHEN A CONSTRAINT IS ADDED TO THE
C     WORKING SET WITH  ZTHZ  NOT POSITIVE DEFINITE.
C ---------------------------------------------------------------------
C     THE LAST DIAGONAL ELEMENT OF  R  IS THAT OF  ZTHZ  PLUS A DIAGONAL
C     MODIFICATION.  THE SQUARE OF THE TRUE DIAGONAL IS RECOVERED FROM
C     THE ROTATIONS USED TO UPDATE  R  WHEN THE CONSTRAINT WAS ADDED TO
C     THE WORKING SET.
C
      RDLAST = RT(NCOLR,NCOLR)
      S = ABS(SNLAST)
      RDSQ = ((CSLAST-S)*RDLAST)*((CSLAST+S)*RDLAST)
      GO TO 120
C
C ---------------------------------------------------------------------
C     THE PROJECTED HESSIAN IS EXPANDED BY A ROW AND COLUMN.  COMPUTE
C     THE FIRST  (NCOLR - 1)  ELEMENTS OF THE NEW COLUMN OF THE
C     CHOLESKY FACTOR R.  ALSO, COMPUTE  RDSQ,  THE SQUARE OF THE LAST
C     DIAGONAL ELEMENT.
C ---------------------------------------------------------------------
   20 CALL F06FBF(N,ZERO,WRK,1)
      IF (UNITQ) GO TO 60
C
C     EXPAND THE NEW COLUMN OF Z IN TO AN N-VECTOR.
C
      DO 40 K = 1, NFREE
         J = KFREE(K)
         WRK(J) = ZY(K,NCOLR)
   40 CONTINUE
      IF (SCLDQP) CALL F06FCF(N,SCALE,1,WRK,1)
      JTHCOL = 0
      GO TO 80
C
C     ONLY BOUNDS ARE IN THE WORKING SET (NFREE  IS EQUAL TO  NCOLZ).
C     THE (NCOLR)-TH  COLUMN OF  Z  IS JUST A COLUMN OF THE IDENTITY
C     MATRIX.
C
   60 JTHCOL = KFREE(NCOLR)
      WRK(JTHCOL) = ONE
C
C     COMPUTE THE HESSIAN TIMES THE LAST COLUMN OF Z.
C
   80 CALL QPHESS(N,NROWH,NCOLH,JTHCOL,HESS,WRK,HZ)
      NHESS = NHESS + 1
C
      IF (UNITQ .AND. SCLDQP) CALL DSCAL(N,SCALE(JTHCOL),HZ,1)
      IF (SCLDQP) CALL F06FCF(N,SCALE,1,HZ,1)
C
C     COMPUTE THE  (NCOLR)-TH  COLUMN OF  Z(T)H Z.
C
      CALL E04VDN(4,N,NFREE,NCOLR,NFREE,NQ,UNITQ,KFREE,KFREE,HZ,ZY,WRK)
C
      CALL DCOPY(NCOLR,HZ,1,RT(1,NCOLR),1)
C
C     COMPUTE THE FIRST  (NCOLR - 1)  ELEMENTS OF THE LAST COLUMN OF  R.
C
      NCOLR1 = NCOLR - 1
      ZTHZ = RT(NCOLR,NCOLR)
      RDSQ = ZTHZ
      IF (NCOLR1.EQ.0) GO TO 100
      IDIAG = 1
      CALL F04YAY(-1,NCOLR1,RT,NROWRT,RT(1,NCOLR),IDIAG)
      RNORM = DNRM2(NCOLR1,RT(1,NCOLR),1)
C
C     COMPUTE THE SQUARE OF THE LAST DIAGONAL ELEMENT OF  R.
C
      RDSQ = ZTHZ - RNORM*RNORM
C
C     UPDATE THE ESTIMATE OF THE NORM OF THE HESSIAN.
C
  100 HSIZE = MAX(HSIZE,ABS(ZTHZ))
C
C ---------------------------------------------------------------------
C     COMPUTE  RDLAST, THE LAST DIAGONAL OF  R.  THE VARIABLES POSDEF
C     AND  NOCURV  ARE SET HERE.  THEY ARE USED TO INDICATE IF THE NEW
C     PROJECTED HESSIAN IS POSITIVE DEFINITE OR SINGULAR.  IF  POSDEF
C     IS SET TO FALSE,  RDLAST  WILL BE THAT OF  ZTHZ  PLUS A DIAGONAL
C     MODIFICATION. IF THE REQUIRED DIAGONAL MODIFICATION IS LARGE,
C     RENEWR  WILL BE SET TO BE  TRUE,  INDICATING THAT THE LAST ROW
C     AND COLUMN OF  R  MUST BE RECOMPUTED WHEN A CONSTRAINT IS ADDED
C     TO THE WORKING SET DURING THE NEXT ITERATION.
C ---------------------------------------------------------------------
  120 NOCURV = .FALSE.
      RENEWR = .FALSE.
      EMAX = ZERO
C
C     RDSMIN  IS THE SQUARE OF THE SMALLEST ALLOWABLE DIAGONAL ELEMENT
C     FOR A POSITIVE-DEFINITE CHOLESKY FACTOR.  NOTE THAT THE TEST FOR A
C     SINGULAR MATRIX IS SCALE DEPENDENT.
C
      IF (NCOLR.EQ.1) RDSMIN = EPSMCH*HSIZE
      IF (NCOLR.GT.1) RDSMIN = (EPSMCH*DRMAX)*DRMAX
      POSDEF = RDSQ .GT. RDSMIN
      IF (POSDEF) GO TO 180
C
      IF (RDSQ.LT.(-RDSMIN)) GO TO 140
C
C ---------------------------------------------------------------------
C     THE PROJECTED HESSIAN IS SINGULAR.
C ---------------------------------------------------------------------
C     THE QUADRATIC HAS NO CURVATURE ALONG AT LEAST ONE DIRECTION.  THE
C     PERTURBATION  EMAX  IS CHOSEN TO MAKE THE NEW EIGENVALUE OF  ZTHZ
C     SMALL AND POSITIVE.
C
      EMAX = RDSMIN - RDSQ
      RDSQ = RDSMIN
      NOCURV = .TRUE.
      GO TO 180
C
C ---------------------------------------------------------------------
C     THE PROJECTED HESSIAN IS INDEFINITE.  THERE ARE TWO CASES.
C ---------------------------------------------------------------------
C     CASE 1.  THE MODULUS OF THE NEW LAST DIAGONAL OF  R  IS NOT TOO
C     LARGE.  THE MODULUS OF  RDSQ  IS USED FOR THE SQUARE ROOT.
C
  140 RDSMAX = TEN*HSIZE
      IF (RDSQ.LT.(-RDSMAX)) GO TO 160
      EMAX = -TWO*RDSQ
      GO TO 180
C
C     CASE 2.  THE MODULUS OF THE LAST DIAGONAL OF  R  IS JUDGED TO BE
C     TOO LARGE (SOME LOSS OF PRECISION MAY HAVE OCCURRED).  SET
C     RENEWR  SO THAT THE LAST COLUMN IS RECOMPUTED LATER.
C
  160 EMAX = RDSMAX - RDSQ
      RENEWR = .TRUE.
      RDSQ = RDSMAX
C
C     COMPUTE THE LAST DIAGONAL ELEMENT.
C
  180 RDLAST = SQRT(ABS(RDSQ))
      RT(NCOLR,NCOLR) = RDLAST
C
      IF (MSG.GE.80 .AND. ( .NOT. POSDEF)) THEN
         WRITE (REC,FMT=99999) POSDEF, NOCURV, EMAX, RDLAST
         CALL X04BAF(NOUT,REC(1))
         CALL X04BAF(NOUT,REC(2))
         CALL X04BAF(NOUT,REC(3))
      END IF
C
      RETURN
C
C
C     END OF E04NAY (QPCOLR)
99999 FORMAT (/' //E04NAY//  POSDEF NOCURV          EMAX        RDLAST',
     *  /' //E04NAY//  ',L6,1X,L6,2(1P,D14.4))
      END
