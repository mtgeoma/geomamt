      SUBROUTINE E04KBR(N,LH,ALPHA,Z,HESD,HESL,IFAIL)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     **************************************************************
C
C     E04KBR (MODCHL) FORMS THE CHOLESKY FACTORIZATION OF THE MATRIX
C     LDLT + ALPHA*ZZT WHERE LT IS THE TRANSPOSE OF L, ZT THE
C     TRANSPOSE OF Z, ALPHA A SCALAR AND D A DIAGONAL MATRIX. THE
C     UNIT LOWER TRIANGULAR MATRIX L IS STORED BY ROWS IN THE ARRAY
C     HESL(I), I = 1(1)N(N-1)/2, WITH THE UNIT DIAGONAL OMITTED. THE
C     DIAGONAL OF THE DIAGONAL MATRIX D AND VECTOR Z ARE STORED IN
C     THE ARRAYS HESD(I) AND Z(I) I = 1(1)N, RESPECTIVELY.BOTH L AND
C     D ARE OVERWRITTEN WITH THE CORRESPONDING FACTORS OF THE
C     MODIFIED MATRICES, THE VALUES OF ALPHA AND Z ARE NOT RETAINED.
C     THE SUBROUTINE SETS INTEGER IFAIL = 1 IF ANY ELEMENT OF THE
C     DIAGONAL FORMED IS ZERO OR NEGATIVE (THIS CANNOT HAPPEN IF THE
C     ORIGINAL DIAGONALS ARE POSITIVE AND A RANK-ONE MATRIX IS BEING
C     ADDED (ALPHA .GT. 0)), OR THE RATIO OF THE NEW DIAGONAL
C     ELEMENT TO THE OLD IS TOO LARGE AND WOULD CAUSE OVERFLOW IF
C     CALCULATED. THE INTEGER IFAIL = 0 FOR A NORMAL EXIT. THIS
C     ROUTINE IS RECOMMENDED IF ALPHA .GT. 0 AND E04JBY (NMDCHL) IS
C     RECOMMENDED IF IT IS REQUIRED TO SUBTRACT A RANK-ONE MATRIX
C     I.E. ALPHA .LT. 0.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, SHEDMOND R.
C     GRAHAM AND MARGARET H. WRIGHT, D.N.A.C. NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     IMPLEMENTORS SHOULD CONSIDER PUTTING THE BODY OF THIS ROUTINE
C     INTO MACHINE CODE.
C
C     **************************************************************
C
C
C     RMAX IS THE LARGEST POSITIVE REAL NUMBER SUCH THAT RMAX AND -
C     RMAX CAN BE HELD IN THE MACHINE.
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  ALPHA
      INTEGER           IFAIL, LH, N
C     .. Array Arguments ..
      DOUBLE PRECISION  HESD(N), HESL(LH), Z(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  A, BETA, DB, DI, GAMMA, P1, RMAX, T, W
      INTEGER           I, IB, IP1, J, K
C     .. External Functions ..
      DOUBLE PRECISION  X02ALF
      EXTERNAL          X02ALF
C     .. Executable Statements ..
      RMAX = X02ALF()
C
      IFAIL = 0
      A = ALPHA
      K = 0
      DO 100 I = 1, N
C
C        USING METHOD C1 ALGORITHM (SEE GILL, GOLUB, MURRAY AND
C        SAUNDERS NAC REPORT 29)
C
         P1 = Z(I)
         DI = HESD(I)
         T = A*P1
         DB = DI + T*P1
         HESD(I) = DB
C
C        EXIT WITH IFAIL = 1 IF ANY OF THE NEW DIAGONAL ELEMENT IS ZERO
C        OR NEGATIVE, OR IF THE RATIO OF DIAGONALS WILL OVERFLOW.
C
         IF (DB.GE.1.0D+0) GO TO 20
         IF (DB.GT.0.0D+0 .AND. DI.LE.RMAX*DB) GO TO 20
         IFAIL = 1
         GO TO 120
   20    GAMMA = DI/DB
         BETA = T/DB
         A = A*GAMMA
         K = K + I
         J = K
         IF (I.EQ.N) GO TO 100
         IP1 = I + 1
         IF (GAMMA.GE.0.25D+0) GO TO 60
         DO 40 IB = IP1, N
            T = HESL(J)
            HESL(J) = T*GAMMA + BETA*Z(IB)
            Z(IB) = Z(IB) - P1*T
            J = J + IB - 1
   40    CONTINUE
         GO TO 100
   60    CONTINUE
C
C        USE ALTERNATIVE UPDATE OF L IF RATIO OF DIAGONALS .GT. 4.
C
         DO 80 IB = IP1, N
            T = HESL(J)
            W = Z(IB) - P1*T
            Z(IB) = W
            HESL(J) = BETA*W + T
            J = J + IB - 1
   80    CONTINUE
  100 CONTINUE
  120 CONTINUE
      RETURN
C
C     END OF E04KBR (MODCHL)
C
      END
