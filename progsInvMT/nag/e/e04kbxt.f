      SUBROUTINE E04KBX(N,NFREE,TOL,ISTATE,G,LM1POS,INEGLM,NUMNEG)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     **************************************************************
C
C     E04KBX (LM1CHK) COMPUTES THE FIRST-ORDER LAGRANGE MULTIPLIER
C     FOR EACH FIXED VARIABLE X(ILM). IF THIS IS NOT INDISPUTABLY
C     POSITIVE, IT UPDATES ISTATE(ILM) FROM - 1 TO - 4 OR FROM - 2
C     TO - 5. WHEN ALL MULTIPLIERS HAVE BEEN COMPUTED, INEGLM = 0
C     ONLY IF THE SET OF SIGNIFICANTLY NEGATIVE MULTIPLIERS IS
C     EMPTY. IF IT IS NOT, RNEGLM WILL CONTAIN THE ACTUAL VALUE AND
C     INEGLM THE INDEX OF THE MOST NEGATIVE MULTIPLIER. THEN E04LBT
C     WILL CHECK THE RELATIVE SIZE OF RNEGLM AND, IF THIS IS TOO
C     SMALL, RESET INEGLM TO ZERO TO INDICATE THAT NO VARIABLE
C     SHOULD BE RELEASED. IN THAT CASE E04KBX RETURNS TO THE CALLING
C     ROUTINE WITH THE - 4 AND - 5 ENTRIES STILL IN ISTATE AND THE
C     NUMBER OF SUCH ENTRIES IN NUMNEG. OTHERWISE E04LBU RESTORES
C     THE ENTRIES TO THEIR NORMAL VALUES AND NUMNEG TO ZERO.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  TOL
      INTEGER           INEGLM, N, NFREE, NUMNEG
      LOGICAL           LM1POS
C     .. Array Arguments ..
      DOUBLE PRECISION  G(N)
      INTEGER           ISTATE(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  BNDSGN, RLM1, RNEGLM
      INTEGER           ILM, ISI
C     .. External Subroutines ..
      EXTERNAL          E04LBT, E04LBU
C     .. Executable Statements ..
      RNEGLM = -TOL
      INEGLM = 0
      NUMNEG = 0
      DO 20 ILM = 1, N
         ISI = ISTATE(ILM)
         IF (ISI.GE.0 .OR. ISI.EQ.-3) GO TO 20
         BNDSGN = 1.0D+0
         IF (ISI.EQ.-1) BNDSGN = -1.0D+0
         RLM1 = BNDSGN*G(ILM)
         IF (RLM1.GT.TOL) GO TO 20
         NUMNEG = NUMNEG + 1
         ISTATE(ILM) = ISI - 3
         IF (RLM1.GE.RNEGLM) GO TO 20
         RNEGLM = RLM1
         INEGLM = ILM
   20 CONTINUE
      LM1POS = NUMNEG .EQ. 0
      IF (INEGLM.GT.0) CALL E04LBT(N,NFREE,TOL,ISTATE,G,RNEGLM,INEGLM)
      IF (INEGLM.GT.0) CALL E04LBU(N,NUMNEG,ISTATE)
      RETURN
C
C     END OF E04KBX (LM1CHK)
C
      END
