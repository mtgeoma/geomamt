      SUBROUTINE E04LBJ(N,NFREE,TOL,LSFAIL,TIGHT,ISTATE,G,P,HESL,LH,
     *                  LM2POS,INEGLM,NUMNEG)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 8 REVISED. IER-238 (APR 1980).
C     MARK 8A REVISED. IER-259 (AUG 1980)
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     **************************************************************
C
C     E04LBJ (LM2CHK) COMPUTES THE FIRST-ORDER MULTIPLIER RLM1 FOR
C     EACH FIXED VARIABLE X(ILM). IF THIS IS NEGATIVE, IT COMPUTES
C     THE SECOND- ORDER MULTIPLIER RLM2, USING E04LBK TO CALCULATE
C     THE ADDITIONAL TERM IF IT IS STILL POSSIBLE TO CONTINUE
C     MINIMIZING ON THE CURRENT SUBSPACE BUT OTHERWISE SETTING RLM2
C     = RLM1. IF RLM2 IS NEGATIVE AND SUFFICIENTLY CLOSE TO RLM1, IT
C     UPDATES ISTATE(ILM) FROM - 1 TO - 4 OR FROM - 2 TO - 5. WHEN
C     ALL MULTIPLIERS HAVE BEEN COMPUTED, RNEGLM CONTAINS THE ACTUAL
C     VALUE AND INEGLM THE INDEX OF THE MOST NEGATIVE MULTIPLIER. IF
C     INEGLM = 0, THAT IS, IF NO MULTIPLIER IS SIGNIFICANTLY
C     NEGATIVE, LM2POS IS SET TO .TRUE. OTHERWISE, E04LBT CHECKS THE
C     RELATIVE SIZE OF RNEGLM. IF THIS IS TOO SMALL, IT RESETS
C     INEGLM TO ZERO TO INDICATE THAT NO VARIABLE SHOULD IMMEDIATELY
C     BE RELEASED. IF, IN ADDITION, THE TIGHTER CONVERGENCE CRITERIA
C     ARE SATISFIED OR IT IS NO LONGER POSSIBLE TO CONTINUE
C     MINIMIZING ON THE CURRENT SUBSPACE, E04LBJ RETURNS TO THE
C     CALLING ROUTINE WITH THE - 4 AND - 5 ENTRIES STILL IN ISTATE
C     AND THE NUMBER OF SUCH ENTRIES IN NUMNEG. OTHERWISE E04LBU
C     RESTORES THE ENTRIES TO THEIR NORMAL VALUES AND NUMNEG TO
C     ZERO.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  TOL
      INTEGER           INEGLM, LH, N, NFREE, NUMNEG
      LOGICAL           LM2POS, LSFAIL, TIGHT
C     .. Array Arguments ..
      DOUBLE PRECISION  G(N), HESL(LH), P(N)
      INTEGER           ISTATE(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  BNDSGN, RLM1, RLM2, RNEGLM, SUM
      INTEGER           ILM, ISI
C     .. External Subroutines ..
      EXTERNAL          E04LBK, E04LBT, E04LBU
C     .. Intrinsic Functions ..
      INTRINSIC         ABS
C     .. Executable Statements ..
      RNEGLM = -TOL
      INEGLM = 0
      NUMNEG = 0
      DO 40 ILM = 1, N
         ISI = ISTATE(ILM)
         IF (ISI.GT.0 .OR. ISI.EQ.-3) GO TO 40
         BNDSGN = 1.0D+0
         IF (ISI.EQ.-1) BNDSGN = -1.0D+0
         RLM1 = BNDSGN*G(ILM)
         IF (RLM1.GT.TOL) GO TO 40
         SUM = 0.0D+0
         IF ( .NOT. LSFAIL .AND. NFREE.GT.0 .AND. .NOT. TIGHT)
     *       CALL E04LBK(N,ILM,SUM,BNDSGN,ISTATE,HESL,LH,P)
         RLM2 = RLM1 + SUM
         IF (RLM2.GT.TOL) GO TO 40
         IF (LSFAIL .OR. NFREE.EQ.0 .OR. TIGHT) GO TO 20
         SUM = 2.0D+0*ABS(SUM)
         IF (RLM1+SUM.GE.0.0D+0 .OR. RLM2+SUM.GE.0.0D+0) GO TO 40
   20    NUMNEG = NUMNEG + 1
         ISTATE(ILM) = ISI - 3
         IF (RLM2.GE.RNEGLM) GO TO 40
         RNEGLM = RLM2
         INEGLM = ILM
   40 CONTINUE
      LM2POS = NUMNEG .EQ. 0
      IF (INEGLM.GT.0) CALL E04LBT(N,NFREE,TOL,ISTATE,G,RNEGLM,INEGLM)
      IF (INEGLM.GT.0 .OR. ( .NOT. TIGHT .AND. .NOT. LSFAIL .AND.
     *    NFREE.GT.0)) CALL E04LBU(N,NUMNEG,ISTATE)
      RETURN
C
C     END OF E04LBJ (LM2CHK)
C
      END
