      SUBROUTINE E04JBU(N,NFRNEW,JFIX,HESL,LH,HESD,LHPROJ,W,IFLAG)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     **************************************************************
C
C     WHEN THE VARIABLE INDEXED BY JFIX IN THE FORMER PERMUTATION
C     OF FREE VARIABLES HAS BEEN FIXED ON A BOUND, E04JBU (MODHPJ)
C     MODIFIES THE CHOLESKY FACTORS OF THE PROJECTED HESSIAN. WHEN
C     JFIX WAS NOT LAST IN THE OLD PERMUTATION, THE AUXILIARY
C     ROUTINE E04KBR PERFORMS A RANK-ONE UPDATE ON THE EXISTING
C     FACTORS.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     .. Scalar Arguments ..
      INTEGER           IFLAG, JFIX, LH, LHPROJ, N, NFRNEW
C     .. Array Arguments ..
      DOUBLE PRECISION  HESD(N), HESL(LH), W(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  DFIX
      INTEGER           IHL, IMINUS, J, JHL, JMINUS, K, KHL, NFROLD
C     .. External Subroutines ..
      EXTERNAL          E04KBR
C     .. Executable Statements ..
      IFLAG = 0
C
C     CALCULATE THE LENGTH OF THE MODIFIED HESL.
C
      NFROLD = NFRNEW + 1
      LHPROJ = 1
      IF (NFRNEW.EQ.0) GO TO 160
      IF (NFRNEW.GE.3) LHPROJ = NFRNEW*(NFRNEW-1)/2
      IF (JFIX.EQ.NFROLD) GO TO 160
C
C     PREPARE THE VECTOR W AND THE SCALAR DFIX FOR THE RANK-ONE
C     UPDATE AND PACK THE REMAINING ELEMENTS OF HESD.
C
      IMINUS = JFIX - 1
      IF (JFIX.EQ.1) GO TO 40
      DO 20 J = 1, IMINUS
         W(J) = 0.0D+0
   20 CONTINUE
   40 DFIX = HESD(JFIX)
      JHL = JFIX*(JFIX+1)/2
      DO 60 J = JFIX, NFRNEW
         HESD(J) = HESD(J+1)
         W(J) = HESL(JHL)
         JHL = JHL + J
   60 CONTINUE
C
C     PACK THE RELEVANT ELEMENTS OF HESL IN PREPARATION FOR THE
C     RANK-ONE UPDATE.
C
      KHL = JFIX*IMINUS/2
      JHL = KHL - IMINUS
      DO 140 J = JFIX, NFRNEW
         IF (JFIX.EQ.1) GO TO 100
         DO 80 K = 1, IMINUS
            JHL = JHL + 1
            KHL = KHL + 1
            HESL(JHL) = HESL(KHL)
   80    CONTINUE
  100    KHL = KHL + 1
         IF (J.EQ.JFIX) GO TO 140
         JMINUS = J - 1
         DO 120 K = JFIX, JMINUS
            JHL = JHL + 1
            KHL = KHL + 1
            HESL(JHL) = HESL(KHL)
  120    CONTINUE
  140 CONTINUE
C
C     PERFORM THE RANK-ONE UPDATE.
C
      CALL E04KBR(NFRNEW,LHPROJ,DFIX,W,HESD,HESL,IFLAG)
      IF (IFLAG.EQ.1) RETURN
C
C     SET THE REDUNDANT ENTRIES IN HESD AND HESL TO ZERO.
C
  160 HESD(NFROLD) = 0.0D+0
      IF (NFRNEW.EQ.0) RETURN
      JHL = 1
      IF (NFRNEW.GE.2) JHL = LHPROJ + 1
      KHL = NFROLD*NFRNEW/2
      DO 180 IHL = JHL, KHL
         HESL(IHL) = 0.0D+0
  180 CONTINUE
      RETURN
C
C     END OF E04JBU (MODHPJ)
C
      END
