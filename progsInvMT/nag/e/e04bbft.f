      SUBROUTINE E04BBF(FUN,EPS,T,A,B,MAXCAL,X,F,G,IFAIL)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 8D REVISED. IER-272 (DEC 1980).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     **************************************************************
C
C     E04BBF ATTEMPTS TO FIND A MINIMUM IN AN INTERVAL A .LE. X .LE.
C     B OF A FUNCTION F(X) OF THE SCALAR X, USING FUNCTION AND
C     GRADIENT VALUES.
C
C     IT IS BASED ON THE SUBROUTINE UNIGRD IN THE NPL ALGORITHMS
C     LIBRARY (REF. NO. E4/14/F). THE FUNCTION F(X) IS DEFINED BY
C     THE USER-SUPPLIED SUBROUTINE FUN. T AND EPS DEFINE A TOLERANCE
C     TOL = EPS * ABS(X) + T, AND FUN IS NEVER EVALUATED AT TWO
C     POINTS CLOSER THAN TOL. IF FUN IS DELTA-UNIMODAL, FOR SOME
C     DELTA LESS THAN TOL, THEN X APPROXIMATES THE GLOBAL MINIMUM OF
C     FUN WITH AN ERROR LESS THAN 3*TOL. IF FUN IS NOT DELTA-
C     UNIMODAL ON (A, B), THEN X MAY APPROXIMATE A LOCAL, BUT NON
C     GLOBAL, MINIMUM. EPS SHOULD BE NO SMALLER THAN 2*EPSMCH, AND
C     PREFERABLY NOT MUCH LESS THAN SQRT(EPSMCH), WHERE EPSMCH IS
C     THE RELATIVE MACHINE PRECISION. T SHOULD BE POSITIVE. NOTE
C     THAT, FOR CONSISTENCY WITH OTHER E04 DOCUMENTATION, THE NAME
C     FUNCT IS USED INSTEAD OF FUN IN THE WRITE-UP.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, HAZEL M.
C     BARBER AND MARGARET H. WRIGHT, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     FUN
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='E04BBF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  A, B, EPS, F, G, T, X
      INTEGER           IFAIL, MAXCAL
C     .. Subroutine Arguments ..
      EXTERNAL          FUN
C     .. Local Scalars ..
      DOUBLE PRECISION  B1, D, E, EPSMCH, FU, FW, GTEST1, GTEST2, GU,
     *                  GW, OLDF, R, RR, RTEPS, SCXBD, SS, TOL, U,
     *                  XLAMDA, XW
      INTEGER           IFLAG, ILOC, ISAVE, NUMF
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  X02AJF
      INTEGER           P01ABF
      EXTERNAL          X02AJF, P01ABF
C     .. External Subroutines ..
      EXTERNAL          E04BBZ
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, SQRT
C     .. Executable Statements ..
      ISAVE = IFAIL
C
C     A MACHINE-DEPENDENT CONSTANT IS SET HERE. EPSMCH IS THE
C     SMALLEST POSITIVE REAL NUMBER SUCH THAT 1.0 + EPSMCH .GT. 1.0
C
      EPSMCH = X02AJF()
C
      RTEPS = SQRT(EPSMCH)
      IF (EPS.LT.EPSMCH) EPS = RTEPS
      IF (T.LT.EPSMCH) T = RTEPS
C
C     ERROR IN INPUT PARAMETERS
C
      IFAIL = 1
      IF (A+T.GE.B .OR. MAXCAL.LT.2) GO TO 140
      R = 5.0D-1*(B-A)
      X = A + R
      CALL FUN(X,F,G)
      XLAMDA = B
      IF (G.GT.0.0D+0) GO TO 20
      U = 5.0D-1*R
      A = 0.0D+0
      B = R + EPS*ABS(XLAMDA) + T
      B1 = B
      RR = -1.0D+0
      GO TO 40
   20 U = -5.0D-1*R
      A = -R
      B = 0.0D+0
      B1 = 0.0D+0
      RR = 1.0D+0
   40 CALL FUN(X+U,FU,GU)
      XW = 0.0D+0
      SCXBD = R
      E = R
      D = U
      SS = 0.0D+0
      OLDF = F + T
      FW = F
      GW = G
      GTEST1 = 0.0D+0
      GTEST2 = 0.0D+0
      TOL = EPS*ABS(X) + T
      NUMF = 2
C
C     SET ILOC TO 2 SO THAT THE MAIN SECTION OF E04BBZ IS EXECUTED
C     AS THE INITIAL 2 POINTS HAVE ALREADY BEEN SET UP
C
      ILOC = 2
   60 CALL E04BBZ(EPS,T,0.0D+0,XLAMDA,U,FU,GU,X,F,G,XW,FW,GW,A,B,OLDF,
     *            B1,SCXBD,E,D,RR,SS,GTEST1,GTEST2,TOL,ILOC,IFLAG)
      IF (IFLAG.NE.1) GO TO 100
      IF (NUMF.GE.MAXCAL) GO TO 80
      CALL FUN(X+U,FU,GU)
      NUMF = NUMF + 1
      GO TO 60
   80 IFAIL = 2
      GO TO 120
  100 IFAIL = 0
  120 MAXCAL = NUMF
      A = A + X
      B = B + X
  140 CONTINUE
      IF (IFAIL.EQ.0) RETURN
      IFAIL = P01ABF(ISAVE,IFAIL,SRNAME,0,P01REC)
      RETURN
C
C     END OF E04BBF (UNIGRD)
C
      END
