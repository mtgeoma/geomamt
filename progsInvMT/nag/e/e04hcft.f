      SUBROUTINE E04HCF(N,SFUN,X,F,G,IW,LIW,W,LW,IFAIL)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C     MARK 14 REVISED. IER-799 (DEC 1989).
C
C     **************************************************************
C
C     E04HCF CHECKS THAT A USER-SUPPLIED ROUTINE FOR EVALUATING AN
C     OBJECTIVE FUNCTION AND ITS FIRST DERIVATIVES PRODUCES
C     DERIVATIVE VALUES WHICH ARE CONSISTENT WITH THE FUNCTION
C     VALUES CALCULATED.
C
C     THE ROUTINE IS ESSENTIALLY IDENTICAL TO THE SUBROUTINE CHKGRD
C     IN THE NPL ALGORITHMS LIBRARY (REF. NO. E4/05/F). W(I), I = 1,
C     2, . . . , 3*N ARE USED AS WORKSPACE. (NOTE THAT, FOR
C     CONSISTENCY WITH OTHER E04 DOCUMENTATION, THE NAME FUNCT IS
C     USED INSTEAD OF SFUN IN THE WRITE-UP.)
C
C     PHILIP E. GILL, ENID M. R. LONG, WALTER MURRAY, SUSAN M.
C     PICKEN, D.N.A.C., NATIONAL PHYSICAL LABORATORY, ENGLAND.
C
C     **************************************************************
C
C     SFUN
C
C     A MACHINE DEPENDENT CONSTANT IS SET HERE. EPSMCH IS THE
C     SMALLEST POSITIVE REAL NUMBER SUCH THAT 1 + EPSMCH .GT. 1.
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='E04HCF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  F
      INTEGER           IFAIL, LIW, LW, N
C     .. Array Arguments ..
      DOUBLE PRECISION  G(N), W(LW), X(N)
      INTEGER           IW(LIW)
C     .. Subroutine Arguments ..
      EXTERNAL          SFUN
C     .. Local Scalars ..
      DOUBLE PRECISION  C1, C2, EPSMCH, F1, H, V1, V2, W1, W2
      INTEGER           I, IA, IAI, IB, IBI, IC, NWHY
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  DDOT, X02AJF
      INTEGER           P01ABF
      EXTERNAL          DDOT, X02AJF, P01ABF
C     .. External Subroutines ..
      EXTERNAL          E04HCZ
C     .. Intrinsic Functions ..
      INTRINSIC         SQRT
C     .. Executable Statements ..
      EPSMCH = X02AJF()
      H = SQRT(EPSMCH)
      IF (N.GE.1 .AND. LW.GE.3*N .AND. LIW.GT.0) GO TO 20
      NWHY = 1
      GO TO 80
C
C     THE FUNCTION VALUE AND THE GRADIENT VECTOR, EVALUATED AT X,
C     ARE ASSIGNED TO F AND ARRAY G RESPECTIVELY
C
   20 NWHY = 2
      CALL SFUN(NWHY,N,X,F,G,IW,LIW,W,LW)
      IF (NWHY.LT.0) GO TO 80
C
C     TWO ORTHOGONAL VECTORS A AND B ARE SET UP.
C
      IA = 1
      IB = N + 1
      IC = 2*N + 1
      CALL E04HCZ(N,W(IA),W(IB))
C
C     W1 AND W2 ARE PUT EQUAL TO GT*A AND GT*B RESPECTIVELY, WHERE T
C     DENOTES TRANSPOSE
C
      W1 = DDOT(N,W(IA),1,G,1)
      W2 = DDOT(N,W(IB),1,G,1)
C
C     A FORWARD-DIFFERENCE APPROXIMATION TO THE GRADIENT IS NOW MADE
C     ALONG A AND B.
C
      DO 40 I = 1, N
         IAI = IA + I - 1
         W(IAI) = X(I) + H*W(IAI)
   40 CONTINUE
      F1 = F
      NWHY = 2
      IF (N.NE.1) CALL SFUN(NWHY,N,W(IA),F1,W(IC),IW,LIW,W,LW)
      IF (NWHY.LT.0) GO TO 80
      V1 = (F1-F)/H
      DO 60 I = 1, N
         IBI = IB + I - 1
         W(IBI) = X(I) + H*W(IBI)
   60 CONTINUE
      NWHY = 2
      CALL SFUN(NWHY,N,W(IB),F1,W(IC),IW,LIW,W,LW)
      IF (NWHY.LT.0) GO TO 80
      V2 = (F1-F)/H
C
C     C1 AND C2 ARE THE DIFFERENCES BETWEEN APPROXIMATED AND
C     PROGRAMMED GRADIENT PROJECTED ALONG A AND B RESPECTIVELY
C
      C1 = V1 - W1
      C2 = V2 - W2
C
C     IF EITHER C1 OR C2 IS TOO LARGE AN ERROR INDICATOR IS SET
C
      NWHY = 0
      IF (C1*C1.GE.H*(W1*W1+1.0D+0) .OR. C2*C2.GE.H*(W2*W2+1.0D+0))
     *    NWHY = 2
   80 IF (NWHY.NE.0) GO TO 100
      IFAIL = 0
      RETURN
  100 IFAIL = P01ABF(IFAIL,NWHY,SRNAME,0,P01REC)
      RETURN
C
C     END OF E04HCF (CHKGRD)
C
      END
