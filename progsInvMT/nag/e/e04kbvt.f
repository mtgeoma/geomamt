      SUBROUTINE E04KBV(N,INEW,G,NFREE,ISTATE,GPROJ,GTG,HESD,LHPROJ,
     *                  BOUNDK,COND)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 14 REVISED. IER-806 (DEC 1989).
C
C     **************************************************************
C
C     E04KBV (DELBND) PLACES THE NEWLY RELEASED VARIABLE X(INEW) AT
C     THE END OF THE PERMUTATION OF FREE VARIABLES AND ADDS A ROW
C     AND A COLUMN OF THE IDENTITY MATRIX TO THE PROJECTED HESSIAN.
C     IT THEN UPDATES THE PROJECTED GRADIENT AND CALCULATES THE
C     SQUARE OF ITS EUCLIDEAN NORM. FINALLY IT USES E04JBW TO
C     COMPUTE A BOUND ON THE CONDITION NUMBER OF THE PROJECTED
C     HESSIAN AND E04KBZ TO MODIFY THIS IF NECESSARY.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  BOUNDK, COND, GTG
      INTEGER           INEW, LHPROJ, N, NFREE
C     .. Array Arguments ..
      DOUBLE PRECISION  G(N), GPROJ(N), HESD(N)
      INTEGER           ISTATE(N)
C     .. External Functions ..
      DOUBLE PRECISION  E04JBW, DDOT
      EXTERNAL          E04JBW, DDOT
C     .. External Subroutines ..
      EXTERNAL          E04KBZ
C     .. Executable Statements ..
      NFREE = NFREE + 1
      ISTATE(INEW) = NFREE
      HESD(NFREE) = 1.0D+0
      LHPROJ = 1
      IF (NFREE.GE.3) LHPROJ = NFREE*(NFREE-1)/2
      GPROJ(NFREE) = G(INEW)
      GTG = DDOT(NFREE,GPROJ,1,GPROJ,1)
      BOUNDK = E04JBW(NFREE)
      CALL E04KBZ(NFREE,BOUNDK,HESD,COND)
      RETURN
C
C     END OF E04KBV (DELBND)
C
      END
