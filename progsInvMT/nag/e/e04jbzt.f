      SUBROUTINE E04JBZ(N,EPS,X,BL,BU,NFREE,NEQUAL,ISTATE,INTYPE)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 8 REVISED. IER-238 (APR 1980).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     **************************************************************
C
C     WHEN THE FUNCTION VALUE, GRADIENT AND/OR PROJECTED HESSIAN
C     FACTORS AT THE INITIAL POINT HAVE BEEN SUPPLIED BY THE USER
C     (I.E. WHEN INTYPE IS STRICTLY POSITIVE), E04JBZ (FEASBL)
C     CHECKS ON THE FEASIBILITY OF THE VARIABLES. IF ANY VARIABLE
C     VIOLATES EITHER OF ITS BOUNDS, INTYPE IS RESET TO - 1 TO
C     INDICATE THAT THE SUPPLIED VALUES MUST BE RECOMPUTED. IF ALL
C     THE VARIABLES ARE FEASIBLE AND THE VECTOR ISTATE HAS BEEN SET
C     BY THE USER (THAT IS, IF INTYPE = 2 OR 3), THE ROUTINE CHECKS
C     WHETHER EACH FIXED X(I) LIES WITHIN A TOLERANCE OF THE BOUND
C     INDICATED BY ISTATE(I). IF NOT, OR IF ANY OF THE ALLEGEDLY
C     FREE VARIABLES IS EFFECTIVELY A CONSTANT, E04JBZ RETURNS
C     TO THE CALLING ROUTINE WITH INTYPE = -3 IF ANY F AND G
C     ARE TO BE COMPUTED BUT INTYPE = -2 OTHERWISE. IF THE
C     ELEMENTS OF ISTATE HAVE BEEN SET CORRECTLY, NFREE RECORDS
C     THE NUMBER OF FREE VARIABLES.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  EPS
      INTEGER           INTYPE, N, NEQUAL, NFREE
C     .. Array Arguments ..
      DOUBLE PRECISION  BL(N), BU(N), X(N)
      INTEGER           ISTATE(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  BNDL, BNDU, TEST, TOL, XI
      INTEGER           I, IEQUAL, IFREE, ISI
C     .. Intrinsic Functions ..
      INTRINSIC         ABS
C     .. Executable Statements ..
      TOL = EPS
      DO 40 I = 1, N
         XI = X(I)
         BNDL = BL(I)
         BNDU = BU(I)
         TEST = TOL*(ABS(BNDL)+1.0D+0)
         IF (BNDL-XI.LE.TEST) GO TO 20
         INTYPE = -1
         RETURN
   20    TEST = TOL*(ABS(BNDU)+1.0D+0)
         IF (XI-BNDU.LE.TEST) GO TO 40
         INTYPE = -1
         RETURN
   40 CONTINUE
      IF (INTYPE.LT.2) RETURN
      IEQUAL = 0
      IFREE = 0
      DO 180 I = 1, N
         ISI = ISTATE(I)
         BNDL = BL(I)
         BNDU = BU(I)
         TEST = TOL*(ABS(BNDL)+1.0D+0)
         IF (BNDU-BNDL.GT.TEST) GO TO 80
         IF (ISI.LT.0) GO TO 60
         INTYPE = -INTYPE
         IF (INTYPE.EQ.-3) RETURN
   60    IEQUAL = IEQUAL + 1
         ISI = -3
         ISTATE(I) = -3
         GO TO 100
   80    IF (ISI.GE.0) GO TO 160
  100    XI = X(I)
         IF (ISI.EQ.-1) GO TO 140
         IF (ABS(XI-BNDL).LE.TEST) GO TO 120
         INTYPE = -3
         RETURN
  120    IF (ISI.EQ.-2) GO TO 180
  140    TEST = TOL*(ABS(BNDU)+1.0D+0)
         IF (ABS(XI-BNDU).LE.TEST) GO TO 180
         INTYPE = -3
         RETURN
  160    IFREE = IFREE + 1
  180 CONTINUE
      NEQUAL = IEQUAL
      NFREE = IFREE
      RETURN
C
C     END OF E04JBZ (FEASBL)
C
      END
