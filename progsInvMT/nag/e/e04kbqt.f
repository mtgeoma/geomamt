      SUBROUTINE E04KBQ(N,LH,Z,HESD,HESL,P)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     **************************************************************
C
C     E04KBQ (NMDCHL) FORMS THE CHOLESKY FACTORIZATION OF THE MATRIX
C     LDLT - ZZT WHERE LT IS THE TRANSPOSE OF L, ZT THE TRANSPOSE OF
C     Z AND D A DIAGONAL MATRIX. THE UNIT LOWER TRIANGULAR MATRIX L
C     IS STORED BY ROWS IN THE ARRAY HESL(I), I = 1(1)N(N - 1)/2,
C     WITH THE UNIT DIAGONAL OMITTED. THE DIAGONAL OF THE DIAGONAL
C     MATRIX D AND VECTOR Z ARE STORED IN THE ARRAYS HESD(I) AND
C     Z(I), I = 1(1)N, RESPECTIVELY BOTH L AND D ARE OVERWRITTEN
C     WITH THE CORRESPONDING FACTORS OF THE MODIFIED MATRICES, THE
C     VALUE OF Z IS NOT RETAINED. THE SUBROUTINE MODIFIES THE
C     CHOLESKY FACTORS OF A MATRIX WHICH COULD BE INDEFINITE, AND
C     ENSURES THAT THE NEW MATRIX IS POSITIVE DEFINITE. IF IT IS
C     REQUIRED TO ADD A RANK-ONE MATRIX E04KBR (MODCHL) SHOULD BE
C     USED.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, SHEDMOND R.
C     GRAHAM AND MARGARET H. WRIGHT, D.N.A.C. NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     IMPLEMENTORS SHOULD CONSIDER PUTTING THE BODY OF THIS ROUTINE
C     INTO MACHINE CODE.
C
C     **************************************************************
C
C
C     EPSMCH IS THE SMALLEST POSITIVE REAL NUMBER SUCH THAT 1 +
C     EPSMCH .GT. 1
C
C     .. Scalar Arguments ..
      INTEGER           LH, N
C     .. Array Arguments ..
      DOUBLE PRECISION  HESD(N), HESL(LH), P(N), Z(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  BETA, DJ, EPSMCH, G, GAMMA, GAMMA1, PJ, T
      INTEGER           I, IB, IQ, J, JN1, JP1, K
C     .. External Functions ..
      DOUBLE PRECISION  X02AJF
      EXTERNAL          X02AJF
C     .. Executable Statements ..
      EPSMCH = X02AJF()
C
C     SOLVE LP = Z.
C
      GAMMA = 0.0D+0
      J = 1
      DO 60 I = 1, N
C
C        SEE GILL, MURRAY, AND SAUNDERS NAC REPORT 56.
C
         T = Z(I)
         IF (I.EQ.1) GO TO 40
         K = I - 1
         DO 20 IB = 1, K
            T = T - P(IB)*HESL(J)
            J = J + 1
   20    CONTINUE
   40    P(I) = T
         GAMMA = GAMMA + T*T/HESD(I)
   60 CONTINUE
C
C     IF 1 - GAMMA .LT. EPSMCH THEN THE MODIFIED MATRIX IS NOT
C     SUFFICIENTLY POSITIVE DEFINITE. GAMMA IS REPLACED BY A
C     QUANTITY WHICH ENSURES THAT THE MODIFIED MATRIX IS POSITIVE
C     DEFINITE REGARDLESS OF SUBSEQUENT ROUNDING ERROR.
C
      GAMMA1 = 1.0D+0 - GAMMA
      GAMMA = EPSMCH
      IF (GAMMA1.GT.EPSMCH) GAMMA = GAMMA1
      IF (-GAMMA1.GT.EPSMCH) GAMMA = -GAMMA1
      K = J + N + N
      JN1 = N + 1
      DO 100 I = 1, N
         J = JN1 - I
         PJ = P(J)
         DJ = HESD(J)
         T = PJ/DJ
         Z(J) = PJ
         BETA = -T/GAMMA
         G = GAMMA + PJ*T
         HESD(J) = DJ*GAMMA/G
         GAMMA = G
         JP1 = J + 1
         K = K - JP1
         IQ = K
         IF (J.EQ.N) GO TO 100
         DO 80 IB = JP1, N
            T = HESL(IQ)
            HESL(IQ) = T + BETA*Z(IB)
            Z(IB) = Z(IB) + PJ*T
            IQ = IQ + IB - 1
   80    CONTINUE
  100 CONTINUE
      RETURN
C
C     END OF E04KBQ (NMDCHL)
C
      END
