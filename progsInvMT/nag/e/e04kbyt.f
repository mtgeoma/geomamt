      SUBROUTINE E04KBY(M,STEP,GOLDTP,GOLD,GNEWTP,GNEW,Y,Z,HESD,HESL,LH,
     *                  IFLAG)
C
C     MARK 6 RELEASE NAG COPYRIGHT 1977
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     **************************************************************
C
C     E04KBY (COMDFP) IS CALLED AFTER A SUCCESSFUL LINEAR SEARCH
C     ALONG THE VECTOR P TO THE POINT (X + STEP*P). IT FIRST CHECKS
C     WHETHER THE NEW GTP IS GREATER THAN THE OLD GTP. IF SO, IT
C     USES THE COMPLEMENTARY DFP RULE TO UPDATE THE LDLT FACTORS OF
C     THE APPROXIMATED HESSIAN MATRIX. E04KBR ADDS THE POSITIVE
C     RANK-ONE MATRIX ALPHA*YYT, WHERE Y IS THE DIFFERENCE BETWEEN
C     THE NEW G AND THE OLD G AND ALPHA IS THE RECIPROCAL OF THE
C     INNER PRODUCT OF Y WITH THE VECTOR STEP*P. THEN E04KBQ
C     SUBTRACTS THE RANK-ONE MATRIX BETA*ZZT, WHERE Z IS THE OLD G
C     AND BETA IS THE RECIPROCAL OF THE ABSOLUTE VALUE OF THE OLD
C     GTP. IF OVERFLOW IS LIKELY TO OCCUR AT ANY STAGE, E04KBY
C     RETURNS IMMEDIATELY WITH IFLAG = - 1. IF NOT, IFLAG = 1 ON
C     EXIT IF THE FACTORS HAVE BEEN UPDATED AND 0 OTHERWISE.
C
C     PHILIP E. GILL, WALTER MURRAY, SUSAN M. PICKEN, MARGARET H.
C     WRIGHT AND ENID M. R. LONG, D.N.A.C., NATIONAL PHYSICAL
C     LABORATORY, ENGLAND
C
C     **************************************************************
C
C
C     A MACHINE-DEPENDENT CONSTANT IS SET HERE. RMAX IS THE LARGEST
C     POSITIVE REAL NUMBER SUCH THAT BOTH RMAX AND - RMAX CAN BE
C     HELD IN THE MACHINE.
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  GNEWTP, GOLDTP, STEP
      INTEGER           IFLAG, LH, M
C     .. Array Arguments ..
      DOUBLE PRECISION  GNEW(M), GOLD(M), HESD(M), HESL(LH), Y(M), Z(M)
C     .. Local Scalars ..
      DOUBLE PRECISION  RMAX, RTALPH, RTBETA, TEST
      INTEGER           I, IFAIL
C     .. External Functions ..
      DOUBLE PRECISION  X02ALF
      EXTERNAL          X02ALF
C     .. External Subroutines ..
      EXTERNAL          E04KBQ, E04KBR
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, SQRT
C     .. Executable Statements ..
      RMAX = X02ALF()
C
      IFLAG = -1
      TEST = (GNEWTP-GOLDTP)*STEP
      IF (TEST.GT.0.0D+0) GO TO 20
      IFLAG = 0
      RETURN
C
C     POSITIVE RANK-ONE UPDATE.
C
   20 IF (TEST.GE.1.0D+0) GO TO 40
      IF (TEST*RMAX.LE.1.0D+0) RETURN
   40 RTALPH = 1.0D+0/SQRT(TEST)
      DO 60 I = 1, M
         Y(I) = (GNEW(I)-GOLD(I))*RTALPH
   60 CONTINUE
      CALL E04KBR(M,LH,1.0D+0,Y,HESD,HESL,IFAIL)
      IF (IFAIL.EQ.1) RETURN
C
C     NEGATIVE RANK-ONE UPDATE.
C
      TEST = ABS(GOLDTP)
      IF (TEST.GE.1.0D+0) GO TO 80
      IF (TEST*RMAX.LE.1.0D+0) RETURN
   80 RTBETA = 1.0D+0/SQRT(TEST)
      DO 100 I = 1, M
         Z(I) = GOLD(I)*RTBETA
  100 CONTINUE
      CALL E04KBQ(M,LH,Z,HESD,HESL,Y)
      IFLAG = 1
      RETURN
C
C     END OF E04KBY (COMDFP)
C
      END
