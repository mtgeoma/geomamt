      SUBROUTINE F01QBF(M,N,A,NRA,C,NRC,WORK,IFAIL)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C     MARK 14 REVISED. IER-731 (DEC 1989).
C     WRITTEN BY S. HAMMARLING, MIDDLESEX POLYTECHNIC (GIVNUQ)
C
C     SUBROUTINE F01QBF RETURNS, IN C, THE GIVENS UQ
C     FACTORIZATION OF THE M*N (M.LE.N) MATRIX A. THAT IS, A
C     IS FACTORIZED AS
C
C         A = (U O)Q , M.LT.N,          A = UQ , M.EQ.N,
C
C     WHERE U IS AN M*M UPPER TRIANGULAR MATRIX AND Q IS AN
C     N*N ORTHOGONAL MATRIX.
C
C     INPUT PARAMETERS.
C
C     M     - NUMBER OF ROWS OF A. M MUST BE AT LEAST UNITY.
C
C     N     - NUMBER OF COLUMNS OF A. N MUST BE AT LEAST M.
C
C     A     - THE M*N MATRIX TO BE FACTORIZED.
C
C     NRA   - ROW DIMENSION OF A AS DECLARED IN THE CALLING PROGRAM.
C             NRA MUST BE AT LEAST M.
C
C     NRC   - ROW DIMENSION OF C AS DECLARED IN THE CALLING PROGRAM.
C             NRC MUST BE AT LEAST M
C
C     IFAIL - THE USUAL FAILURE PARAMETER. IF IN DOUBT SET
C             IFAIL TO ZERO BEFORE CALLING THIS ROUTINE.
C
C     OUTPUT PARAMETERS.
C
C     C     - AN M*N MATRIX CONTAINING DETAILS OF THE UQ
C             FACTORIZATION. U IS RETURNED IN THE LEFT HAND
C             UPPER TRIANGULAR PART OF C. DETAILS OF Q ARE
C             STORED IN THE REMAINING PART OF C SUCH THAT
C             C(K,J) CONTAINS THE TANGENT FOR THE PLANE
C             ROTATION,  R(K,J), IN THE K,J PLANE THAT
C             ANNHILATES THE K,J ELEMENT OF A.
C             THE ROUTINE MAY BE CALLED WITH C=A.
C
C     IFAIL - ON NORMAL RETURN IFAIL WILL BE ZERO.
C             IF AN INPUT PARAMETER IS INCORRECTLY SUPPLIED
C             THEN IFAIL IS SET TO UNITY. NO OTHER FAILURE
C             IS POSSIBLE.
C
C     WORKSPACE PARAMETER.
C
C     WORK  - AN M ELEMENT VECTOR.
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='F01QBF')
C     .. Scalar Arguments ..
      INTEGER           IFAIL, M, N, NRA, NRC
C     .. Array Arguments ..
      DOUBLE PRECISION  A(NRA,*), C(NRC,*), WORK(*)
C     .. Local Scalars ..
      DOUBLE PRECISION  BIG, CKK, CS, RSQTPS, SMALL, SN, SQTEPS, T, Z
      INTEGER           I, IERR, J, JF, JJ, JL, K, KK, KM1
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  F01LZZ, X02AJF, X02AMF
      INTEGER           P01ABF
      EXTERNAL          F01LZZ, X02AJF, X02AMF, P01ABF
C     .. External Subroutines ..
      EXTERNAL          F01LZW, F01LZY
C     .. Intrinsic Functions ..
      INTRINSIC         SQRT
C     .. Executable Statements ..
      IERR = IFAIL
      IF (IERR.EQ.0) IFAIL = 1
C
      IF (NRA.LT.M .OR. NRC.LT.M .OR. M.LT.1 .OR. N.LT.M) GO TO 220
C
      SMALL = X02AMF()
      BIG = 1.0D0/SMALL
      SQTEPS = SQRT(X02AJF())
      RSQTPS = 1.0D0/SQTEPS
C
      DO 40 J = 1, N
         DO 20 I = 1, M
            C(I,J) = A(I,J)
   20    CONTINUE
   40 CONTINUE
C
      IFAIL = 0
      K = M
      DO 200 KK = 1, M
C
         DO 60 I = 1, K
            WORK(I) = C(I,K)
   60    CONTINUE
         CKK = WORK(K)
C
         JL = 1
         KM1 = K - 1
         IF (K.EQ.1) GO TO 140
         JF = KM1
   80    J = JF
         DO 120 JJ = JL, JF
            Z = C(K,J)
C
            T = F01LZZ(CKK,Z,SMALL,BIG)
C
            C(K,J) = T
C
C           C(K,J) NOW CONTAINS THE TANGENT FOR THE PLANE ROTATION
C           R(K,J). IF THE TANGENT IS ZERO THEN WE CAN SKIP THE
C           TRANSFORMATION, OTHERWISE WE COMPUTE CS=COSINE AND
C           SN=SINE AND PERFORM THE TRANSFORMATION R(K,J).
C
            IF (T.EQ.0.0D0) GO TO 100
C
            CALL F01LZW(T,CS,SN,SQTEPS,RSQTPS,BIG)
C
            CKK = CS*CKK + SN*Z
C
            IF (K.GT.1) CALL F01LZY(KM1,CS,SN,WORK,C(1,J))
C
  100       J = J - 1
  120    CONTINUE
C
  140    IF (JL.NE.1 .OR. M.EQ.N) GO TO 160
         JF = N
         JL = M + 1
         GO TO 80
C
C        THE RETURN TO STATEMENT NUMBER 80 IS TO PERFORM THE
C        TRANSFORMATIONS FOR COLUMNS M+1,M+2,...,N.
C
  160    WORK(K) = CKK
         DO 180 I = 1, K
            C(I,K) = WORK(I)
  180    CONTINUE
C
         K = KM1
  200 CONTINUE
C
      RETURN
C
  220 IFAIL = P01ABF(IERR,IFAIL,SRNAME,0,P01REC)
      RETURN
      END
