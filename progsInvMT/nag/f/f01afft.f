      SUBROUTINE F01AFF(N,IM1,IM2,B,IB,DL,Z,IZ)
C     MARK 2 RELEASE. NAG COPYRIGHT 1972
C     MARK 4.5 REVISED
C     MARK 11 REVISED. VECTORISATION (JAN 1984).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 12 REVISED. EXTENDED BLAS (JUNE 1986)
C
C     REBAKA
C     THIS SUBROUTINE PERFORMS, ON THE MATRIX OF EIGENVECTORS, Z,
C     STORED
C     IN COLUMNS IM1 TO IM2 OF THE ARRAY Z(N,N), A BACKWARD
C     SUBSTITUTION
C     LT* X = Z, OVER- WRITING X ON Z. THE DIAGONAL ELEMENTS OF L
C     MUST BE STORED IN THE ARRAY DL(N), AND THE REMAINING
C     TRIANGLE IN THE STRICTLY LOWER TRIANGLE OF THE ARRAY B(N,N).
C     THE SUBROUTINES F01AEF AND F01BDF LEAVE L IN THIS
C     DESIRED FORM. IF X DENOTES ANY COLUMN OF THE RESULTANT MATRIX
C     X, THEN X SATISFIES XT * B * X = ZT * Z, WHERE B = L * LT.
C     1ST AUGUST 1971
C
C     .. Scalar Arguments ..
      INTEGER           IB, IM1, IM2, IZ, N
C     .. Array Arguments ..
      DOUBLE PRECISION  B(IB,N), DL(N), Z(IZ,IM2)
C     .. Local Scalars ..
      INTEGER           J
C     .. External Subroutines ..
      EXTERNAL          DSWAP, DTRSV
C     .. Executable Statements ..
      CALL DSWAP(N,DL,1,B,IB+1)
      DO 20 J = IM1, IM2
         CALL DTRSV('L','T','N',N,B,IB,Z(1,J),1)
   20 CONTINUE
      CALL DSWAP(N,DL,1,B,IB+1)
      RETURN
      END
