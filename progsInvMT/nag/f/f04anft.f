      SUBROUTINE F04ANF(M,N,QR,IQR,ALPHA,IPIVOT,R,Y,Z)
C     MARK 2 RELEASE. NAG COPYRIGHT 1972
C     MARK 4 REVISED.
C     MARK 4.5 REVISED
C     MARK 11 REVISED. VECTORISATION (JAN 1984).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 12 REVISED. EXTENDED BLAS (JUNE 1986)
C
C     SOLVE
C     USING THE VECTORS U WHOSE NONZERO COMPONENTS ARE STORED ON
C     AND BELOW THE MAIN DIAGONAL OF QR(M,N) F04ANF APPLIES THE
C     N TRANSFORMATIONS (I-BETA*U*UT) TO THE RIGHT HAND SIDE R(M).
C     FROM THE REDUCED MATRIX GIVEN IN ALPHA(N) AND THE UPPER
C     RIGHT TRIANGULAR PART OF QR, F04ANF THEN COMPUTES BY BACK-
C     SUBSTITUTION AN APPROXIMATE SOLUTION TO THE LINEAR SYSTEM.
C     THE COMPONENTS OF THE SOLUTION VECTOR ARE STORED IN Y(N)
C     IN THE ORDER PRESCRIBED BY IPIVOT(N)..
C     1ST. MARCH  1972
C
C     .. Scalar Arguments ..
      INTEGER           IQR, M, N
C     .. Array Arguments ..
      DOUBLE PRECISION  ALPHA(N), QR(IQR,N), R(M), Y(N), Z(N)
      INTEGER           IPIVOT(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  D1, GAMMA
      INTEGER           I, II, J
C     .. External Subroutines ..
      EXTERNAL          DSWAP, DTRSV
C     .. Executable Statements ..
      DO 60 J = 1, N
C        APPLY THE J-TH TRANSFORMATION TO THE RIGHT HAND SIDE
         D1 = 0.0D0
         DO 20 I = J, M
            D1 = D1 + QR(I,J)*R(I)
   20    CONTINUE
         GAMMA = D1/(ALPHA(J)*QR(J,J))
         DO 40 I = J, M
            R(I) = R(I) + GAMMA*QR(I,J)
   40    CONTINUE
   60 CONTINUE
C     BACKSUBSTITUTION
      DO 80 I = 1, N
         Z(I) = R(I)
   80 CONTINUE
      CALL DSWAP(N,ALPHA,1,QR,IQR+1)
      CALL DTRSV('U','N','N',N,QR,IQR,Z,1)
      CALL DSWAP(N,ALPHA,1,QR,IQR+1)
      DO 100 I = 1, N
         II = IPIVOT(I)
         Y(II) = Z(I)
  100 CONTINUE
      RETURN
      END
