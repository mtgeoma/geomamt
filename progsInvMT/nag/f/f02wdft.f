      SUBROUTINE F02WDF(M,N,A,NRA,WANTB,B,TOL,SVD,IRANK,Z,SV,WANTR,R,
     *                  NRR,WANTPT,PT,NRPT,WORK,LWORK,IFAIL)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C     WRITTEN BY S. HAMMARLING, MIDDLESEX POLYTECHNIC (SVDGN4)
C
C     F02WDF RETURNS THE HOUSEHOLDER QU FACTORIZATION OF A
C     AND, IF EITHER A IS NOT OF FULL RANK OR IF SVD IS TRUE,
C     PART OR ALL OF THE SINGULAR VALUE DECOMPOSITION (SVD) OF
C     A, WHERE A IS AN M*N (M.GE.N) MATRIX.
C
C     A IS FIRST FACTORIZED AS
C
C     A = Q*(U) ,
C           (0)
C
C     WHERE Q IS ORTHOGONAL AND U IS UPPER TRIANGULAR.
C
C     IF EITHER U IS SINGULAR OR SVD IS TRUE THEN PART OR ALL
C     OF THE SVD OF U GIVEN BY
C
C     U = R*D*(P**T) ,
C
C     WHERE R AND P ARE ORTHOGONAL AND D IS DIAGONAL WITH
C     NON-NEGATIVE DIAGONAL ELEMENTS, IS OBTAINED.
C
C     THE SVD OF A IS THEN GIVEN BY
C
C     A = Q*(R 0)*(D)*(P**T) ,
C           (0 I) (0)
C
C     THE DIAGONAL ELEMENTS OF D BEING THE SINGULAR VALUES OF A.
C
C     IF WANTB IS TRUE THEN (Q**T)*B IS FORMED FOLLOWED, IF
C     APPROPRIATE, BY (R**T 0)*(Q**T)*B.
C                     ( 0   I)
C
C     INPUT PARAMETERS.
C
C     M      - NUMBER OF ROWS OF A. M MUST BE AT LEAST N.
C
C     N      - NUMBER OF COLUMNS OF A. N MUST BE AT LEAST UNITY.
C
C     A      - M*N MATRIX TO BE FACTORIZED.
C
C     NRA    - ROW DIMENSION OF A AS DECLARED IN THE
C              CALLING PROGRAM. NRA MUST BE AT LEAST M.
C
C     WANTB  - MUST BE .TRUE. IF (Q**T)*B OR, IF APPROPRIATE,
C              (R**T 0)*(Q**T)*B IS REQUIRED.
C              ( 0   I)
C              IF WANTB IS .FALSE. THEN B IS NOT REFERENCED.
C
C     B      - AN M ELEMENT VECTOR.
C
C     TOL    - A RELATIVE TOLERANCE USED TO DETERMINE THE RANK OF A.
C              TOL SHOULD BE CHOSEN AS APPROXIMATELY THE LARGEST
C              RELATIVE ERROR IN THE ELEMENTS OF A.
C              LETTING EPS DENOTE THE SMALLEST REAL FOR WHICH
C              1.0+EPS.GT.1.0 ON THE MACHINE, IF TOL IS OUTSIDE THE
C              RANGE (EPS,1) THEN THE VALUE EPS IS USED IN
C              PLACE OF TOL. U IS REGARDED AS SINGULAR IF
C              L(U)*L(U**(-1))*TOL.GT.1, WHERE L(U) DENOTES
C              THE EUCLIDEAN LENGTH OF U.
C              IF THE SINGULAR VALUES OF A ARE COMPUTED
C              THEN THE RANK OF A IS DETERMINED BY
C              NEGLECTING SINGULAR VALUES FOR WHICH
C              SV(1)*TOL.GE.SV(I), WHERE SV(1) IS THE
C              LARGEST SINGULAR  VALUE.
C
C     SVD    - MUST BE .TRUE. IF PART OR ALL OF THE SVD IS
C              REQUIRED EVEN IF A IS OF FULL RANK.
C
C     WANTR  - MUST BE .TRUE. IF THE ORTHOGONAL MATRIX R IS REQUIRED
C              IF WANTR IS .FALSE. THEN R IS NOT REFERENCED.
C
C     NRR    - ROW DIMENSION OF R AS DECLARED IN THE CALLING PROGRAM
C              NRR MUST BE AT LEAST N.
C
C     WANTPT - MUST BE .TRUE. IF THE ORTHOGONAL MATRIX P**T
C              IS REQUIRED. NOTE THAT PT IS REFERENCED EVEN
C              WHEN WANTPT IS .FALSE., BUT SEE OUTPUT
C              PARAMETER PT BELOW.
C
C     NRPT   - ROW DIMENSION OF PT AS DECLARED IN THE
C              CALLING PROGRAM. NRPT MUST BE AT LEAST N.
C
C     IFAIL  - THE USUAL FAILURE PARAMETER. IF IN DOUBT SET IFAIL TO
C              ZERO BEFORE CALLING F02WDF.
C
C     OUTPUT PARAMETERS.
C
C     A      - A, TOGETHER WITH THE VECTOR Z, WILL CONTAIN
C              DETAILS OF THE QU FACTORIZATION AS RETURNED
C              FROM ROUTINE F01QAF, UNLESS F02WDF IS CALLED
C              WITH PT=A AND SVD IS RETURNED AS .TRUE..
C
C     B      - IF WANTB IS .TRUE. THEN IF A IS OF FULL RANK AND SVD
C              IS RETURNED AS .FALSE. B IS OVERWRITTEN BY (Q**T)*B,
C              OTHERWISE B IS OVERWRITTEN BY (R**T 0)*(Q**T)*B.
C                                            ( 0   I)
C              IF WANTB IS .FALSE. THEN B IS NOT REFERENCED.
C
C     SVD    - SVD IS RETURNED AS .TRUE. IF THE SINGULAR VALUES OF A
C              HAVE BEEN OBTAINED AND IS RETURNED AS
C              .FALSE. IF ONLY THE QU FACTORIZATION OF A
C              HAS BEEN OBTAINED.
C
C     IRANK  - THE RANK OF THE MATRIX A.
C
C     Z      - N ELEMENT VECTOR. SEE OUTPUT PARAMETER A ABOVE.
C              IF Z IS NOT REQUIRED THEN THE ROUTINE MAY BE
C              CALLED WITH Z=SV.
C
C     SV     - N ELEMENT VECTOR. IF A IS NOT OF FULL RANK OR IF SVD
C              IS RETURNED AS .TRUE. THEN SV WILL CONTAIN
C              THE SINGULAR VALUES OF A ARRANGED IN
C              DESCENDING ORDER.
C
C     R      - N*N MATRIX. IF WANTR IS .TRUE. AND SVD IS RETURNED AS
C              .TRUE. THEN R RETURNS THE LEFT HAND
C              ORTHOGONAL MATRIX OF THE SVD OF U.
C              IF WANTR IS .FALSE. THEN R IS NOT REFERENCED.
C
C     PT     - N*N MATRIX. IF WANTPT IS .TRUE. AND SVD IS
C              RETURNED AS .TRUE. THEN PT RETURNS THE
C              MATRIX P**T.
C              THE ROUTINE MAY BE CALLED WITH PT=A.
C              IF WANTPT IS .FALSE. BUT WANTR IS .TRUE.
C              THEN THE ROUTINE MAY BE CALLED WITH PT=R.
C
C     IFAIL  - ON NORMAL RETURN IFAIL WILL BE ZERO.
C              IN THE UNLIKELY EVENT THAT THE QR-ALGORITHM
C              FAILS TO FIND THE SINGULAR VALUES IN 50*N
C              ITERATIONS THEN IFAIL WILL BE 2 OR MORE AND
C              SUCH THAT SV(1),SV(2),..., SV(IFAIL-1) MAY
C              NOT HAVE BEEN FOUND.
C              IF AN INPUT PARAMETER IS INCORRECTLY
C              SUPPLIED THEN IFAIL IS SET TO UNITY.
C
C     WORKSPACE PARAMETERS.
C
C     WORK   - A 3*N ELEMENT VECTOR.
C              IF SVD IS RETURNED AS .FALSE. THEN WORK(1) WILL
C              CONTAIN THE CONDITION NUMBER, L(U)*L(U**(-1)), OF THE
C              UPPER TRIANGULAR MATRIX U.
C              IF SVD IS RETURNED AS .TRUE. THEN WORK(1)
C              WILL CONTAIN THE TOTAL NUMBER OF ITERATIONS
C              TAKEN BY THE QR-ALGORITHM.
C
C     LWORK  - LENGTH OF THE VECTOR WORK. LWORK MUST BE AT
C               LEAST 3*N.
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='F02WDF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  TOL
      INTEGER           IFAIL, IRANK, LWORK, M, N, NRA, NRPT, NRR
      LOGICAL           SVD, WANTB, WANTPT, WANTR
C     .. Array Arguments ..
      DOUBLE PRECISION  A(NRA,N), B(M), PT(NRPT,N), R(NRR,N), SV(N),
     *                  WORK(LWORK), Z(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  COND, EPS, TL
      INTEGER           IERR, N1, N2
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  F02WDZ, X02AJF
      INTEGER           F02WDY, P01ABF
      EXTERNAL          F02WDZ, X02AJF, F02WDY, P01ABF
C     .. External Subroutines ..
      EXTERNAL          F01LZF, F01QAF, F02SZF, F02WAY, F02WAZ, F02WCY
C     .. Executable Statements ..
      IERR = IFAIL
      IF (IERR.EQ.0) IFAIL = 1
C
      IF (NRA.LT.M .OR. M.LT.N .OR. N.LT.1 .OR. LWORK.LT.3*N .OR.
     *    NRPT.LT.N) GO TO 40
      IF (WANTR .AND. NRR.LT.N) GO TO 40
C
      EPS = X02AJF()
      TL = TOL
      IF (TL.LT.EPS .OR. TL.GE.1.0D0) TL = EPS
C
      N1 = N + 1
      N2 = N + N1
C
      CALL F01QAF(M,N,A,NRA,A,NRA,Z,IFAIL)
C
      IF (WANTB) CALL F02WAZ(M,N,A,NRA,Z,B,B)
C
      IF (SVD) GO TO 20
C
      COND = F02WDZ(N,A,NRA,WORK)
C
      IF (COND*TL.GT.1.0D0) GO TO 20
      IRANK = N
      SVD = .FALSE.
      WORK(1) = COND
      RETURN
C
   20 SVD = .TRUE.
C
      CALL F01LZF(N,A,NRA,PT,NRPT,WANTB,B,WANTR,.FALSE.,WORK,1,1,
     *            .FALSE.,WORK,1,1,SV,WORK,WORK,WORK,IFAIL)
C
      IF (WANTR) CALL F02WCY(N,PT,NRPT,R,NRR,WORK(N1),WORK(N2))
C
      IF (WANTPT) CALL F02WAY(N,PT,NRPT,PT,NRPT)
C
      IFAIL = 1
      CALL F02SZF(N,SV,WORK,SV,WANTB,B,WANTR,R,NRR,N,WANTPT,PT,NRPT,N,
     *            WORK,WORK(N1),WORK(N2),IFAIL)
C
      IF (IFAIL.NE.0) GO TO 40
C
      IRANK = F02WDY(N,SV,TL)
C
      RETURN
C
   40 IFAIL = P01ABF(IERR,IFAIL,SRNAME,0,P01REC)
      RETURN
      END
