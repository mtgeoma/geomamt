      SUBROUTINE F01AHF(N,IM1,IM2,A,IA,E,Z,IZ)
C     MARK 2 RELEASE. NAG COPYRIGHT 1972
C     MARK 4 REVISED.
C     MARK 4.5 REVISED
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     TRBAK1
C     THIS SUBROUTINE PERFORMS, ON THE MATRIX OF EIGENVECTORS, Z,
C     STORED
C     IN COLUMNS IM1 TO IM2 OF THE ARRAY Z(N,N), A
C     BACKTRANSFORMATION TO
C     FORM THE EIGENVECTORS OF THE ORIGINAL SYMMETRIC MATRIX FROM
C     THE EIGENVECTORS OF THE TRIDIAGONAL MATRIX. THE NEW VECTORS
C     ARE OVERWRITTEN ON THE OLD ONES. THE SUB-DIAGONAL OF THE
C     TRIDIAGONAL MATRIX MUST BE STORED IN THE ARRAY E(N), AND
C     THE DETAILS OF THE HOUSEHOLDER REDUCTION MUST BE STORED IN
C     THE LOWER TRIANGLE OF THE ARRAY A(N,N), AS LEFT BY THE
C     SUBROUTINE F01AGF. IF Z DENOTES ANY COLUMN OF THE RESULTANT
C     MATRIX Z, THEN Z SATISFIES ZT * Z = Z(INPUT)T * Z(INPUT).
C     1ST AUGUST 1971
C
C     .. Scalar Arguments ..
      INTEGER           IA, IM1, IM2, IZ, N
C     .. Array Arguments ..
      DOUBLE PRECISION  A(IA,N), E(N), Z(IZ,IM2)
C     .. Local Scalars ..
      DOUBLE PRECISION  H, S
      INTEGER           I, J, K, L
C     .. Executable Statements ..
      IF (N.EQ.1) RETURN
      DO 80 I = 2, N
         IF (E(I).EQ.0.D0) GO TO 80
         L = I - 1
         H = E(I)*A(I,I-1)
         IF (IM1.GT.IM2) GO TO 80
         DO 60 J = IM1, IM2
            S = 0.0D0
            DO 20 K = 1, L
               S = S + A(I,K)*Z(K,J)
   20       CONTINUE
            S = S/H
            DO 40 K = 1, L
               Z(K,J) = Z(K,J) + S*A(I,K)
   40       CONTINUE
   60    CONTINUE
   80 CONTINUE
      RETURN
      END
