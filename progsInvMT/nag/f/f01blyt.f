      SUBROUTINE F01BLY(N,IR,A,IA,P,B,IB,X,IX)
C     NAG COPYRIGHT 1976. MARK  5 RELEASE.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     PDK, DNAC, NPL, TEDDINGTON. NOVEMBER 1975.
C     NPL DNAC LIBRARY SUBROUTINE STUDUT.
C
C     SOLUTIONS OF AX=B ARE DETERMINED FOR A SET OF IR RIGHT-
C     HAND SIDES, WHERE A IS A SYMMETRIC POSITIVE DEFINITE
C     MATRIX OF ORDER N, USING THE DECOMPOSITION A=U*D*U**T
C     AS FORMED BY SUBROUTINE F01BLZ. OFF-DIAGONAL ELEMENTS OF
C     THE UNIT UPPER-TRIANGULAR MATRIX U ARE SUPPLIED IN THE
C     STRICT UPPER-TRIANGLE OF A(IA,N). THE RECIPROCALS OF THE
C     DIAGONAL ELEMENTS IN THE MATRIX D ARE SUPPLIED IN P(N).
C     THE TRANSPOSED MATRIX OF THE RIGHT-HAND SIDES IS STORED
C     IN THE ARRAY B(IB,IR) AND THE CORRESPONDING IR SOLUTIONS
C     ARE OBTAINED, BY A BACKWARD AND FORWARD SUBSTITUTION,
C     AS THE ROW VECTORS OF THE ARRAY X(IX,IR). WHEN THE
C     SUBROUTINE IS CALLED, THE SAME ACTUAL PARAMETER MAY BE
C     USED FOR BOTH THE FORMAL PARAMETERS B AND X, SO THAT X
C     MAY BE OVERWRITTEN ON B.
C
C
C     SOLUTION FOR A TRANSPOSED SET OF R.H.S.
C     FOLLOWING UDUT DECOMPOSITION.
C
C     .. Scalar Arguments ..
      INTEGER           IA, IB, IR, IX, N
C     .. Array Arguments ..
      DOUBLE PRECISION  A(IA,N), B(IB,N), P(N), X(IX,N)
C     .. Local Scalars ..
      DOUBLE PRECISION  Y
      INTEGER           I, II, IM1, IP1, J, K, KK
C     .. Executable Statements ..
      DO 140 J = 1, IR
C
C        SOLUTION OF U**T*Y=B**T
C
         DO 60 II = 1, N
            I = N + 1 - II
            Y = B(J,I)
            IF (I.EQ.N) GO TO 40
            IP1 = I + 1
            DO 20 K = IP1, N
               Y = Y - A(I,K)*X(J,K)
   20       CONTINUE
   40       X(J,I) = Y
   60    CONTINUE
C
C        SOLUTION OF DLX=Y**T
C
         DO 120 I = 1, N
            Y = X(J,I)*P(I)
            IF (I.EQ.1) GO TO 100
            IM1 = I - 1
            DO 80 KK = 1, IM1
               K = I - KK
               Y = Y - A(K,I)*X(J,K)
   80       CONTINUE
  100       X(J,I) = Y
  120    CONTINUE
  140 CONTINUE
      RETURN
      END
