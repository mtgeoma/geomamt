      SUBROUTINE F04AYF(N,IR,A,IA,P,B,IB,IFAIL)
C     MARK 14 RE-ISSUE.  NAG COPYRIGHT 1989.
C     MARK 17 REVISED. IER-1675 (JUN 1995).
C
C     CALCULATES THE APPROXIMATE SOLUTION OF A SET OF REAL LINEAR
C     EQUATIONS WITH MULTIPLE RIGHT HAND SIDES  A * X = B  AFTER
C     A  HAS BEEN DECOMPOSED BY F01BTF.
C     THE ROUTINE OPERATES ON BLOCKS OF CONSECUTIVE RIGHT HAND SIDES
C     FOR EFFICIENCY ON A PAGED MACHINE.
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='F04AYF')
C     .. Scalar Arguments ..
      INTEGER           IA, IB, IFAIL, IR, N
C     .. Array Arguments ..
      DOUBLE PRECISION  A(IA,*), B(IB,IR), P(*)
C     .. Local Scalars ..
      DOUBLE PRECISION  HALF, ONE, X
      INTEGER           J, K1, K2, L, LACT, LBLK
C     .. Local Arrays ..
      CHARACTER         P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF, X02CAZ
      EXTERNAL          P01ABF, X02CAZ
C     .. External Subroutines ..
      EXTERNAL          DGER, DSCAL, DSWAP
C     .. Intrinsic Functions ..
      INTRINSIC         MAX, MIN
C     .. Data statements ..
      DATA              HALF/0.5D0/, ONE/1.0D0/
C     .. Executable Statements ..
C
      IF (N.LE.0 .OR. IA.LT.N .OR. IB.LT.N) GO TO 80
C
C     LBLK IS THE NUMBER OF RIGHT HAND SIDES THAT ARE TREATED
C     TOGETHER.
C     LBLK IS CHOSEN SO THAT THERE IS ROOM IN THE DESIRED ACTIVE SET
C     FOR LBLK CONTIGUOUS COLUMNS OF B, ANOTHER COLUMN ON ITS OWN,
C     AND THE ARRAY P.
C
      LACT = X02CAZ(X)
      LBLK = IR
      IF (LACT.NE.0) LBLK = MAX(1,LACT/IB-2)
C
C     MAIN LOOP IN WHICH A BLOCK OF RIGHT HAND SIDES IS TREATED.
C     THE ACTIVE BLOCK CONSISTS OF COLUMNS K1 TO K2.
C     THE COLUMNS OF A SHOULD BE PAGED IN AT MOST ONCE DURING THE
C     FORWARD SUBSTITUTION AND ONCE DURING THE BACKWARD SUBSTITUTION
C     IN THIS LOOP.
C
      DO 60 K1 = 1, IR, LBLK
         K2 = MIN(K1+LBLK-1,IR)
C        FORWARD SUBSTITUTION
         DO 20 J = 1, N
            L = P(J) + HALF
            IF (L.NE.J) CALL DSWAP(K2-K1+1,B(J,K1),IB,B(L,K1),IB)
            CALL DSCAL(K2-K1+1,ONE/A(J,J),B(J,K1),IB)
            IF (J.LT.N) CALL DGER(N-J,K2-K1+1,-ONE,A(J+1,J),1,B(J,K1),
     *                            IB,B(J+1,K1),IB)
   20    CONTINUE
C        BACKWARD SUBSTITUTION
         DO 40 J = N, 1, -1
            CALL DGER(J-1,K2-K1+1,-ONE,A(1,J),1,B(J,K1),IB,B(1,K1),IB)
   40    CONTINUE
   60 CONTINUE
C
C     SUCCESSFUL EXIT
C
      IFAIL = 0
      GO TO 100
C
C     ERROR EXIT
C
   80 IFAIL = P01ABF(IFAIL,1,SRNAME,0,P01REC)
  100 RETURN
      END
