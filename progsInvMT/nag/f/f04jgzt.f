      SUBROUTINE F04JGZ(M,N,SVD,IRANK,B,A,NRA,SV,LSV,X,SIGMA,WORK,IFAIL)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 9 REVISED. IER-330 (SEP 1981).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     WRITTEN BY S. HAMMARLING, MIDDLESEX POLYTECHNIC (LSQSVD)
C
C     F04JGZ RETURNS THE N ELEMENT VECTOR X, OF MINIMAL
C     LENGTH, THAT MINIMIZES THE EUCLIDEAN LENGTH OF THE M
C     ELEMENT VECTOR R GIVEN BY
C
C     R = B-A*X ,
C
C     WHERE B IS AN M ELEMENT VECTOR AND A IS AN M*N (M.GE.N)
C     MATRIX, FOLLOWING EITHER A HOUSEHOLDER QU FACTORIZATION
C     OF A OR A SINGULAR VALUE DECOMPOSITION (SVD) OF A GIVEN
C     BY
C
C     A = Q*(U)     OR     A = Q1*(D)*(P**T) ,
C           (0)                   (0)
C
C     WHERE Q, Q1 AND P ARE ORTHOGONAL, U IS A NON-SINGULAR UPPER
C     TRIANGULAR MATRIX AND D IS A DIAGONAL MATRIX WITH
C     NON-NEGATIVE DIAGONAL ELEMENTS, THESE BEING THE SINGULAR
C     VALUES OF A.
C     ROUTINE F02WDF MAY BE USED TO RETURN THE APPROPRIATE
C     FACTORIZATION OF A.
C
C     INPUT PARAMETERS.
C
C     M     - NUMBER OF ROWS OF A. M MUST BE AT LEAST N.
C
C     N     - NUMBER OF COLUMNS OF A. N MUST BE AT LEAST UNITY.
C
C     SVD   - MUST BE .TRUE. IF THE SVD OF A IS TO BE USED
C             TO FIND X. IF IRANK IS LESS THAN N THEN SVD
C             MUST BE .TRUE..
C             SVD MUST BE .FALSE. IF THE QU FACTORIZATION
C             IS TO BE USED TO FIND X. IN THIS CASE IRANK
C             MUST BE N.
C
C     IRANK - THE RANK OF A.
C             IRANK MUST BE AT LEAST ZERO AND MUST NOT BE
C             LARGER THAN N.
C
C     B     - IF SVD IS .TRUE. THEN B MUST CONTAIN THE M
C             ELEMENT VECTOR (Q1**T)*B.
C             IF SVD IS .FALSE. THEN B MUST CONTAIN THE M
C             ELEMENT VECTOR (Q**T)*B.
C
C     A     - IF SVD IS .TRUE. THEN THE IRANK*N PART OF A
C             MUST CONTAIN THE FIRST IRANK ROWS OF THE
C             MATRIX P**T.
C             IF SVD IS .FALSE. THEN THE N*N UPPER
C             TRIANGULAR PART OF A MUST CONTAIN THE UPPER
C             TRIANGULAR MATRIX U. U MUST BE NON-SINGULAR.
C
C     NRA   - ROW DIMENSION OF A AS DECLARED IN THE CALLING PROGRAM.
C             NRA MUST BE AT LEAST MAX(1,IRANK).
C
C     SV    - IF SVD IS .TRUE. THEN SV MUST BE AN IRANK
C             ELEMENT VECTOR CONTAINING THE NON-NEGLIGIBLE
C             SINGULAR VALUES OF A.
C             IF SVD IS .FALSE. THEN SV IS NOT REFERENCED.
C
C     LSV   - LSV MUST BE AT LEAST MAX(1,IRANK).
C
C     IFAIL - THE USUAL FAILURE PARAMETER. IF IN DOUBT SET IFAIL TO
C             ZERO BEFORE CALLING F04JGZ.
C
C     OUTPUT PARAMETERS.
C
C     X     - THE N ELEMENT SOLUTION VECTOR.
C             THE ROUTINE MAY BE CALLED WITH X=B OR WITH X=SV.
C
C     SIGMA - IF M IS GREATER THAN IRANK THEN SIGMA WILL CONTAIN THE
C             STANDARD ERROR GIVEN BY
C             SIGMA=L(R)/SQRT(M-IRANK), WHERE L(R) DENOTES
C             THE EUCLIDEAN LENGTH OF THE RESIDUAL VECTOR
C             R. IF M=IRANK THEN SIGMA IS RETURNED AS ZERO.
C
C     IFAIL - ON NORMAL RETURN IFAIL WILL BE ZERO.
C             IF AN INPUT PARAMETER IS INCORRECTLY SUPPLIED
C             THEN IFAIL IS RETURNED AS UNITY. THIS FAILURE
C             INCLUDES THE CASE WHERE SVD IS SUPPLIED AS
C             .FALSE. BUT OVERFLOW OCCURS IN SOLVING THE
C             UPPER TRIANGULAR EQUATIONS.
C
C     WORKSPACE PARAMETERS.
C
C     WORK  - A MAX(1,IRANK) ELEMENT VECTOR.
C             IF THE ROUTINE IS NOT CALLED WITH X=B THEN IT MAY BE
C             CALLED WITH WORK=B. SIMILARLY IF THE ROUTINE
C             IS NOT CALLED WITH X=SV THEN IT MAY BE CALLED
C             WITH WORK=SV.
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  SIGMA
      INTEGER           IFAIL, IRANK, LSV, M, N, NRA
      LOGICAL           SVD
C     .. Array Arguments ..
      DOUBLE PRECISION  A(NRA,N), B(M), SV(LSV), WORK(LSV), X(N)
C     .. Local Scalars ..
      INTEGER           IRP1, K, MMIR
C     .. External Functions ..
      DOUBLE PRECISION  F04JGW
      EXTERNAL          F04JGW
C     .. External Subroutines ..
      EXTERNAL          F04JAY, F04JGY
C     .. Intrinsic Functions ..
      INTRINSIC         MAX, DBLE, SQRT
C     .. Executable Statements ..
      IFAIL = 1
C
      K = MAX(1,IRANK)
      IF (M.LT.N .OR. N.LT.1 .OR. IRANK.LT.0 .OR. IRANK.GT.N .OR.
     *    NRA.LT.K .OR. LSV.LT.K) GO TO 60
C
      SIGMA = 0.0D0
      IF (IRANK.EQ.M) GO TO 20
      IRP1 = IRANK + 1
      MMIR = M - IRANK
C
      SIGMA = F04JGW(MMIR,B(IRP1))/SQRT(DBLE(MMIR))
C
   20 IF ( .NOT. SVD) GO TO 40
C
      CALL F04JAY(N,IRANK,SV,LSV,B,A,NRA,X,WORK)
C
      IFAIL = 0
      RETURN
C
   40 CALL F04JGY(N,A,NRA,B,X,IFAIL)
C
      IF (IFAIL.EQ.0) RETURN
C
      IFAIL = 1
   60 RETURN
      END
