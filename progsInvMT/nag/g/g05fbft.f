      SUBROUTINE G05FBF(A,N,X)
C     MARK 14 RELEASE. NAG COPYRIGHT 1989.
C
C     Returns a vector of N pseudo-random numbers from a (negative)
C     exponential distribution with mean A.
C
C     Pseudo-random numbers are generated by the auxiliary routine
C     G05CAY, 63 at a time, and stored in the array RV in common block
C     CG05CA. G05FBF copies numbers from the array RV into X,
C     transforming them to the exponential distribution, and calling
C     G05CAY to replenish RV when necessary.
C
C     A call of G05FBF returns the same sequence of pseudo-random
C     numbers as N consecutive calls to G05DBF.
C
C     Jeremy Du Croz, NAG Ltd, June 1989.
C
C     .. Parameters ..
      INTEGER           LV
      PARAMETER         (LV=63)
C     .. Scalar Arguments ..
      DOUBLE PRECISION  A
      INTEGER           N
C     .. Array Arguments ..
      DOUBLE PRECISION  X(N)
C     .. Scalars in Common ..
      INTEGER           KV
C     .. Arrays in Common ..
      DOUBLE PRECISION  RV(LV)
C     .. Local Scalars ..
      DOUBLE PRECISION  T
      INTEGER           I, I1, I2, IV
      LOGICAL           INIT
C     .. External Subroutines ..
      EXTERNAL          G05CAY, G05CAZ
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, LOG, MIN
C     .. Common blocks ..
      COMMON            /CG05CA/RV, KV
C     .. Save statement ..
      SAVE              /CG05CA/, INIT
C     .. Data statements ..
      DATA              INIT/.TRUE./
C     .. Executable Statements ..
      IF (N.LE.0) RETURN
C
C     Ensure that the basic generator has been initialized
C
      IF (INIT) CALL G05CAZ(INIT)
C
C     Replenish the buffer if necessary
C
      IF (KV.GE.LV) CALL G05CAY(.FALSE.)
C
C     Fill the array X in segments: I1 and I2 are indices of first and
C     last elements in current segment
C
      T = -ABS(A)
      I1 = 1
      I2 = -KV
   20 CONTINUE
      I2 = MIN(I2+LV,N)
      IV = KV - I1 + 1
C
C     Generate values for the current segment of X. This loop should
C     be vectorizable; its length is at most LV.
C
CDIR$ SHORTLOOP
      DO 40 I = I1, I2
         X(I) = T*LOG(RV(I+IV))
   40 CONTINUE
      KV = I2 + IV
C
      IF (I2.GE.N) RETURN
C
C     Replenish buffer and go back to generate next segment
C
      CALL G05CAY(.FALSE.)
      I1 = I2 + 1
      GO TO 20
      END
