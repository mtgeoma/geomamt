      SUBROUTINE G02CHF(NCASES,NVARS,NIND,SSPZ,ISSPZ,RZ,IRZ,RESULT,
     *                  COEFF,ICOEFF,RZINV,IRZINV,CZ,ICZ,WKZ,IWKZ,IFAIL)
C     MARK 4 RELEASE NAG COPYRIGHT 1974.
C     MARK 4.5 REVISED
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     NAG SUBROUTINE G02CHF
C     WRITTEN  7.10.73 BY PAUL GRIFFITHS (OXFORD UNIVERSITY)
C
C     PERFORMS A MULTIPLE LINEAR REGRESSION WITH NO CONSTANT ON THE
C     SET OF VARIABLES WHOSE SUMS OF SQUARES AND CROSS-PRODUCTS
C     ABOUT ZERO AND CORRELATION-LIKE COEFFICIENTS ARE GIVEN, WHEN
C     THE CORRELATION-LIKE MATRIX RZ IS POSITIVE-DEFINITE.
C
C     USES NAG ERROR ROUTINE P01AAF
C     NAG LIBRARY ROUTINE    F04ABF
C     AND AUXILIARY ROUTINE  G02CGZ
C
C
C     ABOVE DATA STATEMENT MAY BE MACHINE-DEPENDENT -- DEPENDS ON
C     NUMBER OF CHARACTERS WHICH CAN BE STORED IN A REAL VARIABLE
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='G02CHF')
C     .. Scalar Arguments ..
      INTEGER           ICOEFF, ICZ, IFAIL, IRZ, IRZINV, ISSPZ, IWKZ,
     *                  NCASES, NIND, NVARS
C     .. Array Arguments ..
      DOUBLE PRECISION  COEFF(ICOEFF,3), CZ(ICZ,NIND), RESULT(13),
     *                  RZ(IRZ,NVARS), RZINV(IRZINV,NIND),
     *                  SSPZ(ISSPZ,NVARS), WKZ(IWKZ,NIND)
C     .. Local Scalars ..
      INTEGER           I, IERROR, J
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          F04ABF, G02CGZ
C     .. Executable Statements ..
      IERROR = 0
      IF (ISSPZ.LT.NVARS .OR. IRZ.LT.NVARS .OR. ICOEFF.LT.NIND .OR.
     *    IRZINV.LT.NIND .OR. ICZ.LT.NIND .OR. IWKZ.LT.NIND) IERROR = 4
      IF (NCASES.LT.NVARS) IERROR = 3
      IF (NVARS.NE.(NIND+1)) IERROR = 2
      IF (NVARS.LT.2) IERROR = 1
      IF (IERROR) 20, 20, 140
C
C     SET UP IDENTITY MATRIX IN CZ
C
   20 DO 60 J = 1, NIND
         DO 40 I = 1, NIND
            CZ(I,J) = 0.0D0
   40    CONTINUE
         CZ(J,J) = 1.0D0
   60 CONTINUE
C
C     CALL POSITIVE-DEFINITE ACCURATE SOLUTION OF LINEAR EQUATIONS
C     ROUTINE TO SOLVE  RZ*RZINV=I=CZ  WITH SOFT FAIL OPTION
C
      IERROR = 1
      CALL F04ABF(RZ,IRZ,CZ,ICZ,NIND,NIND,RZINV,IRZINV,COEFF,WKZ,IWKZ,
     *            IERROR)
C
C     RE-CONSTITUTE LOWER TRIANGLE OF ORIGINAL MATRIX WHICH HAS
C     BEEN ALTERED BY F04ABF
C
      DO 100 J = 1, NIND
         DO 80 I = J, NIND
            RZ(I,J) = RZ(J,I)
   80    CONTINUE
  100 CONTINUE
C
C     BRANCH IF ERROR OCCURRED IN F04ABF
C
      IF (IERROR.NE.0) GO TO 120
C
C     GET G02CGZ TO DO ALL THE HARD WORK
C
      CALL G02CGZ(NCASES,NVARS,NIND,SSPZ,ISSPZ,RZ,IRZ,RESULT,COEFF,
     *            ICOEFF,RZINV,IRZINV,CZ,ICZ)
C
C     SORT OUT ALL THE EXITS
C
      IFAIL = 0
      RETURN
  120 IERROR = IERROR + 4
  140 IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
      RETURN
      END
