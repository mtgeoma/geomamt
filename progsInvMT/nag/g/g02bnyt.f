      SUBROUTINE G02BNY(X,N,NV,IP,IPLOW,Y,CORRKT,CORRSP)
C     MARK 4 RELEASE NAG COPYRIGHT 1974.
C     MARK 4.5 REVISED
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     NAG AUXILIARY SUBROUTINE G02BNY
C     WRITTEN 27. 7.73 BY PAUL GRIFFITHS (OXFORD UNIVERSITY)
C
C     ROUTINE IS USED TO ASSIGN RANKS TO OBSERVATIONS AND CALCULATE
C     CORRECTIONS FOR TIES FOR THE RANK CORRELATION ROUTINES --
C     NAMELY G02BQF,G02BRF,G02BSF
C
C     ARRAY X CONTAINS THE ACTUAL OBSERVATIONS
C     N IS THE NUMBER OF OBSERVATIONS
C     NV IS THE NUMBER OF VALID OBSERVATIONS
C     IP POINTS TO THE NEXT LARGEST ELEMENT OF X (FORMED BY G02BNZ)
C     IPLOW IS POINTER FOR SMALLEST ELEMENT IN X (FORMED BY G02BNZ)
C     Y IS ASSIGNED THE RANKS OF THE N OBSERVATIONS, ALLOWING FOR
C     TIES
C     CORRKT IS THE APPROPRIATE CORRECTION FACTOR FOR TIES FOR THE
C     CALCULATION OF KENDALL'S TAU
C     CORRSP IS THE APPROPRIATE CORRECTION FACTOR FOR TIES FOR THE
C     CALCULATION OF SPEARMAN'S R
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  CORRKT, CORRSP
      INTEGER           IPLOW, N, NV
C     .. Array Arguments ..
      DOUBLE PRECISION  X(N), Y(N)
      INTEGER           IP(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  FN, RANK, TIES, XTIES
      INTEGER           K1, K2, KHIGH, KLOW
C     .. Intrinsic Functions ..
      INTRINSIC         DBLE, INT
C     .. Executable Statements ..
      FN = DBLE(NV)
      RANK = 0.0D0
      CORRKT = FN*(FN-1.0D0)/2.0D0
      CORRSP = CORRKT*(FN+1.0D0)
      K2 = IPLOW
   20 K1 = K2
      K2 = IP(K1)
      RANK = RANK + 1.0D0
      IF (X(K2)-X(K1)) 40, 60, 40
   40 Y(K1) = RANK
      GO TO 20
   60 KLOW = K1
      TIES = 1.0D0
   80 IF (K2.EQ.K1) GO TO 100
      K1 = K2
      K2 = IP(K1)
      TIES = TIES + 1.0D0
      IF (X(K2)-X(K1)) 100, 80, 100
  100 KHIGH = INT(TIES+0.5D0)
      XTIES = (TIES-1.0D0)/2.0D0
      RANK = RANK + XTIES
      DO 120 K1 = 1, KHIGH
         Y(KLOW) = RANK
         KLOW = IP(KLOW)
  120 CONTINUE
      RANK = RANK + XTIES
      XTIES = TIES*XTIES
      CORRKT = CORRKT - XTIES
      CORRSP = CORRSP - XTIES*(TIES+1.0D0)
      IF (RANK.LT.(FN-0.25D0)) GO TO 20
      RETURN
      END
