      SUBROUTINE G13CDF(NXY,MTXY,PXY,MW,IS,PW,L,KC,XG,YG,NG,IFAIL)
C     MARK 10 RELEASE. NAG COPYRIGHT 1982.
C     MARK 10B REVISED. IER-406 (JAN 1983).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     G13CDF CALCULATES THE SMOOTHED SAMPLE CROSS SPECTRUM
C     OF A BIVARIATE TIME SERIES USING SPECTRAL SMOOTHING
C     BY THE TRAPEZIUM FREQUENCY (DANIELL) WINDOW.
C
C     CONTRIBUTORS - G. TUNNICLIFFE WILSON,M. HURLEY (LANC. UNIV.)
C     VALIDATOR    - T. LAMBERT ( NAG CENTRAL OFFICE )
C
C     USES NAG LIBRARY ROUTINES G13CCZ, P01AAF
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='G13CDF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  PW, PXY
      INTEGER           IFAIL, IS, KC, L, MTXY, MW, NG, NXY
C     .. Array Arguments ..
      DOUBLE PRECISION  XG(KC), YG(KC)
C     .. Local Scalars ..
      INTEGER           IERROR
C     .. Local Arrays ..
      DOUBLE PRECISION  CXY(1), CYX(1)
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          G13CCZ
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, MOD
C     .. Executable Statements ..
      IERROR = 1
C
C     CHECK THE DATA SET SIZE NON ZERO
      IF (NXY.LT.1) GO TO 40
C     CHECK MEAN-TREND CORRECTION INDICATOR
      IF (MTXY.LT.0 .OR. MTXY.GT.2) GO TO 40
C     CHECK THE DATA WINDOW PROPORTION
      IF (PXY.LT.0.0D0 .OR. PXY.GT.1.0D0) GO TO 40
C     CHECK FREQUENCY WIDTH OF SMOOTHING WINDOW
      IF (MW.LT.1 .OR. MW.GT.NXY) GO TO 40
      IF (MW.EQ.NXY) GO TO 20
C     CHECK WINDOW SHAPE PROPORTION
      IF (PW.LT.0.0D0 .OR. PW.GT.1.0D0) GO TO 40
C     CHECK FREQ OF DIVISION OF SMOOTHED SPECTRUM ESTS
   20 IF (L.LT.1) GO TO 40
C     CHECK ALIGNMENT SHIFT
      IF (ABS(IS).GE.L) GO TO 40
C     CHECK ORDER OF TRANSFORM
      IERROR = 2
      IF (KC.LT.(2*NXY) .OR. MOD(KC,L).NE.0) GO TO 40
C
C     IF THIS POINT IS REACHED USER SUPPLIED
C     PARAMETERS HAVE PASSED INITIAL CHECKS
      IERROR = 0
      IF (MW.LT.NXY) CXY(1) = PW
      IF (MW.LT.NXY) CYX(1) = PW
      CALL G13CCZ(NXY,MTXY,PXY,5,MW,IS,0,1,CXY,CYX,KC,L,KC,XG,YG,NG,
     *            IERROR)
      IF (IERROR.NE.0) GO TO 40
C
C     SUCCESSFUL EXIT
      IFAIL = 0
      RETURN
C
C     COME HERE IF ERRORS
   40 IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
      RETURN
      END
