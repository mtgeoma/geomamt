      SUBROUTINE G02CGF(NCASES,NVARS,NIND,XBAR,SSP,ISSP,R,IR,RESULT,
     *                  COEFF,ICOEFF,CONST,RINV,IRINV,C,IC,WKZ,IWKZ,
     *                  IFAIL)
C     MARK 4 RELEASE NAG COPYRIGHT 1974.
C     MARK 4.5 REVISED
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     NAG SUBROUTINE G02CGF
C     WRITTEN  7.10.73 BY PAUL GRIFFITHS (OXFORD UNIVERSITY)
C
C     PERFORMS A MULTIPLE LINEAR REGRESSION ON THE SET OF VARIABLES
C     WHOSE MEANS, SUMS OF SQUARES AND CROSS-PRODUCTS OF DEVIATIONS
C     FROM MEANS, AND PEARSON PRODUCT-MOMENT CORRELATION
C     COEFFICIENTS
C     ARE GIVEN, WHEN THE CORRELATION MATRIX R IS
C     POSITIVE-DEFINITE.
C
C     USES NAG ERROR ROUTINE P01AAF
C     NAG LIBRARY ROUTINE    F04ABF
C     AND AUXILIARY ROUTINES G02CGZ
C     G02CGY
C
C
C     ABOVE DATA STATEMENT MAY BE MACHINE-DEPENDENT -- DEPENDS ON
C     NUMBER OF CHARACTERS WHICH CAN BE STORED IN A REAL VARIABLE
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='G02CGF')
C     .. Scalar Arguments ..
      INTEGER           IC, ICOEFF, IFAIL, IR, IRINV, ISSP, IWKZ,
     *                  NCASES, NIND, NVARS
C     .. Array Arguments ..
      DOUBLE PRECISION  C(IC,NIND), COEFF(ICOEFF,3), CONST(3),
     *                  R(IR,NVARS), RESULT(13), RINV(IRINV,NIND),
     *                  SSP(ISSP,NVARS), WKZ(IWKZ,NIND), XBAR(NVARS)
C     .. Local Scalars ..
      INTEGER           I, IERROR, J, NTOT
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          F04ABF, G02CGY, G02CGZ
C     .. Executable Statements ..
      IERROR = 0
      IF (ISSP.LT.NVARS .OR. IR.LT.NVARS .OR. ICOEFF.LT.NIND .OR.
     *    IRINV.LT.NIND .OR. IC.LT.NIND .OR. IWKZ.LT.NIND) IERROR = 4
      IF (NCASES.LE.NVARS) IERROR = 3
      IF (NVARS.NE.(NIND+1)) IERROR = 2
      IF (NVARS.LT.2) IERROR = 1
      IF (IERROR) 20, 20, 140
C
C     SET UP IDENTITY MATRIX IN C
C
   20 DO 60 J = 1, NIND
         DO 40 I = 1, NIND
            C(I,J) = 0.0D0
   40    CONTINUE
         C(J,J) = 1.0D0
   60 CONTINUE
C
C     CALL POSITIVE-DEFINITE ACCURATE SOLUTION OF LINEAR EQUATIONS
C     ROUTINE TO SOLVE  R*RINV=I=C  WITH SOFT FAIL OPTION
C
      IERROR = 1
      CALL F04ABF(R,IR,C,IC,NIND,NIND,RINV,IRINV,COEFF,WKZ,IWKZ,IERROR)
C
C     RE-CONSTITUTE LOWER TRIANGLE OF ORIGINAL MATRIX WHICH HAS
C     BEEN ALTERED BY F04ABF
C
      DO 100 J = 1, NIND
         DO 80 I = J, NIND
            R(I,J) = R(J,I)
   80    CONTINUE
  100 CONTINUE
C
C     BRANCH IF ERROR OCCURRED IN F04ABF
C
      IF (IERROR.NE.0) GO TO 120
C
C     GET G02CGZ TO DO ALL THE HARD WORK
C
      NTOT = NCASES - 1
      CALL G02CGZ(NTOT,NVARS,NIND,SSP,ISSP,R,IR,RESULT,COEFF,ICOEFF,
     *            RINV,IRINV,C,IC)
C
C     NOW CALCULATE THREE ITEMS RELATING TO THE REGRESSION CONSTANT
C     USING G02CGY
C
      CALL G02CGY(NCASES,NVARS,NIND,XBAR,RESULT(10)
     *            ,COEFF,ICOEFF,CONST,C,IC)
C
C     SORT OUT ALL THE EXITS
C
      IFAIL = 0
      RETURN
  120 IERROR = IERROR + 4
  140 IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
      RETURN
      END
