      SUBROUTINE G08DAZ(VAL,IX,K,N,RANK,T)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     .. Scalar Arguments ..
      DOUBLE PRECISION  T
      INTEGER           IX, K, N
C     .. Array Arguments ..
      DOUBLE PRECISION  RANK(IX,N), VAL(IX,N)
C     .. Local Scalars ..
      DOUBLE PRECISION  RMAX, RNXT, RTHS, V, VMAX
      INTEGER           I, KS, NTIE
C     .. Intrinsic Functions ..
      INTRINSIC         DBLE
C     .. Executable Statements ..
      T = 0.D0
      DO 40 KS = 1, K
         DO 20 I = 1, N
            RANK(KS,I) = 0.D0
   20    CONTINUE
   40 CONTINUE
C     INITIAL VALUES ARE SET UP
C     - RNXT(THE RANK TO BE ALLOCATED ON THE NEXT PASS)
C           IS SET TO 1
C     - RMAX(THE MAXIMUM FOUND ON THE LAST PASS,TO
C           WHICH THE RANK RTHS IS TO BE GIVEN) IS SET
C           TO 0
C     - RTHS(THE RANK TO BE ASSIGNED TO ELEMENTS OF VAL
C           WHICH CONTAIN RMAX) IS SET TO 0
C     THIS WILL RE-ASSIGN THE RANK OF ZERO TO ANY ELEMENT
C     OF VAL WHICH CONTAINS ZERO,ON THE FIRST PASS
C     THESE RANKS WILL BE RESET TO THEIR CORRECT VALUES ON
C     A LATER PASS
C
C     FIRST SET ALL RANKS TO ZERO
      DO 160 KS = 1, K
         RNXT = 1.D0
         RMAX = 0.D0
         RTHS = 0.D0
C        MAIN LOOP - SET VMAX(MAXIMUM VALUE NOT ALREADY RANKED)
C        TO ZERO AND NTIE(THE NUMBER OF OCCURENCES
C        OF THIS MAXIMUM) TO ZERO
   60    VMAX = 0.D0
         NTIE = 0
C        DO LOOP TO SET UP THE RANKS OF THE MAXIMUM OF THE LAST
C        PASS AND OBTAIN THE MAXIMUM OF THE UNRANKED NUMBERS,WITH
C        THE NUMBER OF OCCURENCES OF THIS MAXIMUM
         DO 140 I = 1, N
            IF (RANK(KS,I).GT.0.D0) GO TO 140
            V = VAL(KS,I)
            IF (V.EQ.RMAX) GO TO 100
            IF (V-VMAX) 140, 80, 120
   80       NTIE = NTIE + 1
            GO TO 140
  100       RANK(KS,I) = RTHS
            GO TO 140
  120       VMAX = V
            NTIE = 1
  140    CONTINUE
C        END OF PASS - RETURN TO FIND THE NEXT HIGHEST NUMBER
C        AND ASSIGN THE RANK TO THE NUMBER JUST FOUND
C        NO NEW MAX FOUND - END RANKING PROCEDURE
         IF (NTIE.LT.1) GO TO 160
         T = T + DBLE(NTIE*(NTIE*NTIE-1))/12.0D0
         RTHS = RNXT + 0.5D0*DBLE(NTIE-1)
         RNXT = RNXT + DBLE(NTIE)
         RMAX = VMAX
         GO TO 60
  160 CONTINUE
      RETURN
      END
