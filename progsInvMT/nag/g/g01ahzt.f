      SUBROUTINE G01AHZ(X,NOBS,XBAR,XSS)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     G01AHZ IS AN AUXILIARY ROUTINE INTENDED FOR GENERAL USE,
C     WHICH COMPUTES THE MEAN AND TOTAL SS ABOUT THE MEAN
C     FOR A VECTOR OF REAL NUMBERS.
C     IT IS CALLED DIRECTLY BY G01AHF.
C     ON INPUT,  X(NOBS) IS AN ARRAY OF NOBS REAL NUMBERS
C     ON OUTPUT, XBAR IS THE MEAN VALUE
C     XSS IS THE TOTAL SUM OF SQUARES ABOUT THE MEAN
C     .. Scalar Arguments ..
      DOUBLE PRECISION  XBAR, XSS
      INTEGER           NOBS
C     .. Array Arguments ..
      DOUBLE PRECISION  X(NOBS)
C     .. Local Scalars ..
      DOUBLE PRECISION  FI, XI, XX
      INTEGER           I
C     .. Executable Statements ..
      FI = 1.0D0
      XSS = 0.0D0
      XBAR = X(1)
      IF (NOBS.EQ.1) GO TO 40
      DO 20 I = 2, NOBS
         XI = X(I)
         FI = FI + 1.0D0
         XX = XI - XBAR
         XSS = XSS + XX*XX*((FI-1.0D0)/FI)
         XBAR = XBAR + XX/FI
   20 CONTINUE
   40 RETURN
      END
