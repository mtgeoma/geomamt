      SUBROUTINE G13AJZ(MR,PAR,NPAR,C,X,NX,EX,EXR,AL,NEX,G,H,S,ST,IDST,
     *                  NST,WA,IDWA,NGH,NGHP,MVC,IERROR)
C     MARK 10 RELEASE. NAG COPYRIGHT 1982.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     G13AJZ DERIVES THE STATE SET FOR A UNIVARIATE ARIMA TIME
C     SERIES MODEL, GIVEN THE SERIES AND THE MODEL PARAMETERS.
C     THE STATE SET IS THE MINIMUM AMOUNT OF INFORMATION
C     REQUIRED FOR FORECASTING.
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  C, S
      INTEGER           IDST, IDWA, IERROR, NEX, NGH, NGHP, NPAR, NST,
     *                  NX
C     .. Array Arguments ..
      DOUBLE PRECISION  AL(NEX), EX(NEX), EXR(NEX), G(NGH), H(NGHP,NGH),
     *                  PAR(NPAR), ST(IDST), WA(IDWA), X(NX)
      INTEGER           MR(7), MVC(4)
C     .. Local Scalars ..
      DOUBLE PRECISION  EPS, FAC, SM, TF, U, ZERO
      INTEGER           I, IERR, IFAILQ, IZ, J, K, KQ, KSCH, KWBE, KWBQ,
     *                  KWBQ2, KWEX, KWPA, KWPH, KWSD, KWSP, KWST, KWTH,
     *                  LEX, NAQ, NB, NBQ, ND, NDD, NDS, NP, NPAR1, NPD,
     *                  NPS, NQ, NQD, NQS, NS
C     .. Local Arrays ..
      INTEGER           MPQS(4)
C     .. External Functions ..
      DOUBLE PRECISION  X02AJF
      EXTERNAL          X02AJF
C     .. External Subroutines ..
      EXTERNAL          F04ASF, G13AAF, G13AET, G13AEU, G13AEV, G13AEY,
     *                  G13AEZ, G13AJY
C     .. Data statements ..
      DATA              U/1.0D0/, ZERO/0.0D0/, FAC/10.0D0/, TF/1000.0D0/
C     .. Executable Statements ..
      S = ZERO
C
C     EXTRACT ORDERS AND GENERALISED ORDERS FROM MR ARRAY
C
      CALL G13AJY(MR,NP,ND,NQ,NPS,NDS,NQS,NS,NPD,NDD,NQD,MPQS,NPAR1)
C
C     CALCULATE THE LENGTH OF THE EXTENDED DIFFERENCED SERIES
C     AND THE STATE SET
C
      LEX = NX - NDD + NQD
      NST = NQD + NDD + (NPS*NS)
      IF (NP.LT.(NQS*NS)) GO TO 20
      NST = NPD + NDD + NQ
   20 IERROR = 4
      IF (IDST.LT.NST) GO TO 340
      DO 40 I = 1, IDWA
         WA(I) = ZERO
   40 CONTINUE
C
C     SM AND EPS ARE FUNCTIONS OF THE SMALLEST VALUE DISTINGUISHABLE
C     BY THE COMPUTER
C
      SM = X02AJF()
      EPS = SM*TF
      SM = SM*2.0D0*FAC
      IERROR = 1
      IFAILQ = 1
C
C     FORM DIFFERENCED VALUE AND STORE IN EX
C
      CALL G13AAF(X,NX,ND,NDS,NS,EX,KQ,IFAILQ)
      IF (IFAILQ.NE.0) GO TO 340
      IERROR = 0
C
C     CALCULATE THE DIMENSIONS OF THE AQ,BETA AND BQ ARRAYS
C
      NAQ = NEX*2
      NB = NPD + 1
      NBQ = NB*2
C
C     CALCULATE THE START POINTS OF ALL THE ARRAYS (OTHER THAN AQ,
C     THE FIRST) WHICH MAKE UP THE WORKING ARRAY
C     I.E.BQ,BETA,EXQ,PHI,THETA,SPHI,STHETA,PA AND SD
C
      KWBQ = 1 + NAQ
      KWBE = KWBQ + NBQ
      KWEX = KWBE + NB
      KWPH = KWEX + NEX
      KWTH = KWPH + NPAR
      KWSP = KWTH + NPAR
      KWST = KWSP + NPAR
      KWPA = KWST + NPAR
      KWSD = KWPA + NPAR
      KWBQ2 = KWBQ + NB
C
C     TRANSFER PARAMETER VALUES FROM PAR TO PHI,THETA,SPHI,STHETA
C
      CALL G13AEZ(PAR,NPAR,MPQS,WA,IDWA,KWPH)
C
C     CHECK VALIDITY OF THE FOUR PARAMETER SETS
C
      CALL G13AEY(MPQS,WA,IDWA,KWPH,NPAR,WA(KWPA),EPS,MVC,IERR)
      IF (IERR.EQ.0) GO TO 60
C
C     ERROR IN AT LEAST ONE PARAMETER SET
C
      IERROR = 6
      GO TO 340
   60 KSCH = 1
      IF (NQD.LE.0) GO TO 120
      KSCH = 2
C
C     SHIFT VALUES UP EX TO MAKE ROOM FOR PRE-X S WHICH ARE
C     INITIALLY SET TO ZERO
C
      DO 80 I = 1, NX
         J = NX - I + 1
         K = J + NQD
         EX(K) = EX(J)
   80 CONTINUE
      DO 100 I = 1, NQD
         EX(I) = ZERO
  100 CONTINUE
C
C     SUBTRACT CONSTANT FROM DIFFERENCED VALUES ONLY
C
  120 DO 160 I = 1, LEX
         J = KWEX + I - 1
         IF (I.LE.NQD) GO TO 140
         WA(J) = EX(I) - C
         GO TO 160
  140    WA(J) = EX(I)
  160 CONTINUE
C
C     ZEROISE A,B,AX,BX
C
      DO 180 I = 1, NAQ
         WA(I) = ZERO
  180 CONTINUE
      DO 200 I = 1, NBQ
         J = KWBQ + I - 1
         WA(J) = ZERO
  200 CONTINUE
C
C     IF KSCH=1 FORM A,B  IF KSCH=2 FORM AX,BX AS WELL
C
      IF (KSCH.EQ.1) GO TO 220
      WA(1) = U
      CALL G13AEU(2,WA(1),AL,WA(LEX+1),LEX,WA(KWBQ),WA(KWBE),WA(KWBQ2)
     *            ,NB,WA(KWPH),WA(KWTH),WA(KWSP),WA(KWST)
     *            ,NPAR,NP,NQ,NPS,NQS,NS,NPD)
      WA(1) = ZERO
  220 CALL G13AEU(1,WA(KWEX),AL,WA(1),LEX,WA(KWBQ2),WA(KWBE),WA(KWBQ)
     *            ,NB,WA(KWPH),WA(KWTH),WA(KWSP),WA(KWST)
     *            ,NPAR,NP,NQ,NPS,NQS,NS,NPD)
      IZ = NGHP
C
C     DERIVE S,G AND H
C
      CALL G13AEV(NP,NQ,NPS,NQS,NS,WA(1),NAQ,WA(KWBQ)
     *            ,NBQ,KSCH,AL,LEX,NB,S,G,H,IZ,NGH)
      IF (KSCH.EQ.2) GO TO 240
      KQ = NAQ + NB + 1
C
C     DERIVE FINAL SET OF AL VALUES
C
      CALL G13AEU(0,WA(KWEX),AL,WA(1),LEX,WA(KWBQ),WA(KWBE),WA(KQ)
     *            ,NB,WA(KWPH),WA(KWTH),WA(KWSP),WA(KWST)
     *            ,NPAR,NP,NQ,NPS,NQS,NS,NPD)
      GO TO 300
  240 IFAILQ = 1
C
C     IF OPTION IS 2, SOLVE THE EQUATIONS DEFINED BY H AND G, AND
C     PUT RESULTS IN WA(KWSD)
C
      CALL F04ASF(H,NGHP,G,NQD,WA(KWSD),AL,EXR,IFAILQ)
      IF (IFAILQ.EQ.0) GO TO 260
      IERROR = 5
      GO TO 340
C
C     OBTAIN ESTIMATES OF THE PRE-X S
C
  260 DO 280 I = 1, NQD
         J = KWSD + I - 1
         EX(I) = EX(I) + WA(J)
  280 CONTINUE
      KSCH = 1
      GO TO 120
C
C     RESIDUALS HELD IN AQ TRANSFERRED TO EXR
C
  300 DO 320 I = 1, LEX
         EXR(I) = WA(I)
  320 CONTINUE
C
C     DERIVE STATE SET FROM VALUES HELD IN EX, AL AND EXR
C
      CALL G13AET(EX,AL,EXR,NEX,NP,ND,NQ,NPS,NDS,NQS,NS,ST,NST)
  340 RETURN
      END
