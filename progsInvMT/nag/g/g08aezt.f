      SUBROUTINE G08AEZ(A,R,N,KI,XF)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     PURPOSE
C     G08AEZ RANKS A VECTOR OF VALUES AND CALCULATES
C     CORRECTION FACTOR DUE TO TIES
C
C     USAGE
C     CALL G08AEZ(A,R,N,KI,XF)
C
C     DESCRIPTION OF PARAMETERS
C     A - INPUT VECTOR OF N VALUES
C     R - OUTPUT VECTOR OF LENGTH N. SMALLEST VALUE IS RANKED 1,
C         LARGEST IS RANKED N. TIES ARE ASSIGNED AVERAGE OF TIED
C         RANKS
C     N - NUMBER OF VALUES
C     KI - INPUT CODE FOR CALCULATION OF CORRECTION FACTOR
C          1   SOLVE XF=SUMME((GLEICH**3-GLEICH)/12.0)
C          2   SOLVE XF=SUMME (GLEICH*(GLEICH-1.0)/2.0)
C          WHERE GLEICH IS THE NUMBER OF OBSERVATIONS TIED
C          FOR A GIVEN RANK
C     XF - CORRECTION FACTOR (OUTPUT)
C
C     REMARKS
C     NONE
C
C     SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C     NONE
C
C     METHOD
C     VECTOR IS SEARCHED FOR SUCCESSIVELY LARGER ELEMENTS. IF TIES
C     OCCUR, THEY ARE LOCATED AND THEIR RANK VALUE COMPUTED.
C     FOR EXAMPLE, IF 2 VALUES ARE TIED FOR SIXTH RANK, THEY ARE
C     ASSIGNED A RANK OF 6.5 (=(6+7)/2)
C     AND CORRECTION FACTOR 1 OR 2 IS SUMMED.
C
C     -------------------------------------------------------------
C
C     INITIALISATION
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  XF
      INTEGER           KI, N
C     .. Array Arguments ..
      DOUBLE PRECISION  A(N), R(N)
C     .. Local Scalars ..
      DOUBLE PRECISION  GLEICH, P, X, XKLEIN
      INTEGER           I, J
C     .. Executable Statements ..
      DO 20 I = 1, N
         R(I) = 0.0D0
   20 CONTINUE
      XF = 0.0D0
C
C     FIND RANK OF DATA
C
      DO 240 I = 1, N
C
C        TEST WHETHER DATA POINT IS ALREADY RANKED
C
         IF (R(I)) 40, 40, 240
C
C        DATA POINT TO BE RANKED
C
   40    XKLEIN = 0.0D0
         GLEICH = 0.0D0
         X = A(I)
         DO 100 J = 1, N
            IF (A(J)-X) 60, 80, 100
C
C           COUNT NUMBER OF DATA POINTS WHICH ARE SMALLER
C
   60       XKLEIN = XKLEIN + 1.0D0
            GO TO 100
C
C           COUNT NUMBER OF DATA POINTS WHICH ARE EQUAL
C           MARK THESE BY SETTING THEIR RANKS TO -1.
C
   80       GLEICH = GLEICH + 1.0D0
            R(J) = -1.0D0
  100    CONTINUE
C
C        TEST FOR TIE
C
         IF (GLEICH-1.0D0) 120, 120, 140
C
C        STORE RANK OF UNTIED DATA POINTS
C
  120    R(I) = XKLEIN + 1.0D0
         GO TO 240
C
C        STORE RANKS OF TIED DATA POINTS
C
  140    P = XKLEIN + GLEICH*(GLEICH+1.0D0)/(GLEICH+GLEICH)
         DO 180 J = I, N
            IF (R(J)+1.0D0) 180, 160, 180
  160       R(J) = P
  180    CONTINUE
         IF (KI-1) 240, 200, 220
  200    CONTINUE
         XF = XF + (GLEICH**3-GLEICH)/12.0D0
         GO TO 240
  220    CONTINUE
         XF = XF + GLEICH*(GLEICH-1.0D0)/2.0D0
  240 CONTINUE
      RETURN
      END
