      SUBROUTINE G02CGY(NCASES,NVARS,NIND,XBAR,S,COEFF,ICOEFF,CONST,C,
     *                  IC)
C     MARK 4 RELEASE NAG COPYRIGHT 1974.
C     MARK 4.5 REVISED
C     MARK 8 REVISED. IER-229 (APR 1980)
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     NAG AUXILIARY ROUTINE G02CGY
C     WRITTEN  8.10.73 BY PAUL GRIFFITHS (OXFORD UNIVERSITY)
C
C     CALCULATES THE REGRESSION CONSTANT, ITS STANDARD ERROR, AND
C     T-STATISTIC.
C
C     USES NAG LIBRARY ROUTINES X02AKF, X02ALF
C
C     INPUT:
C     NCASES   NUMBER OF CASES USED IN THE REGRESSION
C     NVARS    TOTAL NUMBER OF VARIABLES IN REGRESSION --
C     DEPENDENT AND INDEPENDENT -- THE DEPENDENT
C     VARIABLE IS ASSUMED TO BE THE (NVARS)TH
C     VARIABLE
C     NIND     NUMBER OF INDEPENDENT VARIABLES IN REGRESSION --
C     EQUAL TO (NVARS-1)
C     XBAR     NVARS ELEMENT VECTOR CONTAINING MEANS OF
C     VARIABLES -- DEPENDENT VARIABLE IN ELEMENT NVARS
C     S        STANDARD ERROR OF REGRESSION ESTIMATE
C     (SQUARE ROOT OF THE MEAN SQUARE ABOUT REGRESSION)
C     COEFF    NIND*3 MATRIX CONTAINING THE REGRESSION
C     COEFFICIENTS IN THE FIRST COLUMN
C     ICOEFF   FIRST DIMENSION OF COEFF IN THE CALLING SEGMENT
C     C        NIND*NIND MATRIX WHERE
C     C(I,J)= RINV(I,J)*R(I,J)/SSP(I,J)
C     KNOWN AS THE MODIFIED INVERSE MATRIX
C     IC       FIRST DIMENSION OF C IN THE CALLING SEGMENT
C
C     OUTPUT:
C     CONST    3 ELEMENT VECTOR CONTAINING THE FOLLOWING
C     INFORMATION:
C     CONST(1)   THE REGRESSION CONSTANT
C     CONST(2)   STANDARD ERROR OF REGRESSION CONSTANT
C     CONST(3)   T-STATISTIC FOR REGRESSION CONSTANT
C
C     .. Scalar Arguments ..
      DOUBLE PRECISION  S
      INTEGER           IC, ICOEFF, NCASES, NIND, NVARS
C     .. Array Arguments ..
      DOUBLE PRECISION  C(IC,NIND), COEFF(ICOEFF,3), CONST(3),
     *                  XBAR(NVARS)
C     .. Local Scalars ..
      DOUBLE PRECISION  A, B, BIGNUM, D, SMALL
      INTEGER           I, J
C     .. External Functions ..
      DOUBLE PRECISION  X02AKF, X02ALF
      EXTERNAL          X02AKF, X02ALF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, SQRT, DBLE
C     .. Executable Statements ..
      BIGNUM = X02ALF()
      SMALL = X02AKF()
      A = XBAR(NVARS)
      B = 1.0D0/DBLE(NCASES)
      DO 40 J = 1, NIND
         D = 0.0D0
         DO 20 I = 1, NIND
            D = D + XBAR(I)*C(I,J)
   20    CONTINUE
         A = A - XBAR(J)*COEFF(J,1)
         B = B + D*XBAR(J)
   40 CONTINUE
      CONST(1) = A
      CONST(2) = 0.0D0
      CONST(3) = BIGNUM
      B = B*S*S
      IF (B.LE.0.0D0) GO TO 80
      B = SQRT(B)
      CONST(2) = B
      IF (B.LE.1.0D0) GO TO 60
      CONST(3) = 0.0D0
      IF (ABS(A).GE.B*SMALL) CONST(3) = A/B
      GO TO 80
   60 CONST(3) = BIGNUM
      IF (ABS(A).LT.B*BIGNUM) CONST(3) = A/B
   80 RETURN
      END
