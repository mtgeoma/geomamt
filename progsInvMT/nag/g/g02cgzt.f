      SUBROUTINE G02CGZ(NTOT,NVARS,NIND,SSP,ISSP,R,IR,RESULT,COEFF,
     *                  ICOEFF,RINV,IRINV,C,IC)
C     MARK 4 RELEASE NAG COPYRIGHT 1974.
C     MARK 4.5 REVISED  LER-68A
C     MARK 5C REVISED
C     MARK 8 REVISED. IER-229 (APR 1980).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     NAG AUXILIARY ROUTINE G02CGZ
C     WRITTEN  7.10.73 BY PAUL GRIFFITHS (OXFORD UNIVERSITY)
C
C     DOES ALL THE WORK FOR NAG LIBRARY ROUTINES G02CGF-G02CHF.
C
C     USES NAG LIBRARY ROUTINES X02AKF, X02ALF
C
C     INPUT:
C     NTOT     TOTAL DEGREES OF FREEDOM -- EQUAL TO NCASES FOR
C     NO CONSTANT REGRESSION, AND (NCASES-1) OTHERWISE
C     NVARS    TOTAL NUMBER OF VARIABLES IN REGRESSION --
C     DEPENDENT AND INDEPENDENT -- THE DEPENDENT
C     VARIABLE IS ASSUMED TO BE THE (NVARS)TH
C     VARIABLE
C     NIND     NUMBER OF INDEPENDENT VARIABLES IN REGRESSION --
C     EQUAL TO (NVARS-1)
C     SSP      NVARS*NVARS MATRIX OF SUMS OF SQUARES AND
C     CROSS-PRODUCTS
C     ISSP     FIRST DIMENSION OF SSP IN THE CALLING SEGMENT
C     R        NVARS*NVARS MATRIX OF CORRELATIONS
C     IR       FIRST DIMENSION OF R IN THE CALLING SEGMENT
C     RINV     NIND*NIND MATRIX CONTAINING THE INVERSE OF THE
C     (NIND*NIND) CORRELATION MATRIX (A PARTITION OF
C     R) OF THE INDEPENDENT VARIABLES
C     IRINV    THE FIRST DIMENSION OF RINV IN THE CALLING SEGMEN
C
C     OUTPUT:
C     RESULT   13 ELEMENT VECTOR CONTAINING THE FOLLOWING
C     INFORMATION:
C     RESULT( 1)   SUM OF SQUARES DUE TO REGRESSION
C     RESULT( 2)   DEGREES OF FREEDOM DUE TO REGRESSION
C     RESULT( 3)   MEAN SQUARE DUE TO REGRESSION
C     RESULT( 4)   F-STATISTIC
C     RESULT( 5)   SUM OF SQUARES ABOUT REGRESSION
C     RESULT( 6)   DEGREES OF FREEDOM ABOUT REGRESSION
C     RESULT( 7)   MEAN SQUARE ABOUT REGRESSION
C     RESULT( 8)   TOTAL SUM OF SQUARES
C     RESULT( 9)   TOTAL DEGREES OF FREEDOM
C     RESULT(10)   STANDARD ERROR OF THE ESTIMATE
C     RESULT(11)   COEFFICIENT OF MULTIPLE CORRELATION
C     (R)
C     RESULT(12)   COEFFICIENT OF MULTIPLE
C     DETERMINATION (R**2)
C     RESULT(13)   CORRECTED COEFFICIENT OF MULTIPLE
C     DETERMINATION
C     COEFF    NIND*3 MATRIX CONTAINING THE FOLLOWING
C     INFORMATION:
C     COEFF(I,1)   REGRESSION COEFFICIENT OF VARIABLE I
C     COEFF(I,2)   STANDARD ERROR OF REGRESSION
C     COEFFICIENT OF VARIABLE I
C     COEFF(I,3)   T-STATISTIC FOR REGRESSION
C     COEFFICIENT OF VARIABLE I
C     ICOEFF   FIRST DIMENSION OF COEFF IN THE CALLING SEGMENT
C     C        NIND*NIND MATRIX WHERE
C     C(I,J)= RINV(I,J)*R(I,J)/SSP(I,J)
C     KNOWN AS THE MODIFIED INVERSE MATRIX
C     IC       FIRST DIMENSION OF C IN THE CALLING SEGMENT
C
C     .. Scalar Arguments ..
      INTEGER           IC, ICOEFF, IR, IRINV, ISSP, NIND, NTOT, NVARS
C     .. Array Arguments ..
      DOUBLE PRECISION  C(IC,NIND), COEFF(ICOEFF,3), R(IR,NVARS),
     *                  RESULT(13), RINV(IRINV,NIND), SSP(ISSP,NVARS)
C     .. Local Scalars ..
      DOUBLE PRECISION  A, B, BIGNUM, D, FIND, FTOT, SMALL
      INTEGER           I, J
C     .. External Functions ..
      DOUBLE PRECISION  X02AKF, X02ALF
      EXTERNAL          X02AKF, X02ALF
C     .. Intrinsic Functions ..
      INTRINSIC         ABS, SQRT, DBLE
C     .. Executable Statements ..
      BIGNUM = X02ALF()
      SMALL = X02AKF()
      FTOT = DBLE(NTOT)
      FIND = DBLE(NIND)
C
C     SET UP C SO THAT ITS ELEMENTS ARE THE CORRESPONDING ELEMENTS
C     OF  (RINV)*(R)/(SSP)
C
      DO 40 J = 1, NIND
         B = SSP(J,J)
         DO 20 I = 1, NIND
            C(I,J) = RINV(I,J)/SQRT(B*SSP(I,I))
   20    CONTINUE
   40 CONTINUE
C
C     CALCULATE REGRESSION COEFFICIENTS
C
      DO 80 I = 1, NIND
         B = 0.0D0
         DO 60 J = 1, NIND
            B = B + C(I,J)*SSP(J,NVARS)
   60    CONTINUE
         COEFF(I,1) = B
   80 CONTINUE
C
C     CALCULATE ANALYSIS OF REGRESSION TABLE AND ODDS AND ENDS FOR
C     ARRAY RESULT
C
      B = 0.0D0
      DO 100 I = 1, NIND
         B = B + COEFF(I,1)*SSP(I,NVARS)
  100 CONTINUE
      A = SSP(NVARS,NVARS)
      RESULT(1) = B
      RESULT(8) = A
      RESULT(5) = A - B
      RESULT(2) = FIND
      RESULT(6) = FTOT - FIND
      RESULT(9) = FTOT
      RESULT(3) = B/FIND
      RESULT(7) = RESULT(5)/RESULT(6)
      RESULT(4) = BIGNUM
      RESULT(10) = 0.0D0
      RESULT(11) = 0.0D0
      RESULT(12) = 0.0D0
      RESULT(13) = 0.0D0
      D = RESULT(7)
      IF (D.LE.0.0D0) GO TO 140
      RESULT(10) = SQRT(D)
      IF (D.LE.1.0D0) GO TO 120
      RESULT(4) = 0.0D0
      IF (ABS(RESULT(3)).GE.D*SMALL) RESULT(4) = RESULT(3)/D
      GO TO 140
  120 RESULT(4) = BIGNUM
      IF (ABS(RESULT(3)).LT.D*BIGNUM) RESULT(4) = RESULT(3)/D
  140 IF (A.LE.0.0D0) GO TO 160
      RESULT(13) = 1.0D0 - D*FTOT/A
      A = B/A
      IF (A.GT.0.0D0) RESULT(11) = SQRT(A)
      RESULT(12) = A
C
C     CALCULATE STANDARD ERRORS AND T-STATISTICS FOR REGRESSION
C     COEFFICIENTS
C
  160 A = RESULT(10)
      DO 200 I = 1, NIND
         B = 0.0D0
         IF (C(I,I).GT.0.0D0) B = A*SQRT(C(I,I))
         COEFF(I,2) = B
         IF (B.LE.1.0D0) GO TO 180
         COEFF(I,3) = 0.0D0
         IF (ABS(COEFF(I,1)).GE.B*SMALL) COEFF(I,3) = COEFF(I,1)/B
         GO TO 200
  180    COEFF(I,3) = BIGNUM
         IF (ABS(COEFF(I,1)).LT.B*BIGNUM) COEFF(I,3) = COEFF(I,1)/B
  200 CONTINUE
      RETURN
      END
