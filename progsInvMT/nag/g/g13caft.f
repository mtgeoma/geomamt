      SUBROUTINE G13CAF(NX,MTX,PX,IW,MW,IC,NC,C,KC,L,LG,NXG,XG,NG,STATS,
     *                  IFAIL)
C     MARK 10 RELEASE. NAG COPYRIGHT 1982.
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C
C     G13CAF CALCULATES THE SMOOTHED SAMPLE SPECTRUM OF A
C     UNIVARIATE TIME SERIES USING ONE OF FOUR LAG WINDOWS
C     - RECTANGULAR, BARTLETT, TUKEY OR PARZEN WINDOW.
C
C     CONTRIBUTORS - G. TUNNICLIFFE WILSON, M. HURLEY (LANC. UNIV.)
C     VALIDATOR    - T. LAMBERT ( NAG CENTRAL OFFICE )
C
C     USES NAG LIBRARY ROUTINES G13CAZ, P01AAF
C
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='G13CAF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  PX
      INTEGER           IC, IFAIL, IW, KC, L, LG, MTX, MW, NC, NG, NX,
     *                  NXG
C     .. Array Arguments ..
      DOUBLE PRECISION  C(NC), STATS(4), XG(NXG)
C     .. Local Scalars ..
      INTEGER           IERROR
C     .. Local Arrays ..
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      INTEGER           P01ABF
      EXTERNAL          P01ABF
C     .. External Subroutines ..
      EXTERNAL          G13CAZ
C     .. Intrinsic Functions ..
      INTRINSIC         MAX
C     .. Executable Statements ..
      IERROR = 1
C     CHECK THE DATA SET SIZE NON ZERO
      IF (NX.LT.1) GO TO 60
      IF (IC.NE.0) GO TO 20
C     CHECK THE MEAN-TREND CORRECTION INDICATOR
      IF (MTX.LT.0 .OR. MTX.GT.2) GO TO 60
C     CHECK DATA/SPECTRUM ARRAY SIZE
      IF (NXG.LT.MAX(KC,L)) GO TO 60
      GO TO 40
   20 IF (NXG.LT.L) GO TO 60
C     CHECK THE DATA WINDOW PROPORTION
   40 IF (PX.LT.0.0D0 .OR. PX.GT.1.0D0) GO TO 60
C     CHECK THE LAG WINDOW INDICATOR
      IF (IW.LT.1 .OR. IW.GT.4) GO TO 60
C     CHECK LAG WINDOW PARAMETER
      IF (MW.LT.1 .OR. MW.GT.NX) GO TO 60
C     CHECK NUMBER OF COVARIANCES
      IF (NC.LT.MW .OR. NC.GT.NX) GO TO 60
C     CHECK ORDER OF COVARIANCE FFT
      IERROR = 2
      IF (IC.EQ.0 .AND. KC.LT.(NX+NC)) GO TO 60
C     CHECK ORDER OF SPECTRUM FFT
      IERROR = 3
      IF (L.LT.(2*MW-1)) GO TO 60
C
C     IF THIS POINT IS REACHED USER SUPPLIED
C     PARAMETERS HAVE PASSED INITIAL CHECKS
      IERROR = 0
      CALL G13CAZ(NX,MTX,PX,IW,MW,IC,NC,C,KC,L,LG,NXG,XG,NG,STATS,
     *            IERROR)
      IF (IERROR.NE.0) GO TO 60
C
C     SUCCESSFUL EXIT
      IFAIL = 0
      RETURN
C
C     COME HERE IF ERRORS
   60 IFAIL = P01ABF(IFAIL,IERROR,SRNAME,0,P01REC)
      RETURN
      END
