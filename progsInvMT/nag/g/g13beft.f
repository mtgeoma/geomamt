      SUBROUTINE G13BEF(MR,NSER,MT,PARA,NPARA,KFC,NXXY,XXY,IXXY,KEF,NIT,
     *                  KZSP,ZSP,ITC,SD,CM,ICM,S,D,NDF,KZEF,RES,STTF,
     *                  ISTTF,NSTTF,WA,IWA,MWA,IMWA,KPRIV,IFAIL)
C     MARK 11 RELEASE. NAG COPYRIGHT 1983.
C     MARK 11A REVISED. IER-452 (JUN 1984).
C     MARK 11.5(F77) REVISED. (SEPT 1985.)
C     MARK 12 REVISED. IER-521 (AUG 1986).
C     MARK 13 REVISED. USE OF MARK 12 X02 FUNCTIONS (APR 1988).
C
C     SUBROUTINE G13BEF ACCEPTS AN OUTPUT (Y) SERIES AND
C     ANY NUMBER OF INPUT (X) SERIES, THE ORDERS OF ALL
C     THESE SERIES BEING SPECIFIED. THE Y SERIES IS CHARACTERISED
C     BY AN ARIMA MODEL SPECIFICATION AND A SET OF INITIAL PARAMETER
C     ESTIMATES WHICH MAY BE ZERO.
C     THE ROUTINE CARRIES OUT AN ITERATIVE OPTIMISATION
C     PROCEDURE TO OBTAIN A SET OF FINAL ESTIMATES WHICH
C     SATISFY A SPECIFIED CONVERGENCE CRITERION OR WHICH
C     ARE THE RESULTS OF A SPECIFIED NUMBER OF ITERATIONS.
C
C     .. Parameters ..
      CHARACTER*6       SRNAME
      PARAMETER         (SRNAME='G13BEF')
C     .. Scalar Arguments ..
      DOUBLE PRECISION  D, S
      INTEGER           ICM, IFAIL, IMWA, ISTTF, ITC, IWA, IXXY, KEF,
     *                  KFC, KPRIV, KZEF, KZSP, NDF, NIT, NPARA, NSER,
     *                  NSTTF, NXXY
C     .. Array Arguments ..
      DOUBLE PRECISION  CM(ICM,NPARA), PARA(NPARA), RES(NXXY),
     *                  SD(NPARA), STTF(ISTTF), WA(IWA), XXY(IXXY,NSER),
     *                  ZSP(4)
      INTEGER           MR(7), MT(4,NSER), MWA(IMWA)
C     .. Local Scalars ..
      DOUBLE PRECISION  C, U, ZERO
      INTEGER           I, IERR, IH, IPXS, IWAE, IWDS, J, K, L, LBETA,
     *                  LBETAC, LBF, LDELT, LGC, LHC, LLG, LLH, LPXS,
     *                  LWAE, LWDS, NBF, NBFQ, NBSS, NBV, ND, NDD, NDS,
     *                  NGH, NGW, NNB, NNP, NNQ, NNR, NP, NPAR, NPD,
     *                  NPE, NPS, NPX, NPXV, NQ, NQD, NQS, NS, NSW, NWD,
     *                  NWDV, NXS
C     .. Local Arrays ..
      INTEGER           MPQS(4)
      CHARACTER*1       P01REC(1)
C     .. External Functions ..
      DOUBLE PRECISION  X02AJF
      INTEGER           P01ABF
      EXTERNAL          X02AJF, P01ABF
C     .. External Subroutines ..
      EXTERNAL          G13AJY, G13BEX, G13BEZ, G13BFX, G13BFY
C     .. Intrinsic Functions ..
      INTRINSIC         MAX
C     .. Data statements ..
      DATA              ZERO/0.0D0/, U/1.0D0/
C     .. Executable Statements ..
C
C     ZEROISE THE WORKING ARRAYS
C
      DO 20 I = 1, IWA
         WA(I) = ZERO
   20 CONTINUE
      DO 40 I = 1, IMWA
         MWA(I) = 0
   40 CONTINUE
      IERR = 1
C
C     NPE HOLDS THE NUMBER OF ARIMA, OMEGA, DELTA, AND C PARAMETERS
C     BEING ESTIMATED
C
      NPE = NPARA + KFC - 1
C
C     TEST FOR TYPE 1 ERROR
C
      IF (KFC.LT.0 .OR. KFC.GT.1) GO TO 260
      IF (IXXY.LT.NXXY .OR. ICM.LT.NPARA) GO TO 260
      IF (KEF.LT.1 .OR. KEF.GT.3) GO TO 260
      IF (NIT.LT.0) GO TO 260
C
C     TEST FOR NO PARAMETERE IN THE MODEL
C
      IF (NSER.LT.1) GO TO 260
      IF (MR(1).GT.0 .OR. MR(3).GT.0 .OR. MR(4).GT.0 .OR. MR(6).GT.0)
     *   GO TO 50
      IF (KFC.EQ.1) GO TO 50
      IF (NSER.EQ.1) GO TO 260
   50 IERR = 2
C
C     DERIVE MISCELLANEOUS INTEGER VARIABLES ASSOCIATED WITH
C     THE ORDERS OF THE Y SERIES
C
      CALL G13AJY(MR,NP,ND,NQ,NPS,NDS,NQS,NS,NPD,NDD,NQD,MPQS,NPAR)
      NXS = NSER - 1
      IPXS = 1
      IWDS = 1
C
C     NSW, NWDV AND NPXV WILL HOLD THE TOTAL NUMBERS
C     OF SIMPLE OMEGAS, OF ALL OMEGAS AND DELTAS, AND
C     OF PRE-XS
C
      NSW = 0
      NWDV = 0
      NPXV = 0
      IF (NXS.LE.0) GO TO 120
C
C     PROCESS EACH X SERIES IN TURN
C
      DO 100 I = 1, NXS
C
C        DRIVE MISCELLANEOUS INTEGER VARIABLES ASSOCIATED
C        WITH THE ORDERS OF EACH X SERIES
C
         IF (MT(4,I).LT.1 .OR. MT(4,I).GT.3) GO TO 260
         CALL G13BEX(MT,I,NSER,NNB,NNP,NNQ,NNR,NWD,NGW,NPX)
         IF (NNR.NE.1) GO TO 60
         NSW = NSW + 1
   60    NWDV = NWDV + NWD
         NPXV = NPXV + NPX
         IF (NPX.LE.IPXS) GO TO 80
         IPXS = NPX
   80    IF (NWD.LE.IWDS) GO TO 100
         IWDS = NWD
  100 CONTINUE
C
C     TEST FOR TYPE 2 ERROR
C
  120 IF (NPARA.NE.(NPAR+NWDV+1)) GO TO 260
C
C     TEST FOR TYPE 4 ERROR
C
      IERR = 4
      IF (KZSP.EQ.1) GO TO 140
C
C     USE THE DEFAULT SEARCH PROCEDURE VALUES IF KZSP=0
C
      ZSP(1) = 0.01D0
      ZSP(2) = 10.0D0
      ZSP(3) = 1000.0D0
      ZSP(4) = MAX(100.0D0*X02AJF(),1.0D-07)
      GO TO 160
  140 IF (ZSP(1).LE.ZERO) GO TO 260
      IF (ZSP(2).LE.U) GO TO 260
      IF (ZSP(3).LT.U) GO TO 260
      IF (ZSP(4).GE.U .OR. ZSP(4).LT.ZERO) GO TO 260
C
C     CALCULATE NBV AND NBSS VALUES WHICH ARE USED TO DEFINE
C     THE LENGTHS OF THE A(T) AND B(T) SETS
C
  160 CALL G13BEZ(NXS,MT,NP,NQ,NPS,NQS,NS,NDD,NQD,KEF,NBSS,NBV,NBF,NSER)
      NBFQ = MAX(NBF,1)
C
C     CALCULATE IH AND NGH WHICH DEFINE THE SIZE OF
C     THE DERIVATIVE VECTOR G AND MATRIX H, AND NDF
C     THE NUMBER OF DEGREES OF FREEDOM
C
      IH = NPARA + NBF + KFC + NPXV
      NGH = IH - 1
      NDF = NXXY - NDD - NGH + NBF
C
C     DEFINE THE START POINTS OF THE COMPONENT
C     ARRAYS OF WA
C
      LWDS = 1
      LBF = LWDS + IWDS*NSER
      LPXS = LBF + NBFQ
      LBETA = LPXS + IPXS*NSER
      LBETAC = LBETA + NGH
      LLG = LBETAC + NGH
      LGC = LLG + NGH
      LLH = LGC + NGH
      LHC = LLH + IH*NGH
      LDELT = LHC + IH*NGH
      LWAE = LDELT + NGH
      IWAE = IWA - LWAE + 1
      MWA(1) = LWAE - 1
C
C     TEST FOR TYPE 5 ERROR
C
      IERR = 5
      IF (IWAE.LE.0) GO TO 260
      IF (NXS.LE.0) GO TO 220
      L = NPAR
      DO 200 I = 1, NXS
         CALL G13BEX(MT,I,NSER,NNB,NNP,NNQ,NNR,NWD,NGW,NPX)
         IERR = 2
         IF (NWD.LE.0) GO TO 260
C
C        STORE ALL OMEGAS AND DELTAS IN WA(1)
C
         DO 180 J = 1, NWD
            K = IWDS*(I-1) + J
            L = L + 1
            WA(K) = PARA(L)
  180    CONTINUE
  200 CONTINUE
C
C     C HOLDS THE VALUE OF THE CONSTANT
C
  220 C = PARA(NPARA)
C
C     TEST FOR TYPE 7 ERROR
C
      IERR = 7
      I = 30 + 16*NSER + 7*IH + 3*NPE
      IF (IMWA.GE.I) GO TO 240
      MWA(1) = I
      IF (KPRIV.EQ.0 .AND. IFAIL.NE.0) GO TO 260
      CALL G13BFX(ITC,S,D,PARA,NPARA,IMWA,MWA,MWA,7,I)
      GO TO 260
C
C     CALL THE MAIN CALCULATION AUXILIARY
C
  240 IERR = IFAIL
      ITC = 0
      CALL G13BFY(MR,NPARA,PARA,NPE,NXXY,XXY,IXXY,NSER,MT,KEF,NIT,ZSP,
     *            ITC,SD,CM,ICM,S,D,KZEF,RES,MWA,IMWA,WA(LBETA)
     *            ,WA(LBETAC),NGH,WA(LLG),WA(LGC),IH,WA(LLH),WA(LHC)
     *            ,WA(LDELT),IWDS,WA(LWDS),NBFQ,WA(LBF),IPXS,WA(LPXS)
     *            ,WA(LWAE),IWAE,C,KPRIV,STTF,ISTTF,NSTTF,IERR)
  260 IFAIL = P01ABF(IFAIL,IERR,SRNAME,0,P01REC)
      RETURN
      END
